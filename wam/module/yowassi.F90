#define __FILENAME__ "yowassi.F90"

MODULE YOWASSI
    !! This module provides interfaces for callback functions that need to
    !! be registered for Data assimilation routines
    !! The idea is that data assimilation can be added via an external library
    !! which provides implementations for the interfaces.
    !! A setup routine needs to point the HANDLERs to the correct implementation

IMPLICIT NONE
PRIVATE

PUBLIC :: WAMASSI, WAM_ODB_OPEN, WAM_ODB_CLOSE
PUBLIC :: WAMASSI_HANDLER, WAM_ODB_OPEN_HANDLER, WAM_ODB_CLOSE_HANDLER

#define WAMASSI WAMASSI_INTERFACE
#include "wamassi.intfb.h"
#undef WAMASSI

INTERFACE
  SUBROUTINE WAM_ODB_OPEN_INTERFACE()
  END SUBROUTINE
  SUBROUTINE WAM_ODB_CLOSE_INTERFACE()
  END SUBROUTINE
END INTERFACE

PROCEDURE(WAMASSI_INTERFACE),       POINTER :: WAMASSI_HANDLER       => NULL()
PROCEDURE(WAM_ODB_OPEN_INTERFACE),  POINTER :: WAM_ODB_OPEN_HANDLER  => NULL()
PROCEDURE(WAM_ODB_CLOSE_INTERFACE), POINTER :: WAM_ODB_CLOSE_HANDLER => NULL()

CONTAINS

SUBROUTINE WAMASSI(LDSTOP, LDWRRE, BLK2GLO, WVENVI,   &
    &              WVPRPT, FF_NOW, INTFLDS,  &
    &              WAM2NEMO, NEMO2WAM, FL1)
    USE PARKIND_WAVE, ONLY : JWRB
    USE YOWDRVTYPE  , ONLY : WVGRIDGLO, ENVIRONMENT, FREQUENCY, FORCING_FIELDS,  &
    &                        INTGT_PARAM_FIELDS, WAVE2OCEAN, OCEAN2WAVE
    USE YOWGRID     , ONLY : NPROMA_WAM, NCHNK
    USE YOWPARAM    , ONLY : NIBLO ,NANG ,NFRE
    USE YOWABORT    , ONLY : WAM_ABORT

! ----------------------------------------------------------------------
    IMPLICIT NONE

    LOGICAL,                  INTENT(IN)    :: LDSTOP
    LOGICAL,                  INTENT(IN)    :: LDWRRE
    TYPE(WVGRIDGLO),          INTENT(IN)    :: BLK2GLO(NIBLO)
    TYPE(ENVIRONMENT),        INTENT(INOUT) :: WVENVI(NPROMA_WAM, NCHNK)
    TYPE(FREQUENCY),          INTENT(IN)    :: WVPRPT(NPROMA_WAM, NFRE, NCHNK)
    TYPE(FORCING_FIELDS),     INTENT(INOUT) :: FF_NOW(NPROMA_WAM, NCHNK)
    TYPE(INTGT_PARAM_FIELDS), INTENT(INOUT) :: INTFLDS(NPROMA_WAM, NCHNK)
    TYPE(WAVE2OCEAN),         INTENT(INOUT) :: WAM2NEMO(NPROMA_WAM, NCHNK)
    TYPE(OCEAN2WAVE),         INTENT(IN)    :: NEMO2WAM(NPROMA_WAM, NCHNK)
    REAL(KIND=JWRB),          INTENT(INOUT) :: FL1(NPROMA_WAM, NANG, NFRE, NCHNK)

    IF( .NOT. ASSOCIATED(WAMASSI_HANDLER) ) THEN
        CALL WAM_ABORT("WAMASSI_HANDLER is not associated. Make sure WAM_SETUP_ASSI was called.", &
            & __FILENAME__, __LINE__)
    ELSE
        CALL WAMASSI_HANDLER(LDSTOP, LDWRRE, BLK2GLO, WVENVI,   &
            &                WVPRPT, FF_NOW, INTFLDS,  &
            &                WAM2NEMO, NEMO2WAM, FL1)
    ENDIF
END SUBROUTINE

SUBROUTINE WAM_ODB_OPEN
    USE YOWABORT    , ONLY : WAM_ABORT
    IF( .NOT. ASSOCIATED(WAM_ODB_OPEN_HANDLER) ) THEN
        CALL WAM_ABORT("WAM_ODB_OPEN_HANDLER is not associated. Make sure WAM_SETUP_ASSI was called.", &
            & __FILENAME__, __LINE__)
    ELSE
        CALL WAM_ODB_OPEN_HANDLER()
    ENDIF
END SUBROUTINE

SUBROUTINE WAM_ODB_CLOSE
    USE YOWABORT    , ONLY : WAM_ABORT
    IF( .NOT. ASSOCIATED(WAM_ODB_CLOSE_HANDLER) ) THEN
        CALL WAM_ABORT("WAM_ODB_CLOSE_HANDLER is not associated. Make sure WAM_SETUP_ASSI was called.", &
            & __FILENAME__, __LINE__)
    ELSE
        CALL WAM_ODB_CLOSE_HANDLER()
    ENDIF
END SUBROUTINE

end module
