      SUBROUTINE MPBCASTGRDFLD(ISEND,ITAG,NXF,NYF,FIELD) 

!****  *MPBCASTSCFLD* - BROADCAST 2-D DATA FIELD FROM 1 PE TO ALL OTHERS

!     J. BIDLOT    ECMWF   SEPTEMBER 1997

!     PURPOSE.
!     --------

!     BROADCAST DATA CONTAINED IN 2-D ARRAY FIELD FROM PE ISEND 
!     TO ALL OTHER PE's.

!*    INTERFACE.
!     ----------

!     CALL *MPBCASTGRDFLD*(ITAG,NSTART,NEND,FIELD) 

!     *ISEND*     RANK OF THE PROCESS FROM WHICH THE FIELD IS
!                 BROADCASTED
!     *ITAG*      TAG ASSOCIATED WITH A PARTICULAR CALL TO SUBROUTINE
!     *NXF*       FIRST DIMENSION OF FIELD
!     *NYF*       SECOND DIMENSION OF FIELD
!     *FIELD*     INPUT/OUTPUT ARRAY CONTAINING THE FIELD

!     METHOD.
!     -------
!     MPL BROADCAST OF ARRAY FIELD FROM ISEND
!     AND RECEIVE OF THE CONTRIBUTION ON THE OTHER PE's.

!     EXTERNALS.
!     ----------
!     MPL PACKAGE :
!         MPL_BROADCAST

!     REFERENCES.
!     -----------
!         NONE
! -------------------------------------------------------------------

      USE YOWMPP   , ONLY : IRANK    ,NPROC
      USE MPL_MODULE
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

!----------------------------------------------------------------------
      IMPLICIT NONE

      INTEGER :: ISEND,ITAG,NXF,NYF
      INTEGER :: MPLENGTH, I, J, KCOUNT

      REAL :: ZHOOK_HANDLE
      REAL, DIMENSION(NXF,NYF) :: FIELD
      REAL,ALLOCATABLE :: ZCOMBUF(:)

      IF (LHOOK) CALL DR_HOOK('MPBCASTGRDFLD',0,ZHOOK_HANDLE)

      MPLENGTH=NXF*NYF

      IF((ISEND.NE.0).AND.(NPROC.NE.1)) THEN

!     1.1 SEND TO ALL PROCESSORS OTHER THAN ISEND
!         ----------------------------------------
        ALLOCATE(ZCOMBUF(MPLENGTH))

        IF(IRANK.EQ.ISEND) THEN
          KCOUNT=0
          DO J=1,NYF
            DO I=1,NXF
              KCOUNT=KCOUNT+1
              ZCOMBUF(KCOUNT)=FIELD(I,J)
            ENDDO
          ENDDO
        ENDIF

        CALL GSTATS(620,0)
        CALL MPL_BROADCAST(ZCOMBUF(1:MPLENGTH),KROOT=ISEND,
     &   KTAG=ITAG,CDSTRING='MPBCASTGRDFLD:')
        CALL GSTATS(620,1)

        IF(IRANK.NE.ISEND) THEN
          KCOUNT=0
          DO J=1,NYF
            DO I=1,NXF
              KCOUNT=KCOUNT+1
              FIELD(I,J)=ZCOMBUF(KCOUNT)
            ENDDO
          ENDDO
        ENDIF
        DEALLOCATE(ZCOMBUF)

      ENDIF

      IF (LHOOK) CALL DR_HOOK('MPBCASTGRDFLD',1,ZHOOK_HANDLE)
      RETURN
      END SUBROUTINE MPBCASTGRDFLD
