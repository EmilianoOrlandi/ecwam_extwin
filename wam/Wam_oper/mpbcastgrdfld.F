      SUBROUTINE MPBCASTGRDFLD(ISEND,ITAG,NXF,NYF,FIELD) 

!****  *MPBCASTSCFLD* - BROADCAST 2-D DATA FIELD FROM 1 PE TO ALL OTHERS

!     J. BIDLOT    ECMWF   SEPTEMBER 1997

!     PURPOSE.
!     --------

!     BROADCAST DATA CONTAINED IN 2-D ARRAY FIELD FROM PE ISEND 
!     TO ALL OTHER PE's.

!*    INTERFACE.
!     ----------

!     CALL *MPGATHERSCFLD*(ITAG,NSTART,NEND,FIELD) 

!     *ISEND*     RANK OF THE PROCESS FROM WHICH THE FIELD IS
!                 BROADCASTED
!     *ITAG*      TAG ASSOCIATED WITH A PARTICULAR CALL TO SUBROUTINE
!     *NXF*       FIRST DIMENSION OF FIELD
!     *NYF*       SECOND DIMENSION OF FIELD
!     *FIELD*     INPUT/OUTPUT ARRAY CONTAINING THE FIELD

!     METHOD.
!     -------
!     MPE BROADCAST OF ARRAY FIELD FROM ISEND
!     AND RECEIVE OF THE CONTRIBUTION ON THE OTHER PE's.

!     EXTERNALS.
!     ----------
!     MPE PACKAGE :
!         MPE_BROADCAST
!         MPE_RECV
!         MPEI_ABORT 

!     REFERENCES.
!     -----------
!         NONE
! -------------------------------------------------------------------


      USE YOWMPP   , ONLY : IRANK    ,NPROC



!----------------------------------------------------------------------

      REAL,DIMENSION(NXF,NYF) :: FIELD

      REAL,ALLOCATABLE :: ZCOMBUF(:)
      MPLENGTH=NXF*NYF


      IF((ISEND.EQ.0).OR.(NPROC.EQ.1)) THEN
        RETURN
      ELSEIF(IRANK.EQ.ISEND) THEN
!     1.1 SEND TO ALL PROCESSORS OTHER THAN ISEND
!         ----------------------------------------
        ALLOCATE(ZCOMBUF(MPLENGTH))

        KCOUNT=0
        DO J=1,NYF
          DO I=1,NXF
            KCOUNT=KCOUNT+1
            ZCOMBUF(KCOUNT)=FIELD(I,J)
          ENDDO
        ENDDO

        CALL MPE_BROADCAST
     &   (ZCOMBUF,MPLENGTH,2,IRANK,ITAG,0,0,0,IERR)
        IF(IERR.LT.0) CALL MPEI_ABORT
     &   ('MPE_BROADCAST ERROR in MPBCASTGRDFLD' )

        DEALLOCATE(ZCOMBUF)

      ELSE

!     1.2 RECEIVE CONTRIBUTION TO THE FIELD FROM PE ISEND
!         ------------------------------------------------

        ALLOCATE(ZCOMBUF(MPLENGTH))

        CALL MPE_RECV(ZCOMBUF,MPLENGTH,2,ISEND,ITAG,0,0,0,
     &   KRCOUNT,KRFROM,KRTAG,IERR)
        IF(IERR.LT.0) CALL MPEI_ABORT
     &   ('MPE_RECV ERROR in MPBCASTGRDFLD' )
        IF(KRTAG.NE.ITAG) CALL MPEI_ABORT
     &   ('MPE_RECV ERROR in MPBCASTGRDFLD MISMATCHED TAGS' )

        KCOUNT=0
        DO J=1,NYF
          DO I=1,NXF
            KCOUNT=KCOUNT+1
            FIELD(I,J)=ZCOMBUF(KCOUNT)
          ENDDO
        ENDDO

        DEALLOCATE(ZCOMBUF)

      ENDIF

      RETURN
      END SUBROUTINE MPBCASTGRDFLD
