      SUBROUTINE SAVSTRESS(U10OLD,THWOLD,USOLD,TAUW,Z0OLD,NSTART,NEND,
     1                     LDSTOP,LDWRRE,IREST,CDTPRO,CDATEF)
C ----------------------------------------------------------------------
C     J. BIDLOT    ECMWF      MARCH 1997
C     J. BIDLOT    ECMWF      SEPTEMBER 1997 
C
C*    PURPOSE.
C     --------
C     WRITE STRESS/WIND FIELDS TO DISK. IN CASE OF MESSAGE PASSING,
C     DIFFERENT OUTPUT CAN BE GENERATE DEPENDING ON THE VALUE OF IREST
C
C**   INTERFACE.
C     ----------
C     *CALL* *SAVSTRESS(U10OLD,THWOLD,USOLD,TAUW,Z0OLD,NSTART,NEND,
C                       LDSTOP,LDWRRE,IREST,CDTPRO,CDATEF)
C     *U10OLD*    INTERMEDIATE STORAGE OF MODULUS OF WIND
C                 VELOCITY.
C     *THWOLD*    INTERMEDIATE STORAGE OF ANGLE (RADIANS) OF
C                 WIND VELOCITY.
C     *USOLD*     INTERMEDIATE STORAGE OF MODULUS OF FRICTION
C                 VELOCITY.
C     *Z0OLD*     INTERMEDIATE STORAGE OF ROUGHNESS LENGTH IN
C                 M.
C     *TAUW*      WAVE STRESS IN (M/S)**2
C     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
C     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN
C     *LDSTOP*    SET .TRUE. IF STOP SIGNAL RECEIVED.
C     *LDWRRE*    SET .TRUE. IF RESTART SIGNAL RECEIVED.
C     *IREST*     INTEGER   RESTART FILE SAVE OPTION.
C                           = 1  RESTART FILES ARE SAVED
C                           = 2  RESTART FILES ARE SAVED IN SPLIT MODE
C                           = 3  RESTART FILES ARE SAVED AND PREALLOCATION
C                                OF THE OUTPUT FILE WHICH IS SAVED TO A VFL
C                                FILE SYSTEM
C                           = 4  RESTART FILES ARE SAVED IN SPLIT MODE
C                                OF THE OUTPUT FILE WHICH IS SAVED TO A VFL
C                                FILE SYSTEM
C                           OTHERWISE RESTART FILES ARE NOT SAVED.
C     *CDTPRO*    CHAR*10   END DATE OF PROPAGATION.
C
C
C     METHOD.
C     -------
C     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
C     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED BY
C     GRSTNAME. DEPENDING ON THE IREST VALUE, THE SPACE ON DISK WILL BE
C     PREALLOCATED OR NOT FROM WITHIN THE MODEL RUN. THE OUTPUT CAN BE SAVED
C     IN ONE SINGLE FILE OR IN NPROC FILES IF SPLIT MODE WAS SELECTED ( IREST
C     =2 OR 4). IN NON SPLIT MODE (IREST = 1 OR 3), THE CONTRIBUTION FROM
C     ALL PE'S NEED TO BE GATHERED ON THE OUTPUT PE BY CALLING MPGATHERSCFLD.
C     IF LDSTOP OR LDWRRE ARE TRUE THEN THE OUTPUT WILL OCCUR AS IF IREST=1
C
C     EXTERNALS.
C     ----------
C     GRSTNAME
C     MPGATHERSCFLD
C     PREALLOC_FILE
C
C
C     REFERENCE.
C     ----------
C     NONE
C ----------------------------------------------------------------------
C
#include "param.h"
C
#include "comcout.h"
C
#include "comgrid.h"
C
#include "comtest.h"
C
#include "comtext.h"
C
#include "txttext.h"
C
#include "comunit.h"
C
#include "txtunit.h"
C
#include "comwind.h"
C
#include "commesspass.h"
C
#include "commpp.h"

      CHARACTER*80 FILENAME
      CHARACTER*10 CDTPRO,CDATEF
      LOGICAL LPBOPEN, LPBCLOSE
      LOGICAL LDSTOP, LDWRRE
C
C ----------------------------------------------------------------------
      INTEGER,DIMENSION(NPROC) :: NSTART, NEND
      REAL,DIMENSION(NINF:NSUP,NBLO) :: U10OLD,THWOLD,USOLD,TAUW,Z0OLD 
C ----------------------------------------------------------------------
C     define local allocatable arrays
      REAL,ALLOCATABLE,DIMENSION(:,:) :: RU10OLD,RTHWOLD,RUSOLD,
     1                                   RZ0OLD,RTAUW

      LPBOPEN = .TRUE.
      LPBCLOSE = .TRUE.


      IF(LMESSPASS.AND.(IREST.EQ.1.OR.IREST.EQ.3.OR.LDWRRE.OR.LDSTOP))
     1THEN
C
C        C0LLECT THE DIFFERENT CONTRIBUTION FROM THE PROCESSES
C        SINCE THE FILES ARE NOT SAVED IN SPLIT MODE

         ALLOCATE(RU10OLD(NIBLO,NBLO),RTHWOLD(NIBLO,NBLO),
     1   RUSOLD(NIBLO,NBLO),RZ0OLD(NIBLO,NBLO),RTAUW(NIBLO,NBLO))

         RU10OLD=0.
         RTHWOLD=0.
         RUSOLD=0.
         RTAUW=0.
         RZ0OLD=0.
         
         DO IG=1,NBLO
           DO IJ = NSTART(IRANK),NEND(IRANK) 
              RU10OLD(IJ,IG) = U10OLD(IJ,IG)
              RTHWOLD(IJ,IG) = THWOLD(IJ,IG)
              RUSOLD(IJ,IG) = USOLD(IJ,IG)
              RTAUW(IJ,IG) = TAUW(IJ,IG)
              RZ0OLD(IJ,IG) = Z0OLD(IJ,IG)
	        ENDDO
         ENDDO

         IRECV=IPFGTBL(JPPFLAG+1)
         CALL MPGATHERSCFLD(IRECV,151,NSTART,NEND,RU10OLD)
         CALL MPGATHERSCFLD(IRECV,152,NSTART,NEND,RTHWOLD)
         CALL MPGATHERSCFLD(IRECV,153,NSTART,NEND,RUSOLD)
         CALL MPGATHERSCFLD(IRECV,154,NSTART,NEND,RTAUW)
         CALL MPGATHERSCFLD(IRECV,155,NSTART,NEND,RZ0OLD)


         IF (ITEST.GE.2)
     1   WRITE(IU06,*) '   SUB. SAVSTRESS: RESTART FILES COLLECTED'

         IF(IRANK.EQ.IPFGTBL(JPPFLAG+1)) THEN

            CALL GRSTNAME(IU06,CDTPRO,CDATEF,IREST,'LAW',FILENAME)
C
C          THE RESTART FILES COULD BE OUTPUT TO A VFL FILES SYSTEM
C          WHERE PREALLOCATION OF THE SPACE CAN BE DONE BEFORE WRITE TO DISK
C          IREST = 3 MUST BE SELECTED.
C
            IF(IREST.EQ.3) THEN
                IBLK=(5*NIBLO+4)*NPRECI/1000000+1
                CALL PREALLOC_FILE(IU06,IBLK,FILENAME)
            ENDIF

            CALL WRITESTRESS(1,NIBLO,RU10OLD,RTHWOLD,RUSOLD,RTAUW,
     1                       RZ0OLD,FILENAME,LPBOPEN,LPBCLOSE)

         ENDIF

         DEALLOCATE(RU10OLD,RTHWOLD,RUSOLD,RTAUW,RZ0OLD)

      ELSEIF(LMESSPASS.AND.(IREST.EQ.2.OR.IREST.EQ.4)) THEN

        CALL GRSTNAME(IU06,CDTPRO,CDATEF,IREST,'LAW',FILENAME)
C
C       THE RESTART FILES COULD BE OUTPUT TO A VFL FILES SYSTEM
C       WHERE PREALLOCATION OF THE SPACE CAN BE DONE BEFORE WRITE TO DISK
C       THE IREST = 4 OPTION MUST THEN BE SELECTED.
C
        IF(IREST.EQ.4) THEN
            IBLK=(5*(NEND(IRANK)-NSTART(IRANK))+4)*NPRECI/1000000+1
            CALL PREALLOC_FILE(IU06,IBLK,FILENAME)
        ENDIF

            CALL WRITESTRESS(NSTART(IRANK),NEND(IRANK),
     1                       U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     2                       FILENAME,LPBOPEN,LPBCLOSE)

      ELSEIF(.NOT.LMESSPASS) THEN

        IF(LPBIOOUT) CALL GRSTNAME(IU06,CDTPRO,CDATEF,1,'LAW',FILENAME)
        CALL WRITESTRESS(1,NIBLO,U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     1                   FILENAME,LPBOPEN,LPBCLOSE)

      ENDIF

       WRITE(IU06,*) ' '
       WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
       WRITE(IU06,*) ' '
       WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL ....',
     1                      ' CDATEWO = ', CDATEWO
       WRITE(IU06,*) ' NEXT WIND FILE NAME IS ...........',
     1                      ' CDAWIFL = ', CDAWIFL
       WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT',
     1                     ' CDATEFL = ', CDATEFL

       RETURN
       END
