      SUBROUTINE SAVSTRESS(U10OLD, THWOLD, USOLD, TAUW, Z0OLD,
     &                     ROAIRO, ZIDLOLD, CICOVER, CITHICK,
     &                     NBLKS, NBLKE, CDTPRO, CDATEF)
! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      MARCH 1997
!     J. BIDLOT    ECMWF      SEPTEMBER 1997 

!*    PURPOSE.
!     --------
!     WRITE BINARY RESTART STRESS/WIND FIELDS TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSTRESS(U10OLD, THWOLD, USOLD, TAUW, Z0OLD,
!                       ROAIRO, ZIDLOLD, CICOVER, CITHICK,
!                       NBLKS, NBLKE, CDTPRO, CDATEF)
!     *U10OLD*    INTERMEDIATE STORAGE OF WIND SPEED.
!     *THWOLD*    INTERMEDIATE STORAGE OF WIND DIRECTION. 
!     *USOLD*     INTERMEDIATE STORAGE OF MODULUS OF FRICTION VELOCITY.
!     *TAUW*      WAVE STRESS IN (M/S)**2.
!     *Z0OLD*     INTERMEDIATE STORAGE OF ROUGHNESS LENGTH IN M.
!     *ROAIRO*    AIR/WATER DENSITY RATIO.
!     *ZIDLOLD*   Zi/L (Zi: INVERSION HEIGHT, L: MONIN-OBUKHOV LENGTH).
!     *CICOVER*   SEA ICE COVER.
!     *CITHICK*   SEA ICE THICKNESS.
!     *NBLKS*     INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN.
!     *NBLKE*     INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN.
!     *CDTPRO*    CHAR*14   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*14   START DATE FORECAST.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ONTHE OUTPUT
!     PE BY CALLING MPGATHERSCFLD.

!     EXTERNALS.
!     ----------
!     GRSTNAME
!     MPGATHERSCFLD

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWCOUT  , ONLY : NREAL    ,JPPFLAG  ,IPFGTBL
      USE YOWCURR  , ONLY : U        ,V
      USE YOWGRID  , ONLY : IJSLOC   ,IJLLOC   ,IJGLOBAL_OFFSET
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NINF     ,NSUP
      USE YOWPCONS , ONLY : ROAIR
      USE YOWPARAM , ONLY : NBLO     ,NIBLO
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------
      IMPLICIT NONE

      INTEGER :: IG
      INTEGER :: IFLD, IJ, IRECV
      INTEGER, DIMENSION(NPROC) :: NBLKS, NBLKE

      REAL :: ZHOOK_HANDLE
      REAL,DIMENSION(NINF:NSUP,NBLO) :: U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     &                                  ROAIRO,ZIDLOLD,CICOVER,CITHICK
      REAL, ALLOCATABLE,DIMENSION(:,:) :: RDUM 
      REAL, ALLOCATABLE,DIMENSION(:,:) :: RFIELD

      CHARACTER(LEN=14) :: CDTPRO,CDATEF
      CHARACTER(LEN=120) :: FILENAME

      LOGICAL :: LOUNIT, LCUNIT
! ----------------------------------------------------------------------

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('SAVSTRESS',0,ZHOOK_HANDLE)
#endif

      LOUNIT = .TRUE.
      LCUNIT = .TRUE.

!     COLLECT THE DIFFERENT CONTRIBUTIONS FROM THE PROCESSES
!     SINCE THE FILES ARE NOT SAVED IN SPLIT MODE
!     ----------------------------------------------------

      ALLOCATE(RFIELD(NIBLO,NREAL))

!     ASSIGN THE DIFFERENT RESTART FIELDS TO OUTPUT ARRAY.
!     MAKE SURE IT IS THE SAME IN GETSTRESS !
      DO IFLD=1,NREAL
        IG=1
        DO IJ = IJSLOC, IJLLOC 
          RFIELD(IJ+IJGLOBAL_OFFSET,1) = U10OLD(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,2) = THWOLD(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,3) = USOLD(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,4) = TAUW(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,5) = Z0OLD(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,6) = ROAIRO(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,7) = ZIDLOLD(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,8) = CICOVER(IJ,IG)
          RFIELD(IJ+IJGLOBAL_OFFSET,9) = CITHICK(IJ,IG)
          IF(ALLOCATED(U)) THEN
            RFIELD(IJ+IJGLOBAL_OFFSET,10) =  U(IJ,IG) 
          ELSE
            RFIELD(IJ+IJGLOBAL_OFFSET,10) =  0.
          ENDIF
          IF(ALLOCATED(V)) THEN
            RFIELD(IJ+IJGLOBAL_OFFSET,11) =  V(IJ,IG) 
          ELSE
            RFIELD(IJ+IJGLOBAL_OFFSET,11) =  0.
          ENDIF
        ENDDO
      ENDDO

      IRECV=1


!     COLLECT THE DIFFERENT CONTRIBUTIONS
      DO IFLD=1,NREAL
        CALL MPGATHERSCFLD(IRECV,NBLKS,NBLKE,RFIELD(:,IFLD),NIBLO)
      ENDDO

      IF (ITEST.GE.2)
     &   WRITE(IU06,*) '   SUB. SAVSTRESS: RESTART FILES COLLECTED'


      IF(IRANK.EQ.IRECV) THEN
          CALL GRSTNAME(CDTPRO,CDATEF,'LAW',ICPLEN,CPATH,FILENAME)
          CALL WRITESTRESS(NREAL,RFIELD,FILENAME,LOUNIT,LCUNIT)
      ENDIF

      DEALLOCATE(RFIELD)

      WRITE(IU06,*) ' '
      WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
      WRITE(IU06,*) ' '
      WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL ....',
     & ' CDATEWO = ', CDATEWO
      WRITE(IU06,*) ' NEXT WIND FILE NAME IS ...........',
     & ' CDAWIFL = ', CDAWIFL
      WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT',
     & ' CDATEFL = ', CDATEFL
      CALL FLUSH(IU06)

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('SAVSTRESS',1,ZHOOK_HANDLE)
#endif

      RETURN
      END SUBROUTINE SAVSTRESS
