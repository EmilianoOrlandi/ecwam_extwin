      SUBROUTINE SAVSTRESS(U10OLD,THWOLD,USOLD,TAUW,Z0OLD,NSTART,NEND,
     &                     CDTPRO,CDATEF)
! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      MARCH 1997
!     J. BIDLOT    ECMWF      SEPTEMBER 1997 

!*    PURPOSE.
!     --------
!     WRITE RESTART STRESS/WIND FIELDS TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSTRESS(U10OLD,THWOLD,USOLD,TAUW,Z0OLD,NSTART,NEND,
!                       CDTPRO,CDATEF)
!     *U10OLD*    INTERMEDIATE STORAGE OF WIND SPEED.
!     *THWOLD*    INTERMEDIATE STORAGE OF WIND DIRECTION. 
!     *USOLD*     INTERMEDIATE STORAGE OF MODULUS OF FRICTION VELOCITY.
!     *Z0OLD*     INTERMEDIATE STORAGE OF ROUGHNESS LENGTH IN M.
!     *TAUW*      WAVE STRESS IN (M/S)**2.
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN.
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN.
!     *CDTPRO*    CHAR*12   END DATE OF PROPAGATION.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ONTHE OUTPUT
!     PE BY CALLING MPGATHERSCFLD.

!     EXTERNALS.
!     ----------
!     GRSTNAME
!     MPGATHERSCFLD

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWCOUT  , ONLY : JPPFLAG  ,IPFGTBL
      USE YOWMESPAS, ONLY : LMESSPASS,LPBIOOUT
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NINF     ,NSUP     ,
     &            NPRECR
      USE YOWPARAM , ONLY : NBLO     ,NIBLO
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL

! ----------------------------------------------------------------------
      CHARACTER*120 FILENAME
      CHARACTER*12 CDTPRO,CDATEF
      LOGICAL LPBOPEN, LPBCLOSE
      INTEGER,DIMENSION(NPROC) :: NSTART, NEND
      REAL,DIMENSION(NINF:NSUP,NBLO) :: U10OLD,THWOLD,USOLD,TAUW,Z0OLD 
! ----------------------------------------------------------------------
!     define local allocatable arrays
      REAL,ALLOCATABLE,DIMENSION(:,:) :: RU10OLD,RTHWOLD,RUSOLD,
     &                                   RZ0OLD,RTAUW

      LPBOPEN = .TRUE.
      LPBCLOSE = .TRUE.

      IF(LMESSPASS) THEN

!        C0LLECT THE DIFFERENT CONTRIBUTION FROM THE PROCESSES
!        SINCE THE FILES ARE NOT SAVED IN SPLIT MODE

        ALLOCATE(RU10OLD(NIBLO,NBLO),RTHWOLD(NIBLO,NBLO),
     &   RUSOLD(NIBLO,NBLO),RZ0OLD(NIBLO,NBLO),RTAUW(NIBLO,NBLO))

        RU10OLD=0.
        RTHWOLD=0.
        RUSOLD=0.
        RTAUW=0.
        RZ0OLD=0.
         
        DO IG=1,NBLO
          DO IJ = NSTART(IRANK),NEND(IRANK) 
            RU10OLD(IJ,IG) = U10OLD(IJ,IG)
            RTHWOLD(IJ,IG) = THWOLD(IJ,IG)
            RUSOLD(IJ,IG) = USOLD(IJ,IG)
            RTAUW(IJ,IG) = TAUW(IJ,IG)
            RZ0OLD(IJ,IG) = Z0OLD(IJ,IG)
          ENDDO
        ENDDO

        IRECV=IPFGTBL(JPPFLAG+1)
        CALL MPGATHERSCFLD(IRECV,151,NSTART,NEND,RU10OLD)
        CALL MPGATHERSCFLD(IRECV,152,NSTART,NEND,RTHWOLD)
        CALL MPGATHERSCFLD(IRECV,153,NSTART,NEND,RUSOLD)
        CALL MPGATHERSCFLD(IRECV,154,NSTART,NEND,RTAUW)
        CALL MPGATHERSCFLD(IRECV,155,NSTART,NEND,RZ0OLD)

        IF (ITEST.GE.2)
     &   WRITE(IU06,*) '   SUB. SAVSTRESS: RESTART FILES COLLECTED'

        IF(IRANK.EQ.IPFGTBL(JPPFLAG+1)) THEN

          CALL GRSTNAME(CDTPRO,CDATEF,'LAW',ICPLEN,CPATH,FILENAME)

          CALL WRITESTRESS(1,NIBLO,RU10OLD,RTHWOLD,RUSOLD,RTAUW,
     &     RZ0OLD,FILENAME,LPBOPEN,LPBCLOSE)

        ENDIF

        DEALLOCATE(RU10OLD,RTHWOLD,RUSOLD,RTAUW,RZ0OLD)

      ELSE

        IF(LPBIOOUT) CALL GRSTNAME(CDTPRO,CDATEF,'LAW',ICPLEN,CPATH,
     &                             FILENAME)
        CALL WRITESTRESS(1,NIBLO,U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     &                   FILENAME,LPBOPEN,LPBCLOSE)

      ENDIF

      WRITE(IU06,*) ' '
      WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
      WRITE(IU06,*) ' '
      WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL ....',
     & ' CDATEWO = ', CDATEWO
      WRITE(IU06,*) ' NEXT WIND FILE NAME IS ...........',
     & ' CDAWIFL = ', CDAWIFL
      WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT',
     & ' CDATEFL = ', CDATEFL

      RETURN
      END SUBROUTINE SAVSTRESS
