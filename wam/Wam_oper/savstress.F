      SUBROUTINE SAVSTRESS(U10OLD, THWOLD, USOLD, TAUW, Z0OLD,
     &                     ROAIRO, ZIDLOLD, CICOVER, CITHICK,
     &                     NBLKS, NBLKE, CDTPRO, CDATEF)
! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      MARCH 1997
!     J. BIDLOT    ECMWF      SEPTEMBER 1997 

!*    PURPOSE.
!     --------
!     WRITE BINARY RESTART STRESS/WIND FIELDS TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSTRESS(U10OLD, THWOLD, USOLD, TAUW, Z0OLD,
!                       ROAIRO, ZIDLOLD, CICOVER, CITHICK,
!                       NBLKS, NBLKE, CDTPRO, CDATEF)
!     *U10OLD*    INTERMEDIATE STORAGE OF WIND SPEED.
!     *THWOLD*    INTERMEDIATE STORAGE OF WIND DIRECTION. 
!     *USOLD*     INTERMEDIATE STORAGE OF MODULUS OF FRICTION VELOCITY.
!     *TAUW*      WAVE STRESS IN (M/S)**2.
!     *Z0OLD*     INTERMEDIATE STORAGE OF ROUGHNESS LENGTH IN M.
!     *ROAIRO*    AIR/WATER DENSITY RATIO.
!     *ZIDLOLD*   Zi/L (Zi: INVERSION HEIGHT, L: MONIN-OBUKHOV LENGTH).
!     *CICOVER*   SEA ICE COVER.
!     *CITHICK*   SEA ICE THICKNESS.
!     *NBLKS*     INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN.
!     *NBLKE*     INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN.
!     *CDTPRO*    CHAR*14   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*14   START DATE FORECAST.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ONTHE OUTPUT
!     PE BY CALLING MPGATHERSCFLD.

!     EXTERNALS.
!     ----------
!     GRSTNAME
!     MPGATHERSCFLD

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWCOUT  , ONLY : NREAL    ,JPPFLAG  ,IPFGTBL, LRSTPARALW
      USE YOWCURR  , ONLY : U        ,V
      USE YOWGRID  , ONLY : IJSLOC   ,IJLLOC   ,IJGLOBAL_OFFSET
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NINF     ,NSUP
      USE YOWPCONS , ONLY : ROAIR
      USE YOWPARAM , ONLY : NBLO     ,NIBLO
      USE YOWSTAT  , ONLY : NPROMA_WAM
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------
      IMPLICIT NONE

      REAL,DIMENSION(NINF:NSUP,NBLO),INTENT(IN) ::
     &                                  U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     &                                  ROAIRO,ZIDLOLD,CICOVER,CITHICK
      INTEGER, DIMENSION(NPROC),INTENT(IN) :: NBLKS, NBLKE
      CHARACTER(LEN=14), INTENT(IN) :: CDTPRO, CDATEF

      INTEGER :: IG
      INTEGER :: IFLD, IJ, IRECV
      INTEGER :: IJINF, IJSUP, MIJS, MIJL, IJOFFSET
      INTEGER :: JKGLO, KIJS, KIJL, NPROMA
      INTEGER :: LNAME

      REAL :: ZHOOK_HANDLE
      REAL, ALLOCATABLE,DIMENSION(:,:) :: RDUM 
      REAL, ALLOCATABLE,DIMENSION(:,:) :: RFIELD

      CHARACTER(LEN=296) :: FILENAME

      LOGICAL :: LUAL, LVAL
! ----------------------------------------------------------------------

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('SAVSTRESS',0,ZHOOK_HANDLE)
#endif

      IRECV=1

      IF(LRSTPARALW) THEN
        IJINF=NINF
        IJSUP=NSUP
        MIJS=IJINF
        MIJL=IJSUP
        IJOFFSET=0
      ELSE
        IJINF=1
        IJSUP=NIBLO
        MIJS=IJSLOC
        MIJL=IJLLOC 
        IJOFFSET=IJGLOBAL_OFFSET
      ENDIF

      ALLOCATE(RFIELD(IJINF:IJSUP,NREAL))

!     ASSIGN THE DIFFERENT RESTART FIELDS TO OUTPUT ARRAY.
!     MAKE SURE IT IS THE SAME IN GETSTRESS !
      IG=1
      NPROMA=NPROMA_WAM
      LUAL=ALLOCATED(U)
      LVAL=ALLOCATED(V)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JKGLO,KIJS,KIJL,IJ)
      DO JKGLO=MIJS,MIJL,NPROMA
        KIJS=JKGLO
        KIJL=MIN(KIJS+NPROMA-1,MIJL)
        DO IJ=KIJS+IJOFFSET,KIJL+IJOFFSET
          RFIELD(IJ,1) = U10OLD(IJ,IG)
          RFIELD(IJ,2) = THWOLD(IJ,IG)
          RFIELD(IJ,3) = USOLD(IJ,IG)
          RFIELD(IJ,4) = TAUW(IJ,IG)
          RFIELD(IJ,5) = Z0OLD(IJ,IG)
          RFIELD(IJ,6) = ROAIRO(IJ,IG)
          RFIELD(IJ,7) = ZIDLOLD(IJ,IG)
          RFIELD(IJ,8) = CICOVER(IJ,IG)
          RFIELD(IJ,9) = CITHICK(IJ,IG)
          IF(LUAL) THEN
            RFIELD(IJ,10) = U(IJ,IG) 
          ELSE
            RFIELD(IJ,10) = 0.
          ENDIF
          IF(LVAL) THEN
            RFIELD(IJ,11) = V(IJ,IG) 
          ELSE
            RFIELD(IJ,11) = 0.
          ENDIF
        ENDDO
      ENDDO
!$OMP END PARALLEL DO

      IF(.NOT.LRSTPARALW) THEN
!       COLLECT THE DIFFERENT CONTRIBUTIONS FROM THE PROCESSES
!       SINCE THE FILES ARE NOT SAVED IN SPLIT MODE
        DO IFLD=1,NREAL
          CALL MPGATHERSCFLD(IRECV,NBLKS,NBLKE,
     &                       RFIELD(IJINF:IJSUP,IFLD),IJSUP-IJINF+1)
        ENDDO

        IF (ITEST.GE.2)
     &     WRITE(IU06,*) '   SUB. SAVSTRESS: RESTART FILES COLLECTED'
      ENDIF

!     SAVE RESTART
      CALL GRSTNAME(CDTPRO,CDATEF,'LAW',ICPLEN,CPATH,FILENAME)
      IF(LRSTPARALW) THEN
!        RESTART FILES FROM ALL PS's
         LNAME = LEN_TRIM(FILENAME)
         FILENAME=FILENAME(1:LNAME)//'.%p_%n'
         CALL EXPAND_STRING(IRANK,NPROC,0,0,FILENAME,1)
      ENDIF

      IF(LRSTPARALW .OR. IRANK.EQ.IRECV) THEN
          CALL WRITESTRESS(IJINF, IJSUP, NREAL, RFIELD,
     &                     FILENAME, LRSTPARALW)
      ENDIF
      DEALLOCATE(RFIELD)

      WRITE(IU06,*) ' '
      WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
      WRITE(IU06,*) ' '
      WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL ....',
     & ' CDATEWO = ', CDATEWO
      WRITE(IU06,*) ' NEXT WIND FILE NAME IS ...........',
     & ' CDAWIFL = ', CDAWIFL
      WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT',
     & ' CDATEFL = ', CDATEFL
      CALL FLUSH(IU06)

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('SAVSTRESS',1,ZHOOK_HANDLE)
#endif

      RETURN
      END SUBROUTINE SAVSTRESS
