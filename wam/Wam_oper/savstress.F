      SUBROUTINE SAVSTRESS(U10OLD,THWOLD,USOLD,TAUW,Z0OLD,ROAIRO,
     &                     ZIDLOLD,CICOVER,NSTART,NEND,CDTPRO,CDATEF)
! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      MARCH 1997
!     J. BIDLOT    ECMWF      SEPTEMBER 1997 

!*    PURPOSE.
!     --------
!     WRITE RESTART STRESS/WIND FIELDS TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSTRESS(U10OLD,THWOLD,USOLD,Z0OLD,TAUW,ROAIRO,
!                       ZIDLOLD,CICOVER,NSTART,NEND,CDTPRO,CDATEF)
!     *U10OLD*    INTERMEDIATE STORAGE OF WIND SPEED.
!     *THWOLD*    INTERMEDIATE STORAGE OF WIND DIRECTION. 
!     *USOLD*     INTERMEDIATE STORAGE OF MODULUS OF FRICTION VELOCITY.
!     *TAUW*      WAVE STRESS IN (M/S)**2.
!     *Z0OLD*     INTERMEDIATE STORAGE OF ROUGHNESS LENGTH IN M.
!     *ROAIRO*    AIR/WATER DENSITY RATIO.
!     *ZIDLOLD*   Zi/L (Zi: INVERSION HEIGHT, L: MONIN-OBUKHOV LENGTH).
!     *CICOVER*   SEA ICE COVER.
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN.
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN.
!     *CDTPRO*    CHAR*14   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*14   START DATE FORECAST.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ONTHE OUTPUT
!     PE BY CALLING MPGATHERSCFLD.

!     EXTERNALS.
!     ----------
!     GRSTNAME
!     MPGATHERSCFLD

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWCOUT  , ONLY : NREAL    ,JPPFLAG  ,IPFGTBL
      USE YOWCURR  , ONLY : U        ,V
      USE YOWMESPAS, ONLY : LMESSPASS,LPBIOOUT
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NINF     ,NSUP     ,
     &            NPRECR
      USE YOWPCONS , ONLY : ROAIR
      USE YOWPARAM , ONLY : NBLO     ,NIBLO
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL

! ----------------------------------------------------------------------
      CHARACTER*120 FILENAME
      CHARACTER*14 CDTPRO,CDATEF
      LOGICAL LPBOPEN, LPBCLOSE
      INTEGER,DIMENSION(NPROC) :: NSTART, NEND
      REAL,DIMENSION(NINF:NSUP,NBLO) :: U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     &                                  ROAIRO,ZIDLOLD,CICOVER
      INTEGER :: IFLD
! ----------------------------------------------------------------------
!     define local allocatable arrays
      REAL,ALLOCATABLE,DIMENSION(:,:,:) :: RFIELD
      REAL,ALLOCATABLE,DIMENSION(:,:) :: RDUM 

      LPBOPEN = .TRUE.
      LPBCLOSE = .TRUE.

!     COLLECT THE DIFFERENT CONTRIBUTIONS FROM THE PROCESSES
!     SINCE THE FILES ARE NOT SAVED IN SPLIT MODE
!     ----------------------------------------------------

      ALLOCATE(RFIELD(NIBLO,NBLO,NREAL))

!     ASSIGN THE DIFFERENT RESTART FIELDS TO OUTPUT ARRAY.
!     MAKE SURE IT IS THE SAME IN GETSTRESS !
      DO IFLD=1,NREAL
        DO IG=1,NBLO
          DO IJ = NSTART(IRANK),NEND(IRANK) 
            RFIELD(IJ,IG,1) = U10OLD(IJ,IG)
            RFIELD(IJ,IG,2) = THWOLD(IJ,IG)
            RFIELD(IJ,IG,3) = USOLD(IJ,IG)
            RFIELD(IJ,IG,4) = TAUW(IJ,IG)
            RFIELD(IJ,IG,5) = Z0OLD(IJ,IG)
            RFIELD(IJ,IG,6) = ROAIRO(IJ,IG)
            RFIELD(IJ,IG,7) = ZIDLOLD(IJ,IG)
            RFIELD(IJ,IG,8) = CICOVER(IJ,IG)
            IF(ALLOCATED(U)) THEN
              RFIELD(IJ,IG,9) =  U(IJ,IG) 
            ELSE
              RFIELD(IJ,IG,9) =  0.
            ENDIF
            IF(ALLOCATED(V)) THEN
              RFIELD(IJ,IG,10) =  V(IJ,IG) 
            ELSE
              RFIELD(IJ,IG,10) =  0.
            ENDIF
          ENDDO
        ENDDO
      ENDDO

      IRECV=1

      IF(LMESSPASS) THEN

!       COLLECT THE DIFFERENT CONTRIBUTIONS
        DO IFLD=1,NREAL
          ITAG=150+IFLD
          CALL MPGATHERSCFLD(IRECV,ITAG,NSTART,NEND,RFIELD(:,:,IFLD))
        ENDDO

        IF (ITEST.GE.2)
     &   WRITE(IU06,*) '   SUB. SAVSTRESS: RESTART FILES COLLECTED'


      ENDIF

      IF(IRANK.EQ.IRECV .OR. .NOT.LMESSPASS) THEN
          CALL GRSTNAME(CDTPRO,CDATEF,'LAW',ICPLEN,CPATH,FILENAME)
          CALL WRITESTRESS(NREAL,RFIELD,FILENAME,LPBOPEN,LPBCLOSE)
      ENDIF

      DEALLOCATE(RFIELD)

      WRITE(IU06,*) ' '
      WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
      WRITE(IU06,*) ' '
      WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL ....',
     & ' CDATEWO = ', CDATEWO
      WRITE(IU06,*) ' NEXT WIND FILE NAME IS ...........',
     & ' CDAWIFL = ', CDAWIFL
      WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT',
     & ' CDATEFL = ', CDATEFL
      CALL FLUSH(IU06)

      RETURN
      END SUBROUTINE SAVSTRESS
