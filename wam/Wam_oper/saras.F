      SUBROUTINE SARAS

!-------------------------------------------------------------------

!     S.HASSELMANN MPM HAMBURG.
!     P.LIONELLO UNIVERSITY OF PADUA.
!     J-R BIDLOT ECMWF

!*    PURPOSE.
!     --------

!     ENRICHED POOR MAN'S DATA ASSIMILATION SCHEME. INTERFACE ROUTINE
!     WITH WAMASSI.

!**   INTERFACE
!     ---------
 
!      EXTERNALS.
!      ----------
 
!     CORREL     - CROSSASSIGNMENT OF WAM AND SAR PARTITIONINGS AND
!                  COMBINING OF SAR PARTITIONINGS IF DISTANCE TO
!                  WAM PARTITIONINGS IS SMALL.
!     CROSSAS    - CROSSASSIGNMENT OF WAM AND SAR PARTITIONINGS
!                  AT DATA POINTS.
!     CROSSW     - CROSSASSIGNMENT OF WAM AND SAR PARTITIONINGS
!                  FOR EACH WAM FIRST GUESS SPECTRUM AND THE SAR
!                  RETRIEVED SPECTRA INFLUENCING THE WAM POINT.
!     DEV        - COMPUTES ERROR BETWEEN FIRST GUESS PART
!     GETWSPEC   - FINDS SPECTRA IN BLOCKED WAM GRID WHICH ARE
!                  INFLUENCED BY OBSERVATIONAL POINTS.
!     MNINTW     - COMPUTES INDEX OF WIND-SEA PARTITIONING.
!     MEANSTAS   - COMPUTES INTEGRATED VALUES OF TOTAL SPECTRA.
!     MERGE      - MERGES INTERPOLATED SPECTRA INTO BLOCKED
!                  ARRAY OF WAM SPECTRA FOR FURTHER RUN.
!     OPFIL      - OPENS A DISC FILE.
!     OPTINT     - CREATES AN A FIELD OF ANALYSED MEAN PARAMETERS
!                  FOR PARTITIONINGS WITH OPTIMAL INTERPOLATION.
!     READSARSPEC- READS SPECTRA IN FORMAT OF OUTPUT OF SAR
!                  INVERSION PROGRAM.
!     REARRNGSAR - ROTATES SAR SPECTRA TO WAM MODEL FIRST DIRECTION
!                  AND GET WAM SPECTRA AT SAR OBSERVATION LOCATION.
!     SWELLSEP   - PARTITIONS SPECTRA.
!     TUSTREAS   - TRANSFORMS PARTITIONINGS OF WAM FIRST GUESS
!                  SPECTRA TO ANALYSED MEAN VALUES
!                  AND COMBINES ANALYSED PARTITIONINGS
!                  TO ONE SPECTRUM.

!     FILES:
!     ------
!     INPUT FILE:  UNIT 10 : SPECTRA - OUTPUT OF SAR INVERSION
!                                      PROGRAM. CAN BE REPLACED!
!                  UNIT 9  : CORRECTED WAVE SPECTRA FROM SAR
!                            INVERSION ITERATION. OUTPUT OF
!                            PROGRAM PARTOUT.
!                  UNIT 11 : BLOCKED SPECTRA FROM WAMODEL.

!     OUTPUT FILES:
!     - UNIT 20 : PARTITIONINGS OF FIRST GUESS SPECTRA.
!     - UNIT 21 : HS OF FIRST GUESS SPECTRA.
!     - UNIT 22 : MEAN WAVE LENGTH OF FIRST GUESS SPECTRA.
!     - UNIT 23 : HS  OF PARTITIONINGS OF FIRST GUESS SPECTRA.
!     - UNIT 24 : MEAN WAVE LENGTH OF PARTITIONINGS OF FIRST
!                 GUESS SPECTRA.
!     - UNIT 26 : HS OF TOTAL CORRECTED SPECTRA.
!     - UNIT 27 : MEAN WAVE LENGTH OF TOTAL CORRECTED SPECTRA.
!     - UNIT 28 : CORRECTED SPECTRA.

!     - UNIT 30 : PARTITIONINGS OF INVERTED SPECTRA.
!     - UNIT 31 : HS OF INVERTED SPECTRA.
!     - UNIT 32 : MEAN WAVE LENGTH OF INVERTED SPECTRA.
!     - UNIT 33 : HS OF PARTITIONINGS OF INVERTED SPECTRA.
!     - UNIT 34 : MEAN WAVE LENGTH OF PARTITIONINGS OF INVERTED
!                 SPECTRA

!     - UNIT 40 : MEAN WAM FIRST GUESS HS AT DATA POINTS.
!     - UNIT 41 : MEAN WAM FIRST GUESS WAVE LENGTH AT DATA POINT.
!     - UNIT 42 : MEAN SAR FIRST GUESS HS AT DATA POINTS.
!     - UNIT 43 : MEAN SAR FIRST GUESS WAVE LENGTH AT DATA POINT.
!     - UNIT 44 : MEAN WAM FIRST GUESS HS AT INFLUENCE POINTS.
!     - UNIT 45 : MEAN WAM FIRST GUESS WAVE LENGTH AT INFL. POINT.
!     - UNIT 46 : MEAN INTERPOLATED HS AT INFLUENCE POINTS.
!     - UNIT 47 : MEAN INTERPOLATED WAVE LENGTHS AT INFLUENCE POINT.

!*     METHOD.
!      -------
!
!      THE SIMPLE SCHEME BY LIONELLO ET AL. (S.B.) IS EXTENDED TO MEAN
!      SPECTRAL PARAMETERS HS, MEAN DIRECTION AND MEAN FREQUENCY FOR
!      EACH WAVE SYSTEM IN A SPECTRUM. I.E. EACH OBSERVED (INVERTED)
!      SAR SPECTRUM IS PARTITIONED INTO ITS DIFFERENT WAVE SYSTEMS,
!      THE MEAN PARAMETERS ARE COMPUTED FOR EACH OF THESE SYSTEMS.
!      EACH WAMODEL FIRST GUESS SPECTRUM IS THEN
!      PARTITIONED INTO ITS DIFFERENT WAVE SYSTEMS AND EACH SYSTEM IS
!      CROSSASSIGNED TO CORRESPONDING SAR SYSTEMS.  ANALYSED SYSTEMS
!      ARE OBTAINED COMBINING THE FIRST GUESS SYSTEM AND THE
!      CORRESPONDING SAR SYSTEMS BY AN UNISOTROPIC OPTIMAL INTERPOLATION
!      NOTE THAT THE O.I. IS CARRIED OUT AMONG THE INTEGRAL QUANTITIES
!      CHARACTERIZING EACH SYSTEM, I.E. THE TOTAL ENERGY AND THE
!      COMPONENTS OF THE MEAN WAVENUMBER.
!      EACH WAVE SYSTEM IN ROTATED, STRETCHED AND SCALED ACCORDING
!      TO THE RESULTS OF O.I.  AN ANALYSED SPECTRUM IS THEN FORMED FROM
!      THE ANALYSED WAVE SYSTEMS BY INTERPOLATING GAPS OR LINEARLY
!      OVERLAYING OVERLAPPING PARTS.

!      REFERENCES.
!      -----------
 
!      S Hasselmann, P Lionello, K. Hasselmann, 1997:
!        An optimum interpolation scheme for the assimilation of
!        spectral wave data. J. Geophys. Res.,102, C7 15823-15836.
!**********************************************************************

!     MODULES:
!     --------
      USE YOWFRED  , ONLY : FR       ,DFIM     ,TH       ,COSTH    ,
     &            SINTH    ,FRATIO
      USE YOWGRID  , ONLY : IGL      ,IJS      ,IJL2     ,IJL
      USE YOWMAP   , ONLY : IXLG     ,KXLT     ,IRGG     ,AMOWEP   ,
     &            AMOSOP   ,AMOEAP   ,AMONOP   ,XDELLA   ,XDELLO
      USE YOWMESPAS, ONLY : LMESSPASS
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,KTAG
      USE YOWPARAM , ONLY : NANG     ,NFRE     ,NGX      ,NGY      ,
     &            NBLO     ,NIBLO
      USE YOWPCONS , ONLY : G        ,PI       ,ZPI      ,RAD      ,
     &            DEG 

      USE YOWSARAS , ONLY : NSPEC    ,NSPECW   ,JPSIW    ,MPARTSW  ,
     &            MAXMPART ,IGSAR    ,IJSAR    ,SPEC     ,LONG     ,
     &            LAT      ,XKCUTOFF ,XKLOW    ,XKUP     ,U10      ,
     &            USSAR    ,THW      ,
     &            NW1D     ,NTOSIW1D ,NSIW1D   ,SPECW    ,DIST     ,
     &            SARCORDIA,
     &            UW10     ,USW      ,THWW     ,COSSARLAT,SINSARLAT

      USE YOWSTAT  , ONLY : CDTPRO
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE MPL_MODULE
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! -------------------------------------------------------------------

      IMPLICIT NONE
#include "crossas.intfb.h"
#include "crossw.intfb.h"
#include "dev.intfb.h"
#include "getwspec.intfb.h"
#include "mergesarcor.intfb.h"
#include "mnintw.intfb.h"
#include "mpbcastsarin.intfb.h"
#include "optint.intfb.h"
#include "readsarspec.intfb.h"
#include "rearrngsar.intfb.h"
#include "smoothsarspec.intfb.h"
#include "swellsep.intfb.h"
#include "tustreas.intfb.h"

      INTEGER :: IU11,IU12,IU13, IU14
      INTEGER, ALLOCATABLE :: PART(:,:,:,:)
!                   TO SPEC CORRESPONDING ARRAY OF PARTITIONING
!                   INFORMATION PART(ISPEC,IANG,IFRE) =: P MEANS,
!                   THAT IN SPECTRA ISPEC THE ENERGY AT ANGLE IANG
!                   AND FREQUENCY IFRE BELONGS TO PARTITION NUMBER P.
      INTEGER, ALLOCATABLE :: NPART(:,:) ! NUMBER OF PARTITIONINGS
      INTEGER, ALLOCATABLE :: PARTINFO(:,:,:)
!                   INFORMATION FOR WINDSEA-SWELL:
!                   WINDSEA (=1), MIXED (=2), OR SWELL (=0)
!                   (THERE IS NO MORE THAN ONE WINDSEA PARTIONING)
      INTEGER, ALLOCATABLE :: CORRELTAB(:,:)
!                   CROSSASSIGNS WAM AND SAR PARTITIONINGS.
!                   CORRELTAB(ISPEC,IPARTWAM)=IPARTSAR
      REAL :: LOWEST
!                   WAVE SYSTEMS OF  WAVE HEIGHT LESS THAN LOWEST
!                   ARE COMBINED WITH NEXT WAVE SYSTEM.
!
      REAL, ALLOCATABLE, DIMENSION(:,:,:) :: MEANE, MEANANG, MEANFRE,
     &                                       PKFRE 
!                   MEAN ENERGY,MEAN DIRECTION, MEAN FREQUENCY FOR
!                   ALL PARTITIONIGS AND ALL SPECTRA. LAST INDEX
!                   DENOTES WAM IF EQUAL TO 1 AND SAR IF EQUAL TO 2.

      REAL :: ZHOOK_HANDLE ! Dr HOOK

      REAL :: TH0 ! FIRST DIRECTION IN WAM SPECTRUM.
      REAL :: FRE1 !LOWEST FREQUENCY.
      REAL :: ALEVEL,DISMIN
!                   FACTOR FOR THREHHOLD CHECK, AND MINIMUM
!                   DISTANCE BETWEEN TWO WAVE SYSTEMS IN K-SPACE
!                   FOR COMBINING SAR PEAKS.
      INTEGER :: ISPEC,ISPA ! LOOP INDICES.

      LOGICAL
     &      LOTEST,LOTIME,LCONTROL,
     &      LODBG
!     FLAGS TO OUTPUT ON UNIT 6

      REAL, ALLOCATABLE, DIMENSION(:,:) :: EA, XFREA, XANGA 
!         ANALYSED MEAN PARAMETERS.

      REAL, ALLOCATABLE, DIMENSION(:) :: MEANTE, MEANTEU, MEANTANG,
     &                                   MEANTANGU, MEANTFRE, MEANTFREU
!                   MEAN ENERGY, MEAN DIRECTION, MEAN FREQUENCY
!                   OF TOTAL SPECTRUM BEFORE AND AFTER ASSIMILATION.

      INTEGER, ALLOCATABLE, DIMENSION(:) :: NPARTW, NINTW
      INTEGER, ALLOCATABLE, DIMENSION(:,:) :: NIPARTINFW, CORRELTABW
      INTEGER, ALLOCATABLE, DIMENSION(:,:,:) :: NIPARTW

      REAL, ALLOCATABLE, DIMENSION(:,:) :: XMEANEW, XMEANANGW,XMEANFREW,
     &                                     PKFREW, EW, XKW, YKW 
!         CHARACTERISTIC PARAMETERS (ENERGY,WAVE NUMBERS) OF WAM
!         FIRST GUESS SPECTRA.

      REAL, ALLOCATABLE, DIMENSION(:,:) :: ED, XKD, YKD
!         ERRORS AT DATA POINTS BETWEEN CHARACTERISTIC PARAMETERS OF
!         SAR WAVE SYSTEMS AND WAM WAVE SYSTEMS AT DAT POINTS
!         (ENERGY AND MEAN WAVE NUMBER IN LAT AND LON DIRECTION).

!
      INTEGER :: IREAD  ! PE NUMBER FROM WHICH INPUT FILE WILL BE READ
      LOGICAL :: LNODATA
!
!     USED BY CROSSW
      INTEGER, ALLOCATABLE, DIMENSION(:,:,:) :: NPASC2W, CORRELTAS
!     NUMBER OF PARTIONING IN SAR-SPECTRUM JPSIW INFLUENCING
!     PARTIONING NPARTW OF FIRST GUESS SPECTRUM NSPECW.

!--------------------------------------------------------------------
#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('SARAS',0,ZHOOK_HANDLE)
#endif

      CALL GSTATS(1883,0)

      LOTEST=.FALSE.
      LOTIME=.TRUE.
      LODBG=.FALSE.

      IU11 = 11
      IU13 = 13
      IU14 = 14
      IU12 = 12

      LCONTROL=.FALSE.
      IF(ITEST.GT.0.) LCONTROL=.TRUE.

      FRE1=FR(1)
      TH0=TH(1)

      IF(XDELLA.LT.0.3) THEN
        SARCORDIA = 200000.
      ELSE
        SARCORDIA = 300000.
      ENDIF

      MAXMPART=0


!-------------------------------------------------------------------

!     1.1 INPUT OF SAR RETRIEVED AND WAM FIRST GUESS
!         SPECTRA AT SAR POINTS.
!     -----------------------------------------------------------

      WRITE(IU06,*)'  -----SAR SPECTRA ASSIMILATION BEGINS--------'
      IF(LCONTROL)
     &WRITE(IU06,*)'-----------READ SAR SPECTRA BEGINS------------'

      IREAD=1

      IF((LMESSPASS.AND.IRANK.EQ.IREAD).OR.(.NOT.LMESSPASS)) THEN
        CALL READSARSPEC(LNODATA)
      ENDIF
 
      IF(LCONTROL) THEN
        WRITE(IU06,*)'-----------READ SAR SPECTRA DONE------------'
        WRITE(IU06,*)' '
      ENDIF

!     BROADCAST INPUT SAR TO ALL PE'S
!     No attempt is yet made to distribute them per PE's (ie by only 
!     providing what each PE needs)
      IF(LMESSPASS) THEN
        IF(LCONTROL)
     &  WRITE(IU06,*)'----------BROADCAST SAR SPECTRA BEGINS-----------'
        CALL MPBCASTSARIN(IREAD, KTAG, LNODATA)
        IF(LCONTROL) THEN
          WRITE(IU06,*)'----------BROADCAST SAR SPECTRA DONE-----------'
          WRITE(IU06,*)' '
        ENDIF
      ENDIF

      IF(LNODATA) GOTO 5000

!     ROTATE INVERTED SAR SPECTRA TO THE FIRST DIRECTION OF WAM (TH0)
!     AND GET WAM SPECTRA AT OBSERVATION LOCATION

      IF(LCONTROL)
     &WRITE(IU06,*)'-----------REARRNGSAR BEGINS------------'

      CALL REARRNGSAR(TH0)

      IF(LCONTROL) THEN
        WRITE(IU06,*)'-----------REARRNGSAR DONE------------'
        WRITE(IU06,*)' '
      ENDIF

!     ALLOCATE NECESSARY ARRAYS
!     -------------------------

      ALLOCATE(PART(NSPEC,NANG,NFRE,2))
      ALLOCATE(NPART(NSPEC,2))
      ALLOCATE(PARTINFO(NSPEC,MPARTSW,2))
      ALLOCATE(CORRELTAB(NSPEC,MPARTSW))
      ALLOCATE(MEANE(NSPEC,MPARTSW,2))
      ALLOCATE(MEANANG(NSPEC,MPARTSW,2))
      ALLOCATE(MEANFRE(NSPEC,MPARTSW,2))
      ALLOCATE(PKFRE(NSPEC,MPARTSW,2))
      ALLOCATE(ED(NSPEC,0:MPARTSW))
      ALLOCATE(XKD(NSPEC,0:MPARTSW))
      ALLOCATE(YKD(NSPEC,0:MPARTSW))

!     COMPUTE COSINE AND SINE OF LATITUDE OF SAR OBSERVATIONS
!     AT MODEL LOCATION
      ALLOCATE(COSSARLAT(NSPEC))
      ALLOCATE(SINSARLAT(NSPEC))
      DO ISPEC = 1,NSPEC
        COSSARLAT(ISPEC) = COS(RAD*LAT(ISPEC,2))
        SINSARLAT(ISPEC) = SIN(RAD*LAT(ISPEC,2))
      ENDDO


!     1.2 PARTITIONING OF WAM SPECTRA.
!     --------------------------------

      IF(LCONTROL)
     &WRITE(IU06,*)'-------------- PARTITIONING OF WAM BEGINS ---'

      LOWEST=0.1
      ALEVEL=0.75

      IF(NANG.GT.12) THEN
        CALL SMOOTHSARSPEC(0,NSPEC,NANG,NFRE,SPEC(0,1,1,1))
      ENDIF

      CALL SWELLSEP(
     &       NSPEC , MPARTSW ,  NANG , NFRE ,
     &       SPEC(0,1,1,1), USSAR, THW, TH0 , FR , COSTH , SINTH,
     &       ALEVEL, LCONTROL, LOWEST,
     &       NPART(1,1) , PART(1,1,1,1), PARTINFO(1,1,1) , MAXMPART, 
     &       MEANE(1,1,1), MEANANG(1,1,1), MEANFRE(1,1,1), PKFRE(1,1,1))


      IF(LCONTROL) THEN
        WRITE(IU06,*)'-------------- PARTITIONING OF WAM DONE ---'
        WRITE(IU06,*)' '
      ENDIF

!     1.3 PARTITIONING OF SAR RETRIEVED SPECTRA.
!     ------------------------------------------

      IF(LCONTROL)
     &WRITE(IU06,*)'-------------- PARTITIONING OF SAR BEGINS ---'
      ALEVEL=0.75

      IF(NANG.GT.12) THEN
        CALL SMOOTHSARSPEC(0,NSPEC,NANG,NFRE,SPEC(0,1,1,2))
      ENDIF

      CALL SWELLSEP(
     &       NSPEC , MPARTSW ,  NANG , NFRE ,
     &       SPEC(0,1,1,2), USSAR, THW, TH0 , FR , COSTH , SINTH,
     &       ALEVEL, LCONTROL, LOWEST,
     &       NPART(1,2) , PART(1,1,1,2) , PARTINFO(1,1,2) , MAXMPART,
     &       MEANE(1,1,2), MEANANG(1,1,2), MEANFRE(1,1,2), PKFRE(1,1,2))

      IF(LCONTROL) THEN
        WRITE(IU06,*)'-------------- PARTITIONING OF SAR DONE ---'
        WRITE(IU06,*)' '
      ENDIF

!     1.5 CROSS ASSIGN WAM AND SAR WAVE SYSTEMS.
!     ------------------------------------------

      DISMIN=1.5

      IF(LCONTROL)
     &WRITE(IU06,*)'------CROSSAS BEGINS -------------------------'

      CALL CROSSAS(
     &          NSPEC , MPARTSW , NPART(1,1) , NPART(1,2) ,
     &          DISMIN,
     &          MEANANG(1,1,1) , MEANFRE(1,1,1) ,
     &          MEANANG(1,1,2) , MEANFRE(1,1,2) ,
     &          LCONTROL,
     &          CORRELTAB)

      IF(LCONTROL) THEN
        WRITE(IU06,*)'----------CROSSAS DONE -----------------------'
        WRITE(IU06,*)' '
        CALL FLUSH(IU06)
      ENDIF

!     1.6 COMPUTE DEVIATION BETWEEN MEAN VALUES OF PARTITIONINGS OF
!         SAR AND WAM SPECTRA AT OBSERVATIONAL POINTS.
!     ----------------------------------------------------------

      IF(LCONTROL)
     &WRITE(IU06,*)'----------DEV BEGINS -------------------------'

      CALL DEV(
!             DIMENSIONS
     &          NSPEC , MPARTSW ,
     &          NPART(1,1), NPART(1,2) ,
!             INPUT
     &          CORRELTAB, XKLOW, XKUP, XKCUTOFF,
     &          MEANE(1,1,1) , MEANANG(1,1,1) , MEANFRE(1,1,1) ,
     &          PKFRE(1,1,1) ,PARTINFO(1,1,1) ,
     &          MEANE(1,1,2) , MEANANG(1,1,2) , MEANFRE(1,1,2) ,
     &          PKFRE(1,1,2) , PARTINFO(1,1,2),
     &          LCONTROL,
!             OUTPUT
     &          ED,XKD,YKD)

      IF(LCONTROL) THEN
        WRITE(IU06,*)'----------DEV DONE -------------------------'
        WRITE(IU06,*)' '
        CALL FLUSH(IU06)
      ENDIF

!------------------------------------------------------------------

!     2. CREATE AN ANALYSED FIELD OF PARTITIONINGS.
!     ---------------------------------------------

!     2.1 INPUT SPECTRA FROM WAMODEL, FIND SPECTRA AT
!         INFLUENCE POINTS OF OBSERVATIONS, COLLECT INTO ARRAY.
!     ---------------------------------------------------------

      IF(LCONTROL)
     &WRITE(IU06,*)'-------SELECT WAM SPECTRA FOR THE ANALYSIS --'

      CALL GETWSPEC

      IF(LCONTROL) THEN
        WRITE(IU06,*)'-------GETWSPEC DONE ----------------------- ' 
        CALL FLUSH(IU06)
      ENDIF

      IF(NSPECW.LE.0) THEN
        WRITE(IU06,*)' '
        WRITE(IU06,*)' !!! NO SAR DATA INFLUENCING THIS PE        !!! '
        WRITE(IU06,*)' !!! NO NEED OF SAR ASSIMILATION ON THIS PE !!! '
        WRITE(IU06,*)' !!! IRANK = ',IRANK 
        WRITE(IU06,*)' '
        CALL FLUSH(IU06)
        GOTO 4000
      ENDIF

!     ALLOCATE RELEVANT ARRAYS

      ALLOCATE(EA(NSPECW,MPARTSW))
      ALLOCATE(XFREA(NSPECW,MPARTSW))
      ALLOCATE(XANGA(NSPECW,MPARTSW))
      ALLOCATE(MEANTE(NSPECW))
      ALLOCATE(MEANTEU(NSPECW))
      ALLOCATE(MEANTANG(NSPECW))
      ALLOCATE(MEANTANGU(NSPECW))
      ALLOCATE(MEANTFRE(NSPECW))
      ALLOCATE(MEANTFREU(NSPECW))
      ALLOCATE(NPARTW(NSPECW))
      ALLOCATE(NIPARTW(NSPECW,NANG,NFRE))
      ALLOCATE(NINTW(NSPECW))
      ALLOCATE(NIPARTINFW(NSPECW,MPARTSW))
      ALLOCATE(CORRELTABW(NSPECW,MPARTSW))
      ALLOCATE(XMEANEW(NSPECW,MPARTSW))
      ALLOCATE(XMEANANGW(NSPECW,MPARTSW))
      ALLOCATE(XMEANFREW (NSPECW,MPARTSW))
!!!   PKFREW will be deallocated after call to swellspec !!!
      ALLOCATE(PKFREW (NSPECW,MPARTSW))
      ALLOCATE(EW(NSPECW,MPARTSW))
      ALLOCATE(XKW(NSPECW,MPARTSW))
      ALLOCATE(YKW(NSPECW,MPARTSW))
      ALLOCATE(NPASC2W(NSPECW,MPARTSW,JPSIW))
      ALLOCATE(CORRELTAS(NSPECW,MPARTSW,JPSIW))


!     2.2 PARTITION WAM SPECTRA AT INFLUENCE POINTS OF OBSERVATIONS.
!     -------------------------------------------------------------

      IF(LCONTROL)
     &WRITE(IU06,*)'-------PARTITIONING OF WAM SPECTRA BEGINS --'

      IF(NANG.GT.12) THEN
        CALL SMOOTHSARSPEC(0,NSPECW,NANG,NFRE,SPECW)
      ENDIF

      ALEVEL=0.75
      CALL SWELLSEP(
     &         NSPECW , MPARTSW ,  NANG , NFRE ,
     &         SPECW, USW, THWW, TH0 ,  FR , COSTH , SINTH,
     &         ALEVEL, LCONTROL, LOWEST,
     &         NPARTW , NIPARTW , NIPARTINFW , MAXMPART,
     &         XMEANEW , XMEANANGW , XMEANFREW, PKFREW )

      DEALLOCATE(PKFREW)

!     COMPUTED INDEX OF WINDSEA PARTITIONING
!     ---------------------------------------

      CALL  MNINTW (
     &                   NSPECW , MPARTSW ,
     &                   NPARTW, NIPARTINFW,
     &                   NINTW
     &              )

      IF(LCONTROL) THEN
        WRITE(IU06,*)'-------PARTITIONING OF WAM SPECTRA ENDS ----'
        WRITE(IU06,*)' '
        CALL FLUSH(IU06)
      ENDIF

!    2.3 CROSSASSIGN WAM PARTITIONINGS AND SAR RETRIEVED
!        PARTITIONINGS.
!    ---------------------------------------------------------

      IF(LCONTROL)
     &WRITE(IU06,*)'------CROSSASSIGNMENT OF WAM AND SAR BEGINS-'

      CALL CROSSW(
!             DIMENSIONS
     &          NSPEC , NSPECW , MPARTSW, JPSIW,
     &          NPARTW, NPART(1,2) ,MAXMPART,
!             INPUT
     &          DISMIN,
     &          NSIW1D, NTOSIW1D,
     &          MEANANG(1,1,2), MEANFRE(1,1,2) ,
     &          XMEANEW , XMEANANGW, XMEANFREW ,
     &          LCONTROL,
!             OUTPUT
     &          NPASC2W, CORRELTAS, EW, XKW, YKW)


!     2.4 OPTIMAL INTERPOLATION OF MEAN VALUES OF PARTITIONINGS.
!     -----------------------------------------------------------

      IF(LCONTROL) THEN
        WRITE(IU06,*)'------ OPTINT BEGINS ---------------------------'
        CALL FLUSH(IU06)
      ENDIF

      CALL OPTINT(
!           INPUT
     &        NPARTW,NPASC2W,
     &        MEANE(1,1,1),
     &        EW,XKW,YKW,
     &        ED,XKD,YKD,
     &        SPEC(0,1,1,2),CORRELTAS,
     &        PART(1,1,1,2),
!            OUTPUT
     &        EA,XANGA,XFREA)

      IF(LCONTROL) THEN
        WRITE(IU06,*)'------ OPTINT ENDS ---------------------------'
        WRITE(IU06,*)' '
        CALL FLUSH(IU06)
      ENDIF


!     2.5 ADJUST WAM PARTITIONINGS TO SAR PARTITIONINGS BY ROTATION
!         AND FREQUENCY TRANSFORMATION.
!     ------------------------------------------------------------

      DO ISPEC=1,NSPECW
        DO ISPA=1,NPARTW(ISPEC)
          CORRELTABW(ISPEC,ISPA)=ISPA
        ENDDO
      ENDDO

      IF(LCONTROL)
     &WRITE(IU06,*)'---------------TUSTREAS BEGINS---------------'

      CALL TUSTREAS(
!          DIMENSIONS
     &            NSPECW , MPARTSW , NPARTW , NANG , NFRE ,
     &            SPECW , NW1D, NIPARTW , CORRELTABW ,
     &            XMEANEW, XMEANANGW, XMEANFREW,
     &            EA , XANGA , XFREA,
     &            MEANTEU , MEANTANGU , MEANTFREU ,
     &            FRATIO , FR , COSTH , SINTH, LCONTROL,
     &            IU06 )

      IF(LCONTROL) THEN
        WRITE(IU06,*)'---------------TUSTREAS ENDS-----------------'
        CALL FLUSH(IU06)
      ENDIF

!--------------------------------------------------------------------

!     3. CORRECTION OF LOCAL USTAR FROM WINDSEA PARTITIONINGS.
!     -------------------------------------------------------

!     no longer used !!!

!     4. MERGE ANALYSED SPECTRA INTO BLOCKED FIELDS.
!        AND ANALYSED WINDS INTO WIND ARRAYS.
!     -----------------------------------------------

4000  CONTINUE
      CALL MERGESARCOR


!     5. DEALLOCATION OF ARRAYS
!     -------------------------
!!! note it might be possible to deallocate some of the arrays some time
!!! before that.

5000  CONTINUE
      IF(ALLOCATED(IGSAR)) DEALLOCATE(IGSAR)
      IF(ALLOCATED(IJSAR)) DEALLOCATE(IJSAR)
      IF(ALLOCATED(SPEC)) DEALLOCATE(SPEC)
      IF(ALLOCATED(LONG)) DEALLOCATE(LONG)
      IF(ALLOCATED(LAT)) DEALLOCATE(LAT)
      IF(ALLOCATED(XKLOW)) DEALLOCATE(XKLOW)
      IF(ALLOCATED(XKUP)) DEALLOCATE(XKUP)
      IF(ALLOCATED(XKCUTOFF)) DEALLOCATE(XKCUTOFF)
      IF(ALLOCATED(COSSARLAT)) DEALLOCATE(COSSARLAT)
      IF(ALLOCATED(SINSARLAT)) DEALLOCATE(SINSARLAT)
      IF(ALLOCATED(U10)) DEALLOCATE(U10)
      IF(ALLOCATED(USSAR)) DEALLOCATE(USSAR)
      IF(ALLOCATED(THW)) DEALLOCATE(THW)
      IF(ALLOCATED(PART)) DEALLOCATE(PART)
      IF(ALLOCATED(NPART)) DEALLOCATE(NPART)
      IF(ALLOCATED(PARTINFO)) DEALLOCATE(PARTINFO)
      IF(ALLOCATED(CORRELTAB)) DEALLOCATE(CORRELTAB)
      IF(ALLOCATED(MEANE)) DEALLOCATE(MEANE)
      IF(ALLOCATED(MEANANG)) DEALLOCATE(MEANANG)
      IF(ALLOCATED(MEANFRE)) DEALLOCATE(MEANFRE)
      IF(ALLOCATED(PKFRE)) DEALLOCATE(PKFRE)
      IF(ALLOCATED(ED)) DEALLOCATE(ED)
      IF(ALLOCATED(XKD)) DEALLOCATE(XKD)
      IF(ALLOCATED(YKD)) DEALLOCATE(YKD)
      IF(ALLOCATED(NW1D)) DEALLOCATE(NW1D)
      IF(ALLOCATED(NTOSIW1D)) DEALLOCATE(NTOSIW1D)
      IF(ALLOCATED(NSIW1D)) DEALLOCATE(NSIW1D)
      IF(ALLOCATED(SPECW)) DEALLOCATE(SPECW)
      IF(ALLOCATED(DIST)) DEALLOCATE(DIST)
      IF(ALLOCATED(UW10)) DEALLOCATE(UW10)
      IF(ALLOCATED(USW)) DEALLOCATE(USW)
      IF(ALLOCATED(THWW)) DEALLOCATE(THWW)

      IF(ALLOCATED(EA)) DEALLOCATE(EA)
      IF(ALLOCATED(XFREA)) DEALLOCATE(XFREA)
      IF(ALLOCATED(XANGA)) DEALLOCATE(XANGA)
      IF(ALLOCATED(MEANTE)) DEALLOCATE(MEANTE)
      IF(ALLOCATED(MEANTEU)) DEALLOCATE(MEANTEU)
      IF(ALLOCATED(MEANTANG)) DEALLOCATE(MEANTANG)
      IF(ALLOCATED(MEANTANGU)) DEALLOCATE(MEANTANGU)
      IF(ALLOCATED(MEANTFRE)) DEALLOCATE(MEANTFRE)
      IF(ALLOCATED(MEANTFREU)) DEALLOCATE(MEANTFREU)
      IF(ALLOCATED(NPARTW)) DEALLOCATE(NPARTW)
      IF(ALLOCATED(NIPARTW)) DEALLOCATE(NIPARTW)
      IF(ALLOCATED(NINTW)) DEALLOCATE(NINTW)
      IF(ALLOCATED(NIPARTINFW)) DEALLOCATE(NIPARTINFW)
      IF(ALLOCATED(CORRELTABW)) DEALLOCATE(CORRELTABW)
      IF(ALLOCATED(XMEANEW)) DEALLOCATE(XMEANEW)
      IF(ALLOCATED(XMEANANGW)) DEALLOCATE(XMEANANGW)
      IF(ALLOCATED(XMEANFREW)) DEALLOCATE(XMEANFREW)
      IF(ALLOCATED(EW)) DEALLOCATE(EW)
      IF(ALLOCATED(XKW)) DEALLOCATE(XKW)
      IF(ALLOCATED(YKW)) DEALLOCATE(YKW)
      IF(ALLOCATED(NPASC2W)) DEALLOCATE(NPASC2W)
      IF(ALLOCATED(CORRELTAS)) DEALLOCATE(CORRELTAS)


      WRITE(IU06,*) '  SARAS ENDS NORMALLY'
      CALL FLUSH(IU06)

      CALL GSTATS(1883,1)

      CALL MPL_BARRIER(CDSTRING='SARAS:')

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('SARAS',1,ZHOOK_HANDLE)
#endif
      RETURN

      END SUBROUTINE SARAS
