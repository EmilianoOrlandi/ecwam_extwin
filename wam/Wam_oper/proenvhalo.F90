SUBROUTINE PROENVHALO (IJS, IJL, NINF, NSUP,                  &
&                      WAVNUM, CGROUP, OMOSNH2KD,             &
&                      DELLAM1, COSPHM1,                      & 
&                      DEPTH, U, V,                           &
&                      WAVNUM_EXT, CGROUP_EXT, OMOSNH2KD_EXT, &
&                      DELLAM1_EXT, COSPHM1_EXT,              &
&                      DEPTH_EXT, U_EXT, V_EXT )

! ----------------------------------------------------------------------

!**** *PROENVHALO* - WAVE PROPGATION

!*    PURPOSE.
!     --------

!     PRODUCES ARRAYS WITH GRID POINTS VALUES AND THEIR HALO
!     FOR ENVIRONMENT VARIABLES FOR THE WAVE PROPGATION 

! -------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWPARAM , ONLY : NFRE     , NFRE_RED
      USE YOWFRED  , ONLY : WVPRPT_LAND
      USE YOWSHAL  , ONLY : BATHYMAX
      USE YOWSTAT  , ONLY : NPROMA_WAM

      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE

#include "mpexchng.intfb.h"

      INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL  ! GRID POINTS INDEXES
      INTEGER(KIND=JWIM), INTENT(IN) :: NINF, NSUP ! HALO EXTEND NINF to NSUP+1

      REAL(KIND=JWRB), DIMENSION(IJS:IJL, NFRE), INTENT(IN) :: WAVNUM  ! WAVE NUMBER
      REAL(KIND=JWRB), DIMENSION(IJS:IJL, NFRE), INTENT(IN) :: CGROUP  ! GROUP VELOCITY
      REAL(KIND=JWRB), DIMENSION(IJS:IJL, NFRE), INTENT(IN) :: OMOSNH2KD ! OMEGA / SINH(2KD)
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: DELLAM1 ! 1/DELLA 
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: COSPHM1 ! 1/COSPH
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: DEPTH ! WATER DEPTH
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: U ! U-COMPONENT OF SURFACE CURRENT
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: V ! V-COMPONENT OF SURFACE CURRENT

      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: WAVNUM_EXT  ! WAVE NUMBER
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: CGROUP_EXT  ! GROUP VELOCITY
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: OMOSNH2KD_EXT ! OMEGA / SINH(2KD)
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: DELLAM1_EXT ! 1/DELLA
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: COSPHM1_EXT ! 1/COSPH
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: DEPTH_EXT ! WATER DEPTH
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: U_EXT ! U-COMPONENT OF SURFACE CURRENT
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: V_EXT ! V-COMPONENT OF SURFACE CURRENT


      INTEGER(KIND=JWIM) :: IJ, M
      INTEGER(KIND=JWIM) :: JKGLO, KIJS, KIJL, NPROMA

      REAL(KIND=JWRB) :: ZHOOK_HANDLE

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('PROENVHALO',0,ZHOOK_HANDLE)

      NPROMA=NPROMA_WAM
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JKGLO,KIJS,KIJL,M,IJ) 
      DO JKGLO = IJS, IJL, NPROMA
        KIJS=JKGLO
        KIJL=MIN(KIJS+NPROMA-1,IJL)
        DO M = 1, NFRE_RED
           DO IJ = KIJS, KIJL
             WAVNUM_EXT(IJ,M) = WAVNUM(IJ,M)
             CGROUP_EXT(IJ,M) = CGROUP(IJ,M)
             OMOSNH2KD_EXT(IJ,M) = OMOSNH2KD(IJ,M)
           ENDDO
        ENDDO
        DO IJ = KIJS, KIJL
           DELLAM1_EXT(IJ) = DELLAM1(IJ)
           COSPHM1_EXT(IJ) = COSPHM1(IJ)
           DEPTH_EXT(IJ) = DEPTH(IJ)
           U_EXT(IJ) = U(IJ)
           V_EXT(IJ) = V(IJ)
        ENDDO
      ENDDO
!$OMP END PARALLEL DO

!!    should be combine into one single data exchange, when we start using this option.... !!!
      CALL MPEXCHNG(WAVNUM_EXT, NFRE_RED, 1)
      WAVNUM_EXT(NSUP+1,1:NFRE_RED) = WVPRPT_LAND(1:NFRE_RED)%WAVNUM

      CALL MPEXCHNG(CGROUP_EXT, NFRE_RED, 1)
      CGROUP_EXT(NSUP+1,1:NFRE_RED) =  WVPRPT_LAND(1:NFRE_RED)%CGROUP

      CALL MPEXCHNG(OMOSNH2KD_EXT, NFRE_RED, 1)
      OMOSNH2KD_EXT(NSUP+1,1:NFRE_RED) = WVPRPT_LAND(1:NFRE_RED)%OMOSNH2KD

      CALL MPEXCHNG(DELLAM1_EXT, 1, 1)
      DELLAM1_EXT(NSUP+1) = 0.0_JWRB 

      CALL MPEXCHNG(COSPHM1_EXT, 1, 1)
      COSPHM1_EXT(NSUP+1) = 0.0_JWRB 

      CALL MPEXCHNG(DEPTH_EXT, 1, 1)
      DEPTH_EXT(NSUP+1) = BATHYMAX

      CALL MPEXCHNG(U_EXT, 1, 1)
      U_EXT(NSUP+1) = 0.0_JWRB

      CALL MPEXCHNG(V_EXT, 1, 1)
      V_EXT(NSUP+1) = 0.0_JWRB

IF (LHOOK) CALL DR_HOOK('PROENVHALO',1,ZHOOK_HANDLE)

END SUBROUTINE PROENVHALO
