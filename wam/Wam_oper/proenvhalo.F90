SUBROUTINE PROENVHALO (NINF, NSUP,                            &
&                      WAVNUM, CGROUP, OMOSNH2KD,             &
&                      DELLAM1, COSPHM1,                      & 
&                      DEPTH, U, V,                           &
&                      WAVNUM_EXT, CGROUP_EXT, OMOSNH2KD_EXT, &
&                      DELLAM1_EXT, COSPHM1_EXT,              &
&                      DEPTH_EXT, U_EXT, V_EXT )

! ----------------------------------------------------------------------

!**** *PROENVHALO* - WAVE PROPGATION

!*    PURPOSE.
!     --------

!     PRODUCES ARRAYS WITH GRID POINTS VALUES AND THEIR HALO
!     FOR ENVIRONMENT VARIABLES FOR THE WAVE PROPGATION 

! -------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWFRED  , ONLY : WVPRPT_LAND
      USE YOWGRID  , ONLY : NPROMA_WAM, NCHNK
      USE YOWPARAM , ONLY : NFRE     , NFRE_RED
      USE YOWSHAL  , ONLY : BATHYMAX

      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE

#include "mpexchng.intfb.h"

      INTEGER(KIND=JWIM), INTENT(IN) :: NINF, NSUP ! HALO EXTEND NINF to NSUP+1

      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NFRE, NCHNK), INTENT(IN) :: WAVNUM  ! WAVE NUMBER
      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NFRE, NCHNK), INTENT(IN) :: CGROUP  ! GROUP VELOCITY
      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NFRE, NCHNK), INTENT(IN) :: OMOSNH2KD ! OMEGA / SINH(2KD)
      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NCHNK), INTENT(IN) :: DELLAM1 ! 1/DELLA 
      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NCHNK), INTENT(IN) :: COSPHM1 ! 1/COSPH
      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NCHNK), INTENT(IN) :: DEPTH ! WATER DEPTH
      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NCHNK), INTENT(IN) :: U ! U-COMPONENT OF SURFACE CURRENT
      REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NCHNK), INTENT(IN) :: V ! V-COMPONENT OF SURFACE CURRENT

      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: WAVNUM_EXT  ! WAVE NUMBER
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: CGROUP_EXT  ! GROUP VELOCITY
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: OMOSNH2KD_EXT ! OMEGA / SINH(2KD)
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: DELLAM1_EXT ! 1/DELLA
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: COSPHM1_EXT ! 1/COSPH
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: DEPTH_EXT ! WATER DEPTH
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: U_EXT ! U-COMPONENT OF SURFACE CURRENT
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: V_EXT ! V-COMPONENT OF SURFACE CURRENT


      INTEGER(KIND=JWIM) :: IJ, M
      INTEGER(KIND=JWIM) :: ICHNK, KIJS, KIJL, IJSB, IJLB

      REAL(KIND=JWRB) :: ZHOOK_HANDLE

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('PROENVHALO',0,ZHOOK_HANDLE)

!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(ICHNK, KIJS, IJSB, KIJL, IJLB, M)
      DO ICHNK = 1, NCHNK
        KIJS = 1
        IJSB = IJFROMCHNK(KIJS, ICHNK)
        KIJL = NPROMA_WAM
        IJLB = IJFROMCHNK(KIJL, ICHNK)

        WAVNUM_EXT(IJSB:IJLB, 1:NFRE_RED) = WAVNUM(KIJS:KIJL, 1:NFRE_RED, ICHNK)
        CGROUP_EXT(IJSB:IJLB, 1:NFRE_RED) = CGROUP(KIJS:KIJL, 1:NFRE_RED, ICHNK)
        OMOSNH2KD_EXT(IJSB:IJLB, 1:NFRE_RED) = OMOSNH2KD(KIJS:KIJL, 1:NFRE_RED, ICHNK)

        DELLAM1_EXT(IJSB:IJLB) = DELLAM1(KIJS:KIJL, ICHNK)
        COSPHM1_EXT(IJSB:IJLB) = COSPHM1(KIJS:KIJL, ICHNK)
        DEPTH_EXT(IJSB:IJLB) = DEPTH(KIJS:KIJL, ICHNK)
        U_EXT(IJSB:IJLB) = U(KIJS:KIJL, ICHNK)
        V_EXT(IJSB:IJLB) = V(KIJS:KIJL, ICHNK)
      ENDDO
!$OMP END PARALLEL DO

!!    should be combine into one single data exchange, when we start using this option.... !!!
      CALL MPEXCHNG(WAVNUM_EXT, NFRE_RED, 1)
      WAVNUM_EXT(NSUP+1,1:NFRE_RED) = WVPRPT_LAND(1:NFRE_RED)%WAVNUM

      CALL MPEXCHNG(CGROUP_EXT, NFRE_RED, 1)
      CGROUP_EXT(NSUP+1,1:NFRE_RED) =  WVPRPT_LAND(1:NFRE_RED)%CGROUP

      CALL MPEXCHNG(OMOSNH2KD_EXT, NFRE_RED, 1)
      OMOSNH2KD_EXT(NSUP+1,1:NFRE_RED) = WVPRPT_LAND(1:NFRE_RED)%OMOSNH2KD

      CALL MPEXCHNG(DELLAM1_EXT, 1, 1)
      DELLAM1_EXT(NSUP+1) = 0.0_JWRB 

      CALL MPEXCHNG(COSPHM1_EXT, 1, 1)
      COSPHM1_EXT(NSUP+1) = 0.0_JWRB 

      CALL MPEXCHNG(DEPTH_EXT, 1, 1)
      DEPTH_EXT(NSUP+1) = BATHYMAX

      CALL MPEXCHNG(U_EXT, 1, 1)
      U_EXT(NSUP+1) = 0.0_JWRB

      CALL MPEXCHNG(V_EXT, 1, 1)
      V_EXT(NSUP+1) = 0.0_JWRB

IF (LHOOK) CALL DR_HOOK('PROENVHALO',1,ZHOOK_HANDLE)

END SUBROUTINE PROENVHALO
