SUBROUTINE PROENVHALO (IJS, IJL, NINF, NSUP,                  &
&                      WAVNUM, CGROUP, OMOSNH2KD,             &
&                      DEPTH, U, V,                           &
&                      WAVNUM_EXT, CGROUP_EXT, OMOSNH2KD_EXT, &
&                      DEPTH_EXT, U_EXT, V_EXT )

! ----------------------------------------------------------------------

!**** *PROENVHALO* - WAVE PROPGATION

!*    PURPOSE.
!     --------

!     PRODUCES ARRAYS WITH GRID POINTS VALUES AND THEIR HALO
!     FOR ENVIRONMENT VARIABLES FOR THE WAVE PROPGATION 

! -------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWPARAM , ONLY : NFRE     , NFRE_RED
      USE YOWSHAL  , ONLY : BATHYMAX , WAVNUM_LAND, CGROUP_LAND, OMOSNH2KD_LAND
      USE YOWSTAT  , ONLY : NPROMA_WAM

      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE

#include "mpexchng.intfb.h"

      INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL  ! GRID POINTS INDEXES
      INTEGER(KIND=JWIM), INTENT(IN) :: NINF, NSUP ! HALO EXTEND NINF to NSUP+1

      REAL(KIND=JWRB), DIMENSION(IJS:IJL, NFRE), INTENT(IN) :: WAVNUM  ! WAVE NUMBER
      REAL(KIND=JWRB), DIMENSION(IJS:IJL, NFRE), INTENT(IN) :: CGROUP  ! GROUP VELOCITY
      REAL(KIND=JWRB), DIMENSION(IJS:IJL, NFRE), INTENT(IN) :: OMOSNH2KD ! OMEGA / SINH(2KD)
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: DEPTH ! WATER DEPTH
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: U ! U-COMPONENT OF SURFACE CURRENT
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: V ! V-COMPONENT OF SURFACE CURRENT

      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: WAVNUM_EXT  ! WAVE NUMBER
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: CGROUP_EXT  ! GROUP VELOCITY
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1, NFRE_RED), INTENT(OUT) :: OMOSNH2KD_EXT ! OMEGA / SINH(2KD)
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: DEPTH_EXT ! WATER DEPTH
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: U_EXT ! U-COMPONENT OF SURFACE CURRENT
      REAL(KIND=JWRB), DIMENSION(NINF:NSUP+1), INTENT(OUT) :: V_EXT ! V-COMPONENT OF SURFACE CURRENT


      INTEGER(KIND=JWIM) :: IJ, M
      INTEGER(KIND=JWIM) :: JKGLO, KIJS, KIJL, NPROMA

      REAL(KIND=JWRB) :: ZHOOK_HANDLE

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('PROENVHALO',0,ZHOOK_HANDLE)

      CALL GSTATS(1430,0)

      NPROMA=NPROMA_WAM
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JKGLO,KIJS,KIJL,M,IJ) 
      DO JKGLO = IJS, IJL, NPROMA
        KIJS=JKGLO
        KIJL=MIN(KIJS+NPROMA-1,IJL)
        DO M = 1, NFRE_RED
           DO IJ = KIJS, KIJL
             WAVNUM_EXT(IJ,M) = WAVNUM(IJ,M)
             CGROUP_EXT(IJ,M) = CGROUP(IJ,M)
             OMOSNH2KD_EXT(IJ,M) = OMOSNH2KD(IJ,M)
           ENDDO
        ENDDO
        DO IJ = KIJS, KIJL
           DEPTH_EXT(IJ) = DEPTH(IJ)
           U_EXT(IJ) = U(IJ)
           V_EXT(IJ) = V(IJ)
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
      CALL GSTATS(1430,1)

!!    should be combine into one single data exchange, when we start using this option.... !!!
      CALL MPEXCHNG(WAVNUM_EXT, NFRE_RED, 1)
      WAVNUM_EXT(NSUP+1,1:NFRE_RED) = WAVNUM_LAND(1:NFRE_RED)

      CALL MPEXCHNG(CGROUP_EXT, NFRE_RED, 1)
      CGROUP_EXT(NSUP+1,1:NFRE_RED) = CGROUP_LAND(1:NFRE_RED)

      CALL MPEXCHNG(OMOSNH2KD_EXT, NFRE_RED, 1)
      OMOSNH2KD_EXT(NSUP+1,1:NFRE_RED) = OMOSNH2KD_LAND(1:NFRE_RED)

      CALL MPEXCHNG(DEPTH_EXT, 1, 1)
      DEPTH_EXT(NSUP+1) = BATHYMAX 

      CALL MPEXCHNG(U_EXT, 1, 1)
      U_EXT(NSUP+1) = 0.0_JWRB

      CALL MPEXCHNG(V_EXT, 1, 1)
      V_EXT(NSUP+1) = 0.0_JWRB

IF (LHOOK) CALL DR_HOOK('PROENVHALO',1,ZHOOK_HANDLE)

END SUBROUTINE PROENVHALO
