      SUBROUTINE MPGATHERSCFLD(IRECV, NSTART, NEND, FIELD, NLEN)

!****  *MPGATHERSCFLD* - GATHER SCALAR BLOCK DATA FIELD ONTO A SINGLE 
!****                    PROCESS 

!     J. BIDLOT    ECMWF   APRIL 1996  MESSAGE PASSING

!     PURPOSE.
!     --------

!     GATHER SCALAR BLOCK DATA CONTAINED IN ARRAY FIELD DISTRIBUTED
!     ACROSS THE DIFFERENT PROCESSES ONTO THE SINGLE PROCESS IRECV 

!*    INTERFACE.
!     ----------

!     CALL *MPGATHERSCFLD*(IRECV,NSTART,NEND,FIELD,NLEN) 

!     *IRECV*     RANK OF THE PROCESS ONTO WHICH FIELD IS COLLECTED 
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN
!     *FIELD*     INPUT/OUTPUT ARRAY CONTAINING THE FIELD
!     *NLEN*      DIMENSION OF FIELD

!     METHOD.
!     -------

!     EXTERNALS.
!     ----------
!     MPL PACKAGE :
!         MPL_GATHERV

!     REFERENCES.
!     -----------
!         NONE
! -------------------------------------------------------------------

      USE YOWMPP   , ONLY : IRANK     ,NPROC
      USE MPL_MODULE
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

!----------------------------------------------------------------------
      IMPLICIT NONE

      INTEGER :: IRECV, NLEN
      INTEGER :: IP, IJ
      INTEGER :: NST, NND 
      INTEGER, DIMENSION(NPROC) :: NSTART,NEND
      INTEGER, DIMENSION(NPROC) :: KRECVCOUNTS

      REAL :: ZHOOK_HANDLE
      REAL, DIMENSION(NLEN) :: FIELD
      REAL, ALLOCATABLE, DIMENSION(:) :: ZSENDBUF

!----------------------------------------------------------------------

      IF (LHOOK) CALL DR_HOOK('MPGATHERSCFLD',0,ZHOOK_HANDLE)

      IF((IRECV.NE.0).AND.(NPROC.NE.1)) THEN

        CALL GSTATS(674,0)
        DO IP=1,NPROC
         KRECVCOUNTS(IP)=MAX(NEND(IP) - NSTART(IP)+1,0)
        ENDDO
        NST=NSTART(IRANK)
        NND=MAX(NSTART(IRANK),NEND(IRANK))

        ALLOCATE(ZSENDBUF(NST:NND))
        DO IJ=NSTART(IRANK),NEND(IRANK)
          ZSENDBUF(IJ)=FIELD(IJ)
        ENDDO

        CALL MPL_GATHERV(ZSENDBUF(NST:NND),
     &                   KRECVCOUNTS=KRECVCOUNTS,
     &                   PRECVBUF=FIELD,
     &                   KROOT=IRECV,
     &                   CDSTRING='MPGATHERSCFLD:')

        DEALLOCATE(ZSENDBUF)

        CALL MPL_BARRIER(CDSTRING='MPGATHERSCFLD:')

        CALL GSTATS(674,1)

      ENDIF

      IF (LHOOK) CALL DR_HOOK('MPGATHERSCFLD',1,ZHOOK_HANDLE)

      RETURN
      END SUBROUTINE MPGATHERSCFLD
