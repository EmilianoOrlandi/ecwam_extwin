      SUBROUTINE WVALLOC(LFDBOPIFS,LMESSPIFS,LDWCOUIFS,LDWCOU2W)

! ----------------------------------------------------------------------

!**** *WVALLOC* - WAVE MODEL ALLOCATION AND COUPLING TO IFS.

!     J. DOYLE     ECMWF       NOVEMBER 1996 ATMOSPHERIC COUPLING

!     MODIFIED BY:   B. HANSEN    APRIL 1997
!                                 DOCTOR STYLE AND LDWCOU2W
!                    S. ABDALLA   ECMWF  OCTOBER  2001
!                                 INCLUSION OF AIR DENSITY & Zi/L

! ----------------------------------------------------------------------

      USE YOWCOUP  , ONLY : LWCOU    ,LWCOU2W
      USE YOWCOUT  , ONLY : LFDB
      USE YOWMEAN  , ONLY : EMEAN    ,FMEAN    ,THQ      ,AKMEAN
      USE YOWMESPAS, ONLY : LMESSPASS
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NPREVIOUS,NNEXT    ,
     &            NINF     ,NSUP     ,MPMAXLENGTH
      USE YOWPARAM , ONLY : NANG     ,NFRE     ,NBLO     ,NIBLO    ,
     &            LL1D
      USE YOWSPEC, ONLY : NSTART   ,NEND     ,
     &            U10NEW   ,U10OLD   ,THWNEW   ,THWOLD   ,USNEW    ,
     &            USOLD    ,Z0NEW    ,Z0OLD    ,TAUW     ,FCONST   ,
     &            ROAIRN   ,ROAIRO   ,ZIDLNEW  ,ZIDLOLD  ,
     &            FL1      ,FL2      ,FL3      ,SL
      USE MPL_MODULE
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      LOGICAL LFDBOPIFS,LMESSPIFS,LDWCOUIFS, LDWCOU2W
      LOGICAL :: LLIRANK 
      LOGICAL LFRST
      REAL ZHOOK_HANDLE

      DATA LFRST /.TRUE./


      IF (LHOOK) CALL DR_HOOK('WVALLOC',0,ZHOOK_HANDLE)
!     READ FIRST THE WAVE MODEL NAMELIST TO GET THE WAVE MODEL
!     DECOMPOSITION (LL1D).

!!!!! INPUT UNIT FOR THE USER FILE IS NOW 26 (see also *INITMDL*) 
      IU05 = 26

      IF(LFRST) THEN
        CALL MPUSERIN(IU05,LMESSPASS,LFDB,LWCOU,LL1D)
        LFRST=.FALSE.
      ENDIF

!!!   LMESSPASS,LFDB and LWCOU will take the values provided by the IFS
!!!   see below.


! INITIALIZE LOGICALS FROM IFS

      LFDB=LFDBOPIFS
      LMESSPASS=LMESSPIFS
      LWCOU=LDWCOUIFS
      LWCOU2W=LDWCOU2W


      IF(.NOT.ALLOCATED(NSTART)) THEN
        IF(LMESSPASS) THEN
          IU06=20
        ELSE
          IU06=6
        ENDIF

        IF(LMESSPASS) THEN

!     0.1 GET RANK AND TOTAL NUMBER OF PROCESSORS  
!         ---------------------------------------

          IRANK = MPL_MYRANK()
          NPROC = MPL_NPROC()
        ELSE
          IRANK = 1 
          NPROC = 1 
        ENDIF

        NPREVIOUS=IRANK-1
        IF(IRANK.EQ.NPROC) THEN
          NNEXT=0
        ELSE
          NNEXT=IRANK+1
        ENDIF

!     0.2 DETERMINE GRID DOMAIN DECOMPOSITION 
!         -----------------------------------
        NPR=NPROC
        LLIRANK=.FALSE.
        CALL MPDECOMP(NPR,MAXLEN,LLIRANK)
        MPMAXLENGTH=MAXLEN

      ENDIF

!     1.  ALLOCATE NECESSARY ARRAYS
!         -------------------------

!     !!!! NOTE THAT THIS ROUTINE IS ONLY USED FOR COUPLED RUNS !!!
!     !!!! SEE PROGRAM chief FOR UNCOUPLED RUNS !
!     !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!     write(0,*) "JJJ U10OLD =",ALLOCATED(U10OLD)

      IF(.NOT.ALLOCATED(U10OLD)) ALLOCATE(U10OLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(THWOLD)) ALLOCATE(THWOLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(USOLD)) ALLOCATE(USOLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(Z0OLD)) ALLOCATE(Z0OLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(TAUW)) ALLOCATE(TAUW(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(U10NEW)) ALLOCATE(U10NEW(NINF:NSUP))
      IF(.NOT.ALLOCATED(THWNEW)) ALLOCATE(THWNEW(NINF:NSUP))
      IF(.NOT.ALLOCATED(USNEW)) ALLOCATE(USNEW(NINF:NSUP))
      IF(.NOT.ALLOCATED(Z0NEW)) ALLOCATE(Z0NEW(NINF:NSUP))

      IF(.NOT.ALLOCATED(ROAIRO)) ALLOCATE(ROAIRO(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(ROAIRN)) ALLOCATE(ROAIRN(NINF:NSUP))
      IF(.NOT.ALLOCATED(ZIDLOLD)) ALLOCATE(ZIDLOLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(ZIDLNEW)) ALLOCATE(ZIDLNEW(NINF:NSUP))

      IF(.NOT.ALLOCATED(FL1)) ALLOCATE(FL1(NINF-1:NSUP,NANG,NFRE))
      IF(.NOT.ALLOCATED(FL3)) ALLOCATE(FL3(NINF-1:NSUP,NANG,NFRE))

      IF(.NOT.LMESSPASS.AND..NOT.ALLOCATED(FL2))
     &   ALLOCATE(FL2(NINF-1:NSUP,NANG,NFRE))

      IF(.NOT.ALLOCATED(SL)) ALLOCATE(SL(NINF-1:NSUP,NANG,NFRE))
      IF(.NOT.ALLOCATED(FCONST)) ALLOCATE(FCONST(NINF:NSUP,NFRE))

      IF(.NOT.ALLOCATED(EMEAN))
     &  ALLOCATE(EMEAN(NSTART(IRANK):NEND(IRANK)))
      IF(.NOT.ALLOCATED(FMEAN))
     &  ALLOCATE(FMEAN(NSTART(IRANK):NEND(IRANK)))
      IF(.NOT.ALLOCATED(THQ)) ALLOCATE(THQ(NSTART(IRANK):NEND(IRANK)))
      IF(.NOT.ALLOCATED(AKMEAN))
     &  ALLOCATE(AKMEAN(NSTART(IRANK):NEND(IRANK)))

      IF (LHOOK) CALL DR_HOOK('WVALLOC',1,ZHOOK_HANDLE)
 
      RETURN
      END SUBROUTINE WVALLOC
