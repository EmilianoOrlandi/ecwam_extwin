      SUBROUTINE WVALLOC(LFDBOPIFS,LMESSPIFS,LDWCOUIFS,LDWCOU2W,LDWFLUX,
     &                   LWCUR,LFRSTIM)

! ----------------------------------------------------------------------

!**** *WVALLOC* - WAVE MODEL ALLOCATION AND COUPLING TO IFS.

!     J. DOYLE     ECMWF       NOVEMBER 1996 ATMOSPHERIC COUPLING

!     MODIFIED BY:   B. HANSEN    APRIL 1997
!                                 DOCTOR STYLE AND LDWCOU2W
!                    S. ABDALLA   ECMWF  OCTOBER  2001
!                                 INCLUSION OF AIR DENSITY & Zi/L

! ----------------------------------------------------------------------

      USE YOWCOUP  , ONLY : LWCOU    ,LWCOU2W  ,LWFLUX
      USE YOWCOUT  , ONLY : LFDB
      USE YOWCURR  , ONLY : U        ,V
      USE YOWICE   , ONLY : CICOVER  ,CITHICK  ,CIWA
      USE YOWMEAN  , ONLY : EMEAN    ,FMEAN    ,THQ      ,PHIEPS   ,
     &                      PHIAW    ,TAUOC
      USE YOWMESPAS, ONLY : LMESSPASS
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NPREVIOUS,NNEXT    ,
     &            NINF     ,NSUP     ,MPMAXLENGTH
      USE YOWPARAM , ONLY : NANG     ,NFRE     ,NBLO     ,NIBLO    ,
     &            LL1D
      USE YOWSPEC, ONLY : NSTART   ,NEND     ,
     &            U10NEW   ,U10OLD   ,THWNEW   ,THWOLD   ,USNEW    ,
     &            USOLD    ,Z0NEW    ,Z0OLD    ,TAUW     ,BETAOLD  ,
     &            ROAIRN   ,ROAIRO   ,ZIDLNEW  ,ZIDLOLD  ,
     &            FL1      ,FL2      ,FL3      ,SL
      USE YOWSTAT  , ONLY : IPROPAGS ,LSUBGRID ,IREFRA   ,IDELPRO
      USE YOWTEST  , ONLY : IU06
      USE MPL_MODULE
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

      USE YOW_RANK_GLOLOC, ONLY : MyRankGlobal

! ----------------------------------------------------------------------

      IMPLICIT NONE

      INTEGER :: NPR, MAXLEN

      REAL :: ZHOOK_HANDLE

      LOGICAL :: LFDBOPIFS,LMESSPIFS,LDWCOUIFS, LDWCOU2W, LDWFLUX
      LOGICAL :: LLIRANK 
      LOGICAL :: LWCUR
      LOGICAL :: LFRSTIM

#ifdef ECMWF 
      IF (LHOOK) CALL DR_HOOK('WVALLOC',0,ZHOOK_HANDLE)
#endif
!     READ FIRST THE WAVE MODEL NAMELIST TO GET THE WAVE MODEL
!     DECOMPOSITION (LL1D).

!!!   LMESSPASS,LFDB and LWCOU will take the values provided by the IFS
!!!   see below.


! INITIALIZE LOGICALS FROM IFS

      LFDB=LFDBOPIFS
      LMESSPASS=LMESSPIFS
      LWCOU=LDWCOUIFS
      LWCOU2W=LDWCOU2W
      LWFLUX=LDWFLUX

      IF(LFRSTIM) THEN
        IF(LMESSPASS) THEN
          IU06=20
        ELSE
          IU06=6
        ENDIF

        IF(LMESSPASS) THEN

!     0.1 GET RANK AND TOTAL NUMBER OF PROCESSORS  
!         ---------------------------------------

          IRANK = MPL_MYRANK()
          NPROC = MPL_NPROC()
        ELSE
          IRANK = 1 
          NPROC = 1 
        ENDIF

#ifndef MODEL_COUPLING_ATM_WAV
        MyRankGlobal=IRANK
#endif

        NPREVIOUS=IRANK-1
        IF(IRANK.EQ.NPROC) THEN
          NNEXT=0
        ELSE
          NNEXT=IRANK+1
        ENDIF

!     0.2 DETERMINE GRID DOMAIN DECOMPOSITION 
!         -----------------------------------
        NPR=NPROC
        LLIRANK=.FALSE.
        CALL MPDECOMP(NPR,MAXLEN,LLIRANK)
        MPMAXLENGTH=MAXLEN

      ENDIF

!     1.  ALLOCATE NECESSARY ARRAYS
!         -------------------------

!     !!!! NOTE THAT THIS ROUTINE IS ONLY USED FOR COUPLED RUNS !!!
!     !!!! SEE PROGRAM chief FOR UNCOUPLED RUNS !
!     !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


      IF(.NOT.ALLOCATED(U10OLD)) ALLOCATE(U10OLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(THWOLD)) ALLOCATE(THWOLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(USOLD)) ALLOCATE(USOLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(Z0OLD)) ALLOCATE(Z0OLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(TAUW)) ALLOCATE(TAUW(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(U10NEW)) ALLOCATE(U10NEW(NINF:NSUP))
      IF(.NOT.ALLOCATED(THWNEW)) ALLOCATE(THWNEW(NINF:NSUP))
      IF(.NOT.ALLOCATED(USNEW)) ALLOCATE(USNEW(NINF:NSUP))
      IF(.NOT.ALLOCATED(Z0NEW)) ALLOCATE(Z0NEW(NINF:NSUP))

      IF(.NOT.ALLOCATED(BETAOLD)) ALLOCATE(BETAOLD(NINF:NSUP))

      IF(.NOT.ALLOCATED(ROAIRO)) ALLOCATE(ROAIRO(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(ROAIRN)) ALLOCATE(ROAIRN(NINF:NSUP))
      IF(.NOT.ALLOCATED(ZIDLOLD)) ALLOCATE(ZIDLOLD(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(ZIDLNEW)) ALLOCATE(ZIDLNEW(NINF:NSUP))

      IF(.NOT.ALLOCATED(CICOVER)) ALLOCATE(CICOVER(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(CITHICK)) ALLOCATE(CITHICK(NINF:NSUP,NBLO))
      IF(.NOT.ALLOCATED(CIWA)) ALLOCATE(CIWA(NINF:NSUP,NFRE,NBLO))

      IF(.NOT.ALLOCATED(FL1)) ALLOCATE(FL1(NINF-1:NSUP,NANG,NFRE))
      IF(.NOT.ALLOCATED(FL3)) ALLOCATE(FL3(NINF-1:NSUP,NANG,NFRE))

      IF(.NOT.LMESSPASS.AND..NOT.ALLOCATED(FL2))
     &   ALLOCATE(FL2(NINF-1:NSUP,NANG,NFRE))

      IF(.NOT.ALLOCATED(SL)) ALLOCATE(SL(NINF-1:NSUP,NANG,NFRE))

      IF(.NOT.ALLOCATED(EMEAN))
     &  ALLOCATE(EMEAN(NSTART(IRANK):NEND(IRANK)))
      IF(.NOT.ALLOCATED(FMEAN))
     &  ALLOCATE(FMEAN(NSTART(IRANK):NEND(IRANK)))
      IF(.NOT.ALLOCATED(THQ)) 
     &  ALLOCATE(THQ(NSTART(IRANK):NEND(IRANK)))

        IF(.NOT.ALLOCATED(PHIEPS)) THEN 
          ALLOCATE(PHIEPS(NSTART(IRANK):NEND(IRANK)))
          PHIEPS = 0.
        ENDIF
        IF(.NOT.ALLOCATED(PHIAW)) THEN 
          ALLOCATE(PHIAW(NSTART(IRANK):NEND(IRANK)))
          PHIAW = 0.
        ENDIF
        IF(.NOT.ALLOCATED(TAUOC)) THEN
          ALLOCATE(TAUOC(NSTART(IRANK):NEND(IRANK)))
          TAUOC = 0.
        ENDIF

      IF ( (LWCOU .AND. LWCUR) .OR.
     &      IREFRA.EQ.2 .OR. IREFRA.EQ.3) THEN
        IF(.NOT.ALLOCATED(U)) THEN
          ALLOCATE(U(NINF-1:NSUP,NBLO))
          U=0.
        ENDIF
        IF(.NOT.ALLOCATED(V)) THEN
          ALLOCATE(V(NINF-1:NSUP,NBLO))
          V=0.
        ENDIF
      ENDIF

#ifdef ECMWF 
      IF (LHOOK) CALL DR_HOOK('WVALLOC',1,ZHOOK_HANDLE)
#endif
 
      RETURN
      END SUBROUTINE WVALLOC
