SUBROUTINE SAVSTRESS(WVENVI, FF_NOW, NBLKS, NBLKE, CDTPRO, CDATEF)
! ----------------------------------------------------------------------

!*    PURPOSE.
!     --------
!     WRITE BINARY RESTART STRESS/WIND FIELDS TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSTRESS(WVENVI, FF_NOW, NBLKS, NBLKE, CDTPRO, CDATEF)
!     *WVENVI*    WAVE ENVIRONMENT
!     *FF_NOW*    FORCING FIELDS AT CURRENT TIME
!     *NBLKS*     INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN.
!     *NBLKE*     INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN.
!     *CDTPRO*    CHAR*14   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*14   START DATE FORECAST.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ONTHE OUTPUT
!     PE BY CALLING MPGATHERSCFLD.

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU
      USE YOWDRVTYPE  , ONLY : ENVIRONMENT, FORCING_FIELDS

      USE YOWCOUT  , ONLY : NREAL    ,JPPFLAG  ,IPFGTBL, LRSTPARALW
      USE YOWGRID  , ONLY : IJSLOC   ,IJLLOC   ,IJGLOBAL_OFFSET,    &
 &                          NPROMA_WAM, NCHNK, KIJL4CHNK, IJFROMCHNK
      USE YOWMPP   , ONLY : IRANK    ,NPROC
      USE YOWPARAM , ONLY : NIBLO
      USE YOWTEST  , ONLY : IU06
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL

      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "expand_string.intfb.h"
#include "grstname.intfb.h"
#include "mpgatherscfld.intfb.h"
#include "writestress.intfb.h"

      INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL
      TYPE(ENVIRONMENT), DIMENSION(NPROMA_WAM, NCHNK), INTENT(IN) :: WVENVI
      TYPE(FORCING_FIELDS), DIMENSION(NPROMA_WAM, NCHNK), INTENT(IN) :: FF_NOW
      INTEGER(KIND=JWIM), DIMENSION(NPROC),INTENT(IN) :: NBLKS, NBLKE
      CHARACTER(LEN=14), INTENT(IN) :: CDTPRO, CDATEF


      INTEGER(KIND=JWIM) :: IFLD, IJ, IRECV
      INTEGER(KIND=JWIM) :: IJINF, IJSUP, MIJS, MIJL, IJOFFSET
      INTEGER(KIND=JWIM) :: IPRM, ICHNK
      INTEGER(KIND=JWIM) :: LNAME
      INTEGER(KIND=JWIM) :: IFCST

      REAL(KIND=JWRB) :: ZHOOK_HANDLE
      REAL(KIND=JWRB), ALLOCATABLE,DIMENSION(:,:) :: RDUM 
      REAL(KIND=JWRB), ALLOCATABLE,DIMENSION(:,:) :: RFIELD

      CHARACTER(LEN=296) :: FILENAME

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SAVSTRESS',0,ZHOOK_HANDLE)

ASSOCIATE(UCUR => WVENVI%UCUR, &
 &        VCUR => WVENVI%VCUR, &
 &        WSWAVE => FF_NOW%WSWAVE, &
 &        WDWAVE => FF_NOW%WDWAVE, &
 &        UFRIC => FF_NOW%UFRIC, &
 &        Z0M => FF_NOW%Z0M, &
 &        Z0B => FF_NOW%Z0B, &
 &        TAUW => FF_NOW%TAUW, &
 &        TAUWDIR => FF_NOW%TAUWDIR, &
 &        AIRD => FF_NOW%AIRD, &
 &        WSTAR => FF_NOW%WSTAR, &
 &        CICOVER => FF_NOW%CICOVER, &
 &        CITHICK => FF_NOW%CITHICK )

      IRECV=1

      IF (LRSTPARALW) THEN
        IJINF=IJS
        IJSUP=IJL
        MIJS=IJINF
        MIJL=IJSUP
        IJOFFSET=0
      ELSE
        IJINF=1
        IJSUP=NIBLO
        MIJS=IJSLOC
        MIJL=IJLLOC 
        IJOFFSET=IJGLOBAL_OFFSET
      ENDIF

      ALLOCATE(RFIELD(IJINF:IJSUP,NREAL))

!     ASSIGN THE DIFFERENT RESTART FIELDS TO OUTPUT ARRAY.
!     MAKE SURE IT IS THE SAME IN GETSTRESS !
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(ICHNK, IPRM, IJ)
      DO ICHNK = 1, NCHNK
        DO IPRM = 1, KIJL4CHNK(ICHNK) 
          IJ = IJFROMCHNK(IPRM,ICHNK)
          IF (IJ >= MIJS .AND. IJ <= MIJL) THEN
            IJ = IJ + IJOFFSET
            RFIELD(IJ,1) = WSWAVE(IPRM, ICHNK)
            RFIELD(IJ,2) = WDWAVE(IPRM, ICHNK)
            RFIELD(IJ,3) = UFRIC(IPRM, ICHNK)
            RFIELD(IJ,4) = TAUW(IPRM, ICHNK)
            RFIELD(IJ,5) = TAUWDIR(IPRM, ICHNK)
            RFIELD(IJ,6) = Z0M(IPRM, ICHNK)
            RFIELD(IJ,7) = Z0B(IPRM, ICHNK)
            RFIELD(IJ,8) = AIRD(IPRM, ICHNK)
            RFIELD(IJ,9) = WSTAR(IPRM, ICHNK)
            RFIELD(IJ,10) = CICOVER(IPRM, ICHNK)
            RFIELD(IJ,11) = CITHICK(IPRM, ICHNK)
            RFIELD(IJ,12) = UCUR(IPRM, ICHNK) 
            RFIELD(IJ,13) = VCUR(IPRM, ICHNK) 
          ENDIF
        ENDDO
      ENDDO
!$OMP END PARALLEL DO

      IF (.NOT.LRSTPARALW) THEN
!       COLLECT THE DIFFERENT CONTRIBUTIONS FROM THE PROCESSES
!       SINCE THE FILES ARE NOT SAVED IN SPLIT MODE
        DO IFLD=1,NREAL
          CALL MPGATHERSCFLD(IRECV,NBLKS,NBLKE, RFIELD(IJINF:IJSUP,IFLD),IJSUP-IJINF+1) 
        ENDDO
      ENDIF

!     SAVE RESTART
      IFCST = 0
      CALL GRSTNAME(CDTPRO,CDATEF,IFCST,'LAW',ICPLEN,CPATH,FILENAME)
      IF (LRSTPARALW) THEN
!        RESTART FILES FROM ALL PS's
         LNAME = LEN_TRIM(FILENAME)
         FILENAME=FILENAME(1:LNAME)//'.%p_%n'
         CALL EXPAND_STRING(IRANK,NPROC,0,0,FILENAME,1)
      ENDIF

      IF (LRSTPARALW .OR. IRANK == IRECV) THEN
          CALL WRITESTRESS(IJINF, IJSUP, NREAL, RFIELD, FILENAME, LRSTPARALW) 
      ENDIF
      DEALLOCATE(RFIELD)

      WRITE(IU06,*) ' '
      WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
      WRITE(IU06,*) ' '
      WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL .... CDATEWO = ', CDATEWO
      WRITE(IU06,*) ' NEXT WIND FILE NAME IS ........... CDAWIFL = ', CDAWIFL
      WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT CDATEFL = ',CDATEFL
      CALL FLUSH(IU06)

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SAVSTRESS',1,ZHOOK_HANDLE)

END SUBROUTINE SAVSTRESS
