      SUBROUTINE SAVSTRESS(WSWAVE, WDWAVE, UFRIC, TAUW, TAUWDIR, Z0M, &
     &                     AIRD, WSTAR, CICOVER, CITHICK,             &
     &                     NBLKS, NBLKE, CDTPRO, CDATEF)
! ----------------------------------------------------------------------

!*    PURPOSE.
!     --------
!     WRITE BINARY RESTART STRESS/WIND FIELDS TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSTRESS(WSWAVE, WDWAVE, UFRIC, TAUW, TAUWDIR, Z0M,
!                       AIRD, WSTAR, CICOVER, CITHICK,
!                       NBLKS, NBLKE, CDTPRO, CDATEF)
!     *WSWAVE*    INTERMEDIATE STORAGE OF WIND SPEED.
!     *WDWAVE*    INTERMEDIATE STORAGE OF WIND DIRECTION. 
!     *UFRIC*     INTERMEDIATE STORAGE OF MODULUS OF FRICTION VELOCITY.
!     *TAUW*      WAVE STRESS IN (M/S)**2.
!     *TAUWDIR*   WAVE STRESS DIRECTION. 
!     *Z0M*     INTERMEDIATE STORAGE OF ROUGHNESS LENGTH IN M.
!     *AIRD*    AIR/WATER DENSITY RATIO.
!     *WSTAR*  CONVECTIVE VELOCITY.
!     *CICOVER*   SEA ICE COVER.
!     *CITHICK*   SEA ICE THICKNESS.
!     *NBLKS*     INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN.
!     *NBLKE*     INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN.
!     *CDTPRO*    CHAR*14   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*14   START DATE FORECAST.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ONTHE OUTPUT
!     PE BY CALLING MPGATHERSCFLD.

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUT  , ONLY : NREAL    ,JPPFLAG  ,IPFGTBL, LRSTPARALW
      USE YOWCURR  , ONLY : U        ,V
      USE YOWGRID  , ONLY : IJS      ,IJL      ,IJSLOC   ,IJLLOC   ,IJGLOBAL_OFFSET
      USE YOWMPP   , ONLY : IRANK    ,NPROC
      USE YOWPARAM , ONLY : NIBLO
      USE YOWSTAT  , ONLY : NPROMA_WAM
      USE YOWTEST  , ONLY : IU06
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL

      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "expand_string.intfb.h"
#include "grstname.intfb.h"
#include "mpgatherscfld.intfb.h"
#include "writestress.intfb.h"

      REAL(KIND=JWRB),DIMENSION(IJS:IJL),INTENT(IN) :: WSWAVE, WDWAVE
      REAL(KIND=JWRB),DIMENSION(IJS:IJL),INTENT(IN) :: UFRIC, TAUW, TAUWDIR, Z0M 
      REAL(KIND=JWRB),DIMENSION(IJS:IJL),INTENT(IN) :: AIRD, WSTAR, CICOVER, CITHICK 
      INTEGER(KIND=JWIM), DIMENSION(NPROC),INTENT(IN) :: NBLKS, NBLKE
      CHARACTER(LEN=14), INTENT(IN) :: CDTPRO, CDATEF


      INTEGER(KIND=JWIM) :: IFLD, IJ, IRECV
      INTEGER(KIND=JWIM) :: IJINF, IJSUP, MIJS, MIJL, IJOFFSET
      INTEGER(KIND=JWIM) :: JKGLO, KIJS, KIJL, NPROMA
      INTEGER(KIND=JWIM) :: LNAME

      REAL(KIND=JWRB) :: ZHOOK_HANDLE
      REAL(KIND=JWRB), ALLOCATABLE,DIMENSION(:,:) :: RDUM 
      REAL(KIND=JWRB), ALLOCATABLE,DIMENSION(:,:) :: RFIELD

      CHARACTER(LEN=296) :: FILENAME

      LOGICAL :: LUAL, LVAL
! ----------------------------------------------------------------------

      IF (LHOOK) CALL DR_HOOK('SAVSTRESS',0,ZHOOK_HANDLE)

      IRECV=1

      IF (LRSTPARALW) THEN
        IJINF=IJS
        IJSUP=IJL
        MIJS=IJINF
        MIJL=IJSUP
        IJOFFSET=0
      ELSE
        IJINF=1
        IJSUP=NIBLO
        MIJS=IJSLOC
        MIJL=IJLLOC 
        IJOFFSET=IJGLOBAL_OFFSET
      ENDIF

      ALLOCATE(RFIELD(IJINF:IJSUP,NREAL))

!     ASSIGN THE DIFFERENT RESTART FIELDS TO OUTPUT ARRAY.
!     MAKE SURE IT IS THE SAME IN GETSTRESS !
      NPROMA=NPROMA_WAM
      LUAL=ALLOCATED(U)
      LVAL=ALLOCATED(V)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JKGLO,KIJS,KIJL,IJ)
      DO JKGLO=MIJS,MIJL,NPROMA
        KIJS=JKGLO
        KIJL=MIN(KIJS+NPROMA-1,MIJL)
        DO IJ=KIJS+IJOFFSET,KIJL+IJOFFSET
          RFIELD(IJ,1) = WSWAVE(IJ)
          RFIELD(IJ,2) = WDWAVE(IJ)
          RFIELD(IJ,3) = UFRIC(IJ)
          RFIELD(IJ,4) = TAUW(IJ)
          RFIELD(IJ,5) = TAUWDIR(IJ)
          RFIELD(IJ,6) = Z0M(IJ)
          RFIELD(IJ,7) = AIRD(IJ)
          RFIELD(IJ,8) = WSTAR(IJ)
          RFIELD(IJ,9) = CICOVER(IJ)
          RFIELD(IJ,10) = CITHICK(IJ)
          IF (LUAL) THEN
            RFIELD(IJ,11) = U(IJ) 
          ELSE
            RFIELD(IJ,11) = 0.0_JWRB
          ENDIF
          IF (LVAL) THEN
            RFIELD(IJ,12) = V(IJ) 
          ELSE
            RFIELD(IJ,12) = 0.0_JWRB
          ENDIF
        ENDDO
      ENDDO
!$OMP END PARALLEL DO

      IF (.NOT.LRSTPARALW) THEN
!       COLLECT THE DIFFERENT CONTRIBUTIONS FROM THE PROCESSES
!       SINCE THE FILES ARE NOT SAVED IN SPLIT MODE
        DO IFLD=1,NREAL
          CALL MPGATHERSCFLD(IRECV,NBLKS,NBLKE, RFIELD(IJINF:IJSUP,IFLD),IJSUP-IJINF+1) 
        ENDDO
      ENDIF


!     SAVE RESTART
      CALL GRSTNAME(CDTPRO,CDATEF,'LAW',ICPLEN,CPATH,FILENAME)
      IF (LRSTPARALW) THEN
!        RESTART FILES FROM ALL PS's
         LNAME = LEN_TRIM(FILENAME)
         FILENAME=FILENAME(1:LNAME)//'.%p_%n'
         CALL EXPAND_STRING(IRANK,NPROC,0,0,FILENAME,1)
      ENDIF

      IF (LRSTPARALW .OR. IRANK == IRECV) THEN
          CALL WRITESTRESS(IJINF, IJSUP, NREAL, RFIELD, FILENAME, LRSTPARALW) 
      ENDIF
      DEALLOCATE(RFIELD)

      WRITE(IU06,*) ' '
      WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
      WRITE(IU06,*) ' '
      WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL ....',              &
     & ' CDATEWO = ', CDATEWO
      WRITE(IU06,*) ' NEXT WIND FILE NAME IS ...........',              &
     & ' CDAWIFL = ', CDAWIFL
      WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT',              &
     & ' CDATEFL = ',CDATEFL
      CALL FLUSH(IU06)

      IF (LHOOK) CALL DR_HOOK('SAVSTRESS',1,ZHOOK_HANDLE)

      END SUBROUTINE SAVSTRESS
