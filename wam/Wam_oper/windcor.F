      SUBROUTINE WINDCOR(
!                DIMENSION
     &                NSPECW,MPART,
!                INPUT
     &                EW,XMEANFREW,XMEANANGW,
     &                EA,XFREA,XANGA,
     &                NINTW, WSPMIN, LOTEST, IU06,
!                MODIFIED
     &                UW10,THWW,DELUW10,DUU,DUV)

! ----------------------------------------------------------------------

!    S.HASSELMANN, MPM, HAMBURG 3/94.
!    R.BROKOPF, MPM, HAMBURG 3/94.
!    P.LIONELLO (UNIV. OF PADUA), HAMBURG 3/94

!     PURPOSE.
!     --------
!     CORRECT LOCAL WINDS FROM LOCAL WAVE CORRECTIONS.

!     EXTERNALS.
!     ----------
!      U10COR - COMPUTES RELATION BETWEEN U10 AND ENERGY , 
!               MEAN FREQUENCY.

!     METHOD:
!     -------
!     A DIMENSIONLESS RELATION IN THE FORM
!          E10= A * FR10 **B,                            (1)
!     IT IS ASSUMED WITH
!          E10=E G**2/U10**4  (DIMENSIONLESS ENERGY)     (2)
!     AND
!          FR10=FR * U10/G (DIMENSIONLESS FREQUENCY).    (3)
!     U10 IS THE WIND SPEED AT 10M., I.E. THE INPUT OF WAM.

!     SUBSTITUITING E10 AND FR10 IN (1) ACCORDING TO (2) AND (3),
!     ONE HAS AN EQUATION FOR THE UNKNOWN U10. WITH GIVES

!      U10= XX**(-1/(4+B)) ; XX = A F**B / (E G**(B+2).

!     THE CONSTANT A AND B HAVE BEEN OBTAINED FROM THE WAM TIME
!     LIMITED GROWTH CURVE. A IS AN EVALUATION OBTAINED AT
!     20M/S, AM AT 10M/S. THE WIND SPEED IS ESTIMATED FROM A
!     WEIGHTED AVERAGE OF THE TWO EVALUATION.

!     THIS ESTIMATE  IS APPLIED TO THE FIRST GUESS WIND-SEA SPECTRUM,
!     OBTAINING U10OE. IF THE AGREEMENT WITH U10OLD, THAT IS ACTUALLY 
!     USED BY THE MODEL IS POOR (NOTE THAT THE ANALYSIS OF THE WIND IS
!     CARRIED OUT ALSO IN THE MIXED WINDSEA-SWELL SITUATION), THEN
!     NO ANALYSIS IS CARRIED OUT. IF THE AGREMENT IS ACCEPTABLE,
!     THEN THE ESTIMATE IS APPLIED TO THE ANALYSED WINDSEA OBTAINING
!     U10NE.
!     IN ORDER TO AVOID A TOO STRONG DEPENDANCE ON THE GROWTH CURVE
!     USED FOR THE ESTIMATE,THE DEVIATION DU10=U10NE-U10OE IS USED:
!     U10AN=U10OLD+DU10
!     WHERE U10AN IS THE ANALYSED WIND SPEED.
!    

! ----------------------------------------------------------------------

      IMPLICIT NONE

!     INTERFACE

      INTEGER 
     &   NSPECW, MPART

      REAL 
     &   EW(NSPECW,MPART),
     &   XMEANFREW(NSPECW,MPART),
     &   XMEANANGW(NSPECW,MPART),
     &   EA(NSPECW,MPART),
     &   XFREA(NSPECW,MPART),
     &   XANGA(NSPECW,MPART),
     &   UW10(NSPECW),
     &   THWW(NSPECW),
     &   DELUW10(NSPECW),
     &   DUU(NSPECW),
     &   DUV(NSPECW)

      REAL 
     &   U10FG(NSPECW),
     &   U10A(NSPECW)

      INTEGER 
     &   NINTW(NSPECW)
      REAL
     &  UVXW,UVYW,UVXO,UVYO,UVXA,UVYA,U10XC,U10YC,WSPMIN
 
      LOGICAL LOTEST

      INTEGER IU06

!     LOCAL VARIABLES:
      INTEGER ISPEC,JPART

      REAL U10C,UDF

! ----------------------------------------------------------------------

      IF(LOTEST) WRITE(IU06,*)' INSIDE WINDCOR '

!*    ESTIMATE OF THE FIRST GUESS U10.
!     --------------------------------

      CALL U10COR (U10FG, XMEANFREW, EW, NSPECW, MPART, NINTW, UW10,
     &             WSPMIN)

!*    ESTIMATE OF THE ANALYSED WIND SPEED.
!     ------------------------------------

      CALL U10COR (U10A, XFREA, EA, NSPECW, MPART, NINTW, UW10,WSPMIN)

!*    COMPUTE WIND CORRECTIONS.
!     -------------------------

      DO ISPEC=1,NSPECW
        IF(NINTW(ISPEC).NE.0) THEN
            JPART=NINTW(ISPEC)
            UVXW=U10FG(ISPEC)*COS(XMEANANGW(ISPEC,JPART))
            UVYW=U10FG(ISPEC)*SIN(XMEANANGW(ISPEC,JPART))
            UVXO=UW10(ISPEC)*COS(THWW(ISPEC))
            UVYO=UW10(ISPEC)*SIN(THWW(ISPEC))
            UDF=ABS(U10FG(ISPEC)-UW10(ISPEC))
            UVXA=U10A(ISPEC)*COS(XANGA(ISPEC,JPART))
            UVYA=U10A(ISPEC)*SIN(XANGA(ISPEC,JPART))
            DUU(ISPEC)=UVXA-UVXW
            DUV(ISPEC)=UVYA-UVYW
            U10XC=UVXA-UVXW+UVXO
            U10YC=UVYA-UVYW+UVYO

!           COMPUTE CORRECTED WIND SPEED AND DIRECTION.
!           -------------------------------------------

            U10C=SQRT(U10XC**2+U10YC**2)
            DELUW10(ISPEC)=U10C-UW10(ISPEC)
            UW10(ISPEC)=U10C
            THWW(ISPEC)=ATAN2(U10YC,U10XC)
        ELSE
            DUU(ISPEC)=0.
            DUV(ISPEC)=0.
            DELUW10(ISPEC)=0.
        END IF
      END DO

      RETURN
      END SUBROUTINE WINDCOR
