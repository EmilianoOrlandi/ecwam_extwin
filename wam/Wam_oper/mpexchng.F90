      SUBROUTINE MPEXCHNG(FLD, NDIM2, NDIM3)


!****  *MPEXCHNG* - EXHANGES MESSAGE BETWEEN THE PROCESS IRANK
!****               AND THE ALL PE's THAT CONTAINS INFORMATION NEEDED
!                   FOR THE ADVECTION OR THE CALCULATION OF THE
!                   CURRENT VELOCITY GRADIENT. 

!     J. BIDLOT    ECMWF   MARCH 1996  MESSAGE PASSING
!                  ECMWF   JANUARY 2004 MADE THE INTERFACE MORE GENERAL

!     PURPOSE.
!     --------
!     EXHANGE MESSAGE BETWEEN ONE PROCESS AND ITS NEIGHBOURS (HALO). 

!*    INTERFACE.
!     ----------
!     CALL *MPEXCHNG*(FLD,NDIM2,NDIM3)

!      *FLD*       ARRAY TO EXCHANGE 
!                  THE FIRST DIMENSION IS ALWAYS GIVEN AS NINF-1:NSUP
!                  BECAUSE OF THE HALO CONFIGURATION (SEE MPDECOMP)
!      *NDIM2*     SECOND DIMENSION OF FLD.
!      *NDIM3*     THIRD DIMENSION OF FLD.

!     METHOD.
!     -------

!     NOW UPDATED TO USE NON-BLOCKING SENDS AND RECEIVES


!     EXTERNALS.
!     ----------
!     MPL PACKAGE :
!         MPL_SEND
!         MPL_RECV
!         MPL_ABORT

!     REFERENCES.
!     -----------
!     CHAPTER 4 OF
!     USING MPI, PORTABLE PARALLEL PROGRAMMING WITH THE MESSAGE PASSING
!     INTERFACE. W.CROPP, E LUSK, A SKJELLUM. MIT PRESS 1995

! -------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NINF     ,NSUP     ,    &
     &          KTAG
      USE YOWSPEC, ONLY   : NTOPE    ,NTOPEMAX ,IJTOPE   ,NGBTOPE  ,    &
     &          NTOPELST   ,NFROMPE  ,NFROMPEMAX,NIJSTART,NGBFROMPE,    &
     &          NFROMPELST
      USE MPL_MODULE

!----------------------------------------------------------------------

      IMPLICIT NONE

      INTEGER(KIND=JWIM), INTENT(IN) :: NDIM2, NDIM3
      REAL(KIND=JWRB), DIMENSION(NINF-1:NSUP,NDIM2,NDIM3), INTENT(INOUT) :: FLD

      INTEGER(KIND=JWIM) :: INGB, M, K, IH, IJ, KCOUNT, IPROC, IR
      INTEGER(KIND=JWIM) :: NBUFMAX
      INTEGER(KIND=JWIM), DIMENSION(NGBTOPE+NGBFROMPE) :: IREQ

      REAL(KIND=JWRB) :: ZHOOK_HANDLE
      REAL(KIND=JWRB) :: ZDUM(2)
      REAL(KIND=JWRB), ALLOCATABLE :: ZCOMBUFS(:,:)
      REAL(KIND=JWRB), ALLOCATABLE :: ZCOMBUFR(:,:)

      LOGICAL :: LLOK

!----------------------------------------------------------------------

      IF (LHOOK) CALL DR_HOOK('MPEXCHNG',0,ZHOOK_HANDLE)

      IF (NPROC.LE.1) THEN
        IF (LHOOK) CALL DR_HOOK('MPEXCHNG',1,ZHOOK_HANDLE)
        RETURN
      ENDIF

      CALL GSTATS_BARRIER(736)

      NBUFMAX=MAX(NTOPEMAX,NFROMPEMAX)*NDIM2*NDIM3
      ALLOCATE(ZCOMBUFS(NBUFMAX,NGBTOPE))
      ALLOCATE(ZCOMBUFR(NBUFMAX,NGBFROMPE))


!     PACK SEND BUFFERS FOR NGBTOPE NEIGHBOURING PE's
!     -------------------------------------------------
      CALL GSTATS(1892,0)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(INGB,IPROC,KCOUNT,M,K,IH,IJ)
      DO INGB=1,NGBTOPE
        IPROC=NTOPELST(INGB)
        KCOUNT=0
        DO M=1,NDIM3
          DO K=1,NDIM2
            DO IH=1,NTOPE(IPROC)
              IJ=IJTOPE(IH,IPROC)
              KCOUNT=KCOUNT+1
              ZCOMBUFS(KCOUNT,INGB)=FLD(IJ,K,M)
            ENDDO
          ENDDO
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
      CALL GSTATS(1892,1)

!     DO NON BLOCKING SENDS AND RECVS

      IR=0
      CALL GSTATS(676,0)

      DO INGB=1,NGBFROMPE
        IR=IR+1
        IPROC=NFROMPELST(INGB)
        KCOUNT=NDIM3*NDIM2*NFROMPE(IPROC)
        CALL MPL_RECV(ZCOMBUFR(1:KCOUNT,INGB),KSOURCE=IPROC,KTAG=KTAG,  &
     &     KMP_TYPE=JP_NON_BLOCKING_STANDARD,KREQUEST=IREQ(IR),         &
     &     CDSTRING='MPEXCHNG:')
      ENDDO

      DO INGB=1,NGBTOPE
        IR=IR+1
        IPROC=NTOPELST(INGB)
        KCOUNT=NDIM3*NDIM2*NTOPE(IPROC)
        CALL MPL_SEND(ZCOMBUFS(1:KCOUNT,INGB),KDEST=IPROC,KTAG=KTAG,    &
     &     KMP_TYPE=JP_NON_BLOCKING_STANDARD,KREQUEST=IREQ(IR),         &
     &     CDSTRING='MPEXCHNG:')
      ENDDO

!     NOW WAIT FOR ALL TO COMPLETE

      CALL MPL_WAIT(KREQUEST=IREQ(1:IR),CDSTRING='MPEXCHNG:')

      CALL GSTATS(676,1)

!     DECODE THE RECEIVED BUFFERS

      CALL GSTATS(1893,0)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(INGB,IPROC,KCOUNT,M,K,IH,IJ)
      DO INGB=1,NGBFROMPE
        IPROC=NFROMPELST(INGB)
        KCOUNT=0
        DO M=1,NDIM3
          DO K=1,NDIM2
            DO IH=1,NFROMPE(IPROC)
              IJ=NIJSTART(IPROC)+IH-1
              KCOUNT=KCOUNT+1
              FLD(IJ,K,M)=ZCOMBUFR(KCOUNT,INGB)
            ENDDO
          ENDDO
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
      CALL GSTATS(1893,1)

      KTAG=KTAG+1

      DEALLOCATE(ZCOMBUFS)
      DEALLOCATE(ZCOMBUFR)

      IF (LHOOK) CALL DR_HOOK('MPEXCHNG',1,ZHOOK_HANDLE)

      END SUBROUTINE MPEXCHNG
