SUBROUTINE WVCOUPLE_UPDATE_GRIB_HANDLES(YREWCOU,YDGEOMETRY, YDRIP)

!----------------------------------------------------------------------

!**** *WVCOUPLE_UPDATE_GRIB_HANDLES* 

!     J. HAWKES    ECMWF NVOEMBER 2017

!*    PURPOSE.
!     --------
!     Initialize and update GRIB handles for WAM when coupled to IFS.

!**   INTERFACE.
!     ----------

!     SUBROUTINE WVCOUPLE_UPDATE_GRIB_HANDLES
!                INPUT: YDGEOMETRY
!                OUTPUT:

!     METHOD.
!     -------

!     EXTERNALS.
!     ----------
!     WVCOUPLE_INIT_EARLY
!     WVXF2GB

!     REFERENCE.
!     ----------
!     EXTRACTED FROM WVXF2FB
!

!-------------------------------------------------------------------

	USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK
	USE PARKIND1 , ONLY : JPIM, JPRB
	USE YOEWCOU  , ONLY : TEWCOU
	USE YOMCT3   , ONLY : NSTEP
    USE YOMRIP   , ONLY : TRIP
    USE GEOMETRY_MOD , ONLY : GEOMETRY
	USE IOSTREAM_MIX, ONLY : NGRIB_HANDLE_GG
	USE GRIB_API_INTERFACE, ONLY : IGRIB_CLONE
	USE GRIB_API_INTERFACE, ONLY : IGRIB_SET_VALUE
	USE GRIB_UTILS_MOD,ONLY : GRIB_SET_PARAMETER

	USE YOMDYNCORE, ONLY: LPPSTEPS

	IMPLICIT NONE

	TYPE(TEWCOU)   ,INTENT(INOUT)    :: YREWCOU
	TYPE(GEOMETRY)  ,INTENT(IN)    :: YDGEOMETRY
	TYPE(TRIP)    	,INTENT(IN)    :: YDRIP
	REAL :: ZHOOK_HANDLE

	!TEST
	INTEGER(KIND=JPIM) :: ILEV, IGRIBCD, IDUMMY(1)
	CHARACTER (LEN=2) :: CLREPR
	CHARACTER (LEN=3) :: CLTYPE
	INTEGER(KIND=JPIM) :: ITOP_DUM,IBOT_DUM
	LOGICAL :: LLGRAD_DUM
	REAL(KIND=JPRB) :: ZDUMMY(1)
	INTEGER(KIND=JPIM) :: ISTEP_OUT

!-------------------------------------------------------------------

#ifdef ECMWF 
      IF (LHOOK) CALL DR_HOOK('WVCOUPLE_UPDATE_GRIB_HANDLES',0,ZHOOK_HANDLE)
#endif

	ASSOCIATE( &
		& LWINIT=>YREWCOU%LWINIT, &
		& TSTEP=>YDRIP%TSTEP, &
		& KSTPW=>YREWCOU%NSTPW &
	&)


	CALL GSTATS(1714,0)
	IF (.NOT.LWINIT) THEN
		! INITIALISE GRIB HANDLE FOR WAM
		IF(NGRIB_HANDLE_GG < 0 ) THEN
			CALL ABOR1("WVXF2GB: PROBLEM NGRIB_HANDLE_GG < 0 ")
		ELSE
			YREWCOU%NGRIB_HANDLE_FOR_WAM=-99
			CALL IGRIB_CLONE(NGRIB_HANDLE_GG,YREWCOU%NGRIB_HANDLE_FOR_WAM)
		ENDIF
	ENDIF

	! UPDATE GRIB HANDLE FOR WAM
	! ONLY THE FIRST TIME AND EVERY HOUR (3 or 6) for long runs AFTER THAT 
	! The wave model does not output more often than every hour
	! (it will be longer it TSEP is not a multiple of one hour)
	! We could do it all the time but then there is a potential
	! problem in *GRIB_CODE_MESSAGE* whereby grib_api is unable
	! to find an appropriate time unit when coding the step (it is a GRIB-1 limitation)  
	! There is another GRIB-1 limitation that limit the forecast to be less than 2**16=65536
	! whatever the time unit is. This limits the hourly output to (2**16-1) hours
	! Instead, one can hope to have 3 hourly output up to (2**16-1)*3 hours
	! Followed by 6 hourly output up to 2**16-1)*6 hours and so on.
	IF(MOD(3600,NINT(TSTEP)) == 0 .AND. NINT(NSTEP*(TSTEP/3600._JPRB)) <= 65535) THEN 
		ISTEP_OUT=MAX(NINT(3600._JPRB/TSTEP),1)
	ELSEIF(MOD(10800,NINT(TSTEP)) == 0 .AND. NINT(NSTEP*(TSTEP/10800._JPRB)) <= 65535 ) THEN 
		ISTEP_OUT=MAX(NINT(10800._JPRB/TSTEP),1)
	ELSEIF(MOD(21600,NINT(TSTEP)) == 0 .AND. NINT(NSTEP*(TSTEP/21600._JPRB)) <= 65535 ) THEN 
		ISTEP_OUT=MAX(NINT(21600._JPRB/TSTEP),1)
	ELSEIF(MOD(43200,NINT(TSTEP)) == 0 .AND. NINT(NSTEP*(TSTEP/43200._JPRB)) <= 65535 ) THEN 
		ISTEP_OUT=MAX(NINT(43200._JPRB/TSTEP),1)
	ELSEIF(MOD(86400,NINT(TSTEP)) == 0 .AND. NINT(NSTEP*(TSTEP/86400._JPRB)) <= 65535 ) THEN 
		ISTEP_OUT=MAX(NINT(86400._JPRB/TSTEP),1)
	ELSE
		ISTEP_OUT=1
	ENDIF

	IF ((NSTEP == KSTPW) .OR. MOD(NSTEP,ISTEP_OUT) == 0 .OR. LPPSTEPS ) THEN
		IGRIBCD=165
		ILEV=0
		CLREPR='GG'
		CLTYPE='SFC'
		CALL GRIB_CODE_MESSAGE(YDGEOMETRY%YRDIM,YREWCOU%NGRIB_HANDLE_FOR_WAM,IGRIBCD,ILEV,CLREPR,CLTYPE,&
			& LLGRAD_DUM,ITOP_DUM,IBOT_DUM)
		CALL GRIB_SET_PARAMETER(YREWCOU%NGRIB_HANDLE_FOR_WAM,IGRIBCD,ILEV,&
			& IDUMMY,IDUMMY,IDUMMY,IDUMMY,ZDUMMY)
		ZDUMMY=0.0_JPRB
		CALL IGRIB_SET_VALUE(YREWCOU%NGRIB_HANDLE_FOR_WAM,'values',ZDUMMY)
	ENDIF
	CALL GSTATS(1714,1)

    END ASSOCIATE

#ifdef ECMWF 
    IF (LHOOK) CALL DR_HOOK('WVCOUPLE_UPDATE_GRIB_HANDLES',1,ZHOOK_HANDLE)
#endif

      RETURN
END SUBROUTINE WVCOUPLE_UPDATE_GRIB_HANDLES 
