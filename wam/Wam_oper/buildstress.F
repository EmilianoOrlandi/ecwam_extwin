      SUBROUTINE BUILDSTRESS(U10OLD, THWOLD, USOLD, TAUW, Z0OLD,
     &                       ROAIRO, ZIDLOLD, CICOVER, CITHICK,
     &                       IREAD)

! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF   APRIL 1998 

!     J. BIDLOT    ECMWF   FEBRUARY 1999 TAUT --> SQRT(TAUT)

!     S. ABDALLA   ECMWF   OCTOBER 1999 MODIFICATION THE CALL TO GETWND
 
!     J. BIDLOT    ECMWF   AUGUST 2008 : MAKE IT MORE PARALLEL.

!*    PURPOSE.
!     --------
!     CREATES WIND AND STRESS FIELDS FROM GRIB WINDS AND CD.

!**   INTERFACE.
!     ----------
!     CALL *BUILDSTRESS*(U10OLD,THWOLD,USOLD,TAUW,Z0OLD,ROAIRO,
!    &                   ROAIRO, ZIDLOLD, CICOVER, CITHICK,
!    &                   IREAD)*
!     *U10OLD*   WIND SPEED.
!     *THWOLD*   WIND DIRECTION (RADIANS).
!     *USOLD*    FRICTION VELOCITY.
!     *TAUW*     WAVE STRESS.
!     *Z0OLD*    ROUGHNESS LENGTH IN M.
!     *RAD0OLD*   AIR DENSITY IN KG/M3.
!     *RZIDL0OLD* Zi/L (Zi: INVERSION HEIGHT, L: MONIN-OBUKHOV LENGTH).
!     *CICOVER*   SEA ICE COVER.
!     *CITHICK*   SEA ICE THICKNESS.
!     *IREAD*     PROCESSOR WHICH WILL ACCESS THE FILE ON DISK

!     METHOD.
!     -------

!     EXTERNALS.
!     ----------
!     *ABORT1*
!     *AIRSEA*
!     *GETWND*
!     *READWGRIB*

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWCOUP  , ONLY : LWCOU    ,ALPHA    ,XKAPPA   ,XNLEV, 
     &                      LWNEMOCOUCIC, LWNEMOCOUCIT
      USE YOWGRID  , ONLY : IGL      ,IJS      ,IJL
      USE YOWMPP   , ONLY : NINF     ,NSUP     ,NPROC    ,IRANK   ,
     &              KTAG
      USE YOWMESPAS, ONLY : LMESSPASS,LNOCDIN  ,LWAVEWIND
      USE YOWPARAM , ONLY : NBLO
      USE YOWPCONS , ONLY : G        ,ROAIR    ,EPSUS    ,EPSU10
      USE YOWSTAT  , ONLY : CDATEA   ,CDTPRO   ,NPROMA_WAM
      USE YOWTABL  , ONLY : ITAUMAX  ,JUMAX    ,JPLEVT   ,TAUT
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWWNDG  , ONLY : ICODE
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL  ,FIELDG   ,
     &                      NXFF     ,NYFF
      USE YOWNEMOFLDS,ONLY: NEMOCICOVER, NEMOCITHICK

      USE MPL_MODULE
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE

      INTEGER, INTENT(IN) :: IREAD
      INTEGER :: IG
      INTEGER :: ILEN, LIU, IPARAM, ILEV, KZLEVUWAVE, KZLEVCD 
      INTEGER :: IJ, MIJS, MIJL
      INTEGER :: JKGLO, KIJS, KIJL, NPROMA
      INTEGER :: NWAVEWIND(1)

      REAL, DIMENSION(NINF:NSUP,NBLO), INTENT(OUT) :: U10OLD, THWOLD
      REAL, DIMENSION(NINF:NSUP,NBLO), INTENT(OUT) :: USOLD, Z0OLD, TAUW
      REAL, DIMENSION(NINF:NSUP,NBLO), INTENT(OUT) :: ROAIRO, ZIDLOLD
      REAL, DIMENSION(NINF:NSUP,NBLO), INTENT(OUT) :: CICOVER,CITHICK

      REAL :: ZHOOK_HANDLE
      REAL :: TEMPXNLEV, CDINV, CDSQRTINV, CHARNOCK
      REAL, ALLOCATABLE :: CD(:,:), TEMPTAUT(:,:,:)

      CHARACTER(LEN=24) :: FILNM

      LOGICAL :: LLONLYPOS
      LOGICAL :: LWNDFILE, LCLOSEWND
      LOGICAL :: LCR
      LOGICAL :: LLALLOC_ONLY, LLINIALL, LLOCAL

! ----------------------------------------------------------------------

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('BUILDSTRESS',0,ZHOOK_HANDLE)
#endif

      CDATEWO = ' '
      CDAWIFL = ' '
      CDATEFL = ' '
      CDTPRO = CDATEA
      LCR=.FALSE.

!     GETWND AND READWGRIB REQUIRES FIELDG TO BE ALLOCATED !
      LLALLOC_ONLY=.FALSE.
      LLINIALL=.TRUE.
      LLOCAL=.TRUE.
      CALL INIT_FIELDG(LLALLOC_ONLY,LLINIALL,LLOCAL)

!     1.1 GET ATMOSPHERIC MODEL FORCINGS FIELDS 
!         -------------------------------------

      ILEN=1
      LWNDFILE=.TRUE.
      LCLOSEWND=.TRUE.

      IF(LWNEMOCOUCIC.OR.LWNEMOCOUCIT) THEN
        IG=1
        ALLOCATE(NEMOCICOVER(IJS(IG):IJL(IG)),
     &           NEMOCITHICK(IJS(IG):IJL(IG)))
        NEMOCICOVER(IJS(IG):IJL(IG))=0.0
        NEMOCITHICK(IJS(IG):IJL(IG))=0.0
      ENDIF
      CALL GETWND (U10OLD, THWOLD, ROAIRO, ZIDLOLD, CICOVER, CITHICK,
     &             CDTPRO, LWNDFILE, LCLOSEWND, IREAD,
     &             LCR)

      IF(LWNEMOCOUCIC.OR.LWNEMOCOUCIT) THEN
        DEALLOCATE(NEMOCICOVER,NEMOCITHICK)
      ENDIF

      IF (ITEST.GT.0) WRITE (IU06,*) ' SUB. GETWND DONE'


!     1.2 USE DATA FROM A FILE CONTAINING WIND SPEED MODIFIED BY
!         ----------------------------------------------------
!     A PREVIOUS WAVE MODEL RUN ON WHICH THIS RESTART IS BASED
!     IF IT EXISTS, OTHERWISE IT WILL USE THE INFORMATION FROM
!     THE ATMOSPHERIC MODEL WIND SPEED ARCHIVE.


      FILNM='uwavein'
      LIU = LEN_TRIM(FILNM)
      FILNM=FILNM(1:LIU)
      IF(IREAD.EQ.IRANK) THEN
        INQUIRE(FILE=FILNM,EXIST=LWAVEWIND)
        IF(LWAVEWIND) THEN
          NWAVEWIND(1)=1
        ELSE
          NWAVEWIND(1)=0
        ENDIF
      ENDIF

!     USE MESSAGE PASSING TO SEND FILE STATUS TO THE OTHER PE'S
      CALL GSTATS(696,0)
      IF(NPROC.GT.1) THEN
        CALL MPL_BROADCAST(NWAVEWIND,KROOT=IREAD,KTAG=KTAG,
     &    CDSTRING='BUILDSTRESS :')
        KTAG=KTAG+1
        IF(NWAVEWIND(1).EQ.1) THEN
          LWAVEWIND=.TRUE.
        ELSE
          LWAVEWIND=.FALSE.
        ENDIF
      ENDIF
      CALL GSTATS(696,1)

      IF(LWAVEWIND) THEN
        IPARAM=245
        LLONLYPOS=.FALSE.
        CALL READWGRIB(IU06,FILNM,IPARAM,CDTPRO,U10OLD,
     &                 KZLEVUWAVE, LLONLYPOS, IREAD)
        IF (ITEST.GT.0) WRITE (IU06,*) ' SUB. READWGRIB DONE FOR ',FILNM

        WRITE(IU06,*) ' '
        WRITE(IU06,*) ' A DATA FILE CONTAINING WIND SPEED INFORMATION'
        WRITE(IU06,*) ' AS PROVIDED BY A PREVIOUS WAVE MODEL RUN'
        WRITE(IU06,*) ' WAS USED TO UPDATE THE INPUT ATMOSPHERIC WINDS'
        WRITE(IU06,*) ' '
        WRITE(IU06,*) ' THE INPUT WINDS AND DRAG COEFFICIENT ARE FOUND'
        WRITE(IU06,*) ' TO HAVE BEEN DETERMINED FOR HEIGHT AT ',
     &                  KZLEVUWAVE,' m'
        IF (ITEST.GT.0) CALL FLUSH(IU06) 

        ILEV=1

      ELSE

        ILEV=1
        KZLEVUWAVE=10
        WRITE(IU06,*) ' '
        WRITE(IU06,*) '          !!!! NOTE !!!!'
        WRITE(IU06,*) ' '
        WRITE(IU06,*) ' NO INFORMATION ON WIND SPEEDS FROM THE'
        WRITE(IU06,*) ' WAVE MODEL WAS PROVIDED'
        WRITE(IU06,*) ' NO UPDATE OF THE INPUT ATMOSPHERIC WINDS'
        WRITE(IU06,*) ' WAS POSSIBLE'
        WRITE(IU06,*) ' '
        WRITE(IU06,*) ' THE INPUT WIND AND DRAG COEFFICIENT ARE ASSUMED'
        WRITE(IU06,*) ' TO HAVE BEEN DETERMINED FOR HEIGHT AT ',
     &                  KZLEVUWAVE,' m'

        IF (ITEST.GT.0) CALL FLUSH(IU06) 
      ENDIF
 
!     TEST WHETHER THE HEIGHT OF THE INPUT WINDS IS THE SAME AS DEFINED
!     FOR THE REST OF THE RUN (SEE CALL TO STRESS IN PREPROC). IF NOT
!     RECOMPUTE THE TABLE. THE ORIGINAL TABLE WILL BE SWAP BACK AT THE
!     END OF THE ROUTINE.

      IF (KZLEVUWAVE.NE.NINT(XNLEV(1))) THEN
        WRITE(IU06,*) ' '
        WRITE(IU06,*) ' THE REFERENCE HEIGHT TO BE USED IN WAMODEL'
        WRITE(IU06,*) ' ',XNLEV(1) 
        WRITE(IU06,*) ' IS DIFFERENT THAN THE INPUT FIELDS HEIGHT'
        WRITE(IU06,*) ' ',FLOAT(KZLEVUWAVE)
        WRITE(IU06,*) ' THE NECESSARY ADJUSTMENTS WILL BE MADE'
        WRITE(IU06,*) ' TO DETERMINE THE INITIAL FIELDS.'
        WRITE(IU06,*) ' '
        IF (ITEST.GT.0) CALL FLUSH(IU06) 

        ALLOCATE(TEMPTAUT(0:ITAUMAX,0:JUMAX,JPLEVT))
        TEMPTAUT(:,:,:)=TAUT(:,:,:)
        TEMPXNLEV=XNLEV(1)
        XNLEV(1)=KZLEVUWAVE

        CALL STRESS(IU06,ITEST)
        IF (ITEST.GE.2)
     1    WRITE(IU06,*) ' SUB. BUILDSTRESS: CALL STRESS DONE'
        IF (ITEST.GT.0) CALL FLUSH(IU06) 

      ENDIF

      IF(.NOT.ALLOCATED(CD)) ALLOCATE(CD(NINF:NSUP,NBLO))

!     1.3 INITIALISE CD USING THE FRICTION VELOCITY FOR TAUW=0.
!         ----------------------------------------------------

      DO IG=1,IGL
        MIJS=IJS(IG)
        MIJL=IJL(IG)
! Mod for OPENMP
        NPROMA=NPROMA_WAM
        CALL GSTATS(1444,0)
!$OMP   PARALLEL DO SCHEDULE(DYNAMIC,1) 
!$OMP+  PRIVATE(JKGLO,KIJS,KIJL,IJ,CDINV)
        DO JKGLO=MIJS,MIJL,NPROMA
          KIJS=JKGLO
          KIJL=MIN(KIJS+NPROMA-1,MIJL)

          DO IJ=KIJS,KIJL
            TAUW(IJ,IG)=0.
          ENDDO

          CALL AIRSEA (U10OLD(KIJS,IG),TAUW(KIJS,IG),USOLD(KIJS,IG),
     &                 Z0OLD(KIJS,IG), KIJS, KIJL, ILEV, ICODE)

          DO IJ=KIJS,KIJL
!!          THE NUMERICAL RELATION BETWEEN USOLD AND U10OLD SHOULD
!!          ALWAYS BE AS IN OUTGRID
            CDINV = MAX(U10OLD(IJ,IG)**2,EPSU10)/
     &              MAX(USOLD(IJ,IG)**2,EPSUS)
            CDINV = MIN(CDINV,10000.) 
            CD(IJ,IG) = 1./CDINV
          ENDDO
        ENDDO
!$OMP   END PARALLEL DO
        CALL GSTATS(1444,1)
        IF (ITEST.GT.0) WRITE (IU06,*) ' SUB. AIRSEA DONE AT 1'
      ENDDO

!     1.4  GET DRAG COEFFICIENT
!          --------------------
      IF(.NOT.LNOCDIN) THEN
        IPARAM=233
        LLONLYPOS=.TRUE.
        FILNM='cdwavein'
        CALL READWGRIB(IU06,FILNM,IPARAM,CDTPRO,CD,
     &                 KZLEVCD, LLONLYPOS, IREAD)
        IF (ITEST.GT.0) WRITE (IU06,*) ' SUB. READWGRIB DONE FOR ',FILNM

!       TEST REFERENCE LEVEL FOR UWAVE AND CD

        IF(KZLEVUWAVE.NE.0.AND.KZLEVCD.NE.0) THEN
          IF(KZLEVUWAVE.NE.KZLEVCD) THEN
            WRITE(IU06,*)'************************************'
            WRITE(IU06,*)'*                                  *'
            WRITE(IU06,*)'* FATAL ERROR IN SUB BUILDSTRESS   *'
            WRITE(IU06,*)'* ==============================   *'
            WRITE(IU06,*)'* REFERENCE LEVEL FOR CD AND UWAVE *'
            WRITE(IU06,*)'* DIFFER. PROGRAM WILL ABORT       *'
            WRITE(IU06,*)'* REF. LEVEL CD = ',KZLEVCD
            WRITE(IU06,*)'* REF. LEVEL UWAVE = ',KZLEVUWAVE
            WRITE(IU06,*)'*                                  *'
            WRITE(IU06,*)'************************************'
            CALL ABORT1
          ENDIF
        ENDIF



!       1.5 COMPUTE TAUW,USOLD AND Z0OLD
!           ----------------------------

        ILEV=1

        DO IG=1,IGL
          MIJS=IJS(IG)
          MIJL=IJL(IG)
! Mod for OPENMP
          NPROMA=NPROMA_WAM
          CALL GSTATS(1444,0)
!$OMP     PARALLEL DO SCHEDULE(DYNAMIC,1) 
!$OMP+    PRIVATE(JKGLO,KIJS,KIJL,IJ,CDSQRTINV,CHARNOCK)
          DO JKGLO=MIJS,MIJL,NPROMA
            KIJS=JKGLO
            KIJL=MIN(KIJS+NPROMA-1,MIJL)
            DO IJ=KIJS,KIJL
              CDSQRTINV = MIN(1./SQRT(CD(IJ,IG)),100.0)
              Z0OLD(IJ,IG) = XNLEV(ILEV)*EXP(-XKAPPA*CDSQRTINV)
!!            USOLD WILL FIRST CONTAIN ITS SQUARE
!!            THE NUMERICAL RELATION BETWEEN USOLD AND U10OLD SHOULD
!!            ALWAYS BE AS IN OUTGRID
              USOLD(IJ,IG) = CD(IJ,IG)*MAX(U10OLD(IJ,IG)**2,EPSU10)
              USOLD(IJ,IG) = MAX(USOLD(IJ,IG),EPSUS)
              CHARNOCK = G*Z0OLD(IJ,IG)/USOLD(IJ,IG)
              CHARNOCK = MAX(CHARNOCK,ALPHA)
              TAUW(IJ,IG) = USOLD(IJ,IG)*(1.-(ALPHA/CHARNOCK)**2)
              USOLD(IJ,IG) = SQRT(USOLD(IJ,IG))
            ENDDO
          ENDDO
!$OMP     END PARALLEL DO
          CALL GSTATS(1444,1)
        ENDDO
      ENDIF

      IF(ALLOCATED(CD)) DEALLOCATE(CD)

      IF(ALLOCATED(TEMPTAUT)) THEN
        XNLEV(1)=TEMPXNLEV
        TAUT(:,:,:)=TEMPTAUT(:,:,:)
        DEALLOCATE(TEMPTAUT)
      ENDIF

      DEALLOCATE(FIELDG)

      WRITE(IU06,*) ' SUB. BUILDSTRESS: INPUT OF RESTART FILES DONE'
#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('BUILDSTRESS',1,ZHOOK_HANDLE)
#endif

      RETURN
      END SUBROUTINE BUILDSTRESS
