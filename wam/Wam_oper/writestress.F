      SUBROUTINE WRITESTRESS(NREAL,RFIELD,FILENAME,LOUNIT,LCUNIT)

! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      APRIL 1997

!*    PURPOSE.
!     --------
!     WRITES THE RESTART WIND AND STRESS FIELDS TO FILE.

!**   INTERFACE.
!     ----------

!     CALL *WRITESTRESS*(NREAL, RFIELD,FILENAME,LOUNIT,LCUNIT)
!     *NREAL*    NUMBER OF FIELDS IN RFIELD
!     *RFIELD*   REAL RESTART FIELDS
!     *FILENAME* FILENAME (INCLUDING PATH) OF TARGET FILE
!     *LOUNIT*   LOGICAL, TRUE IF FREE UNIT HAS TO BE FOUND.
!     *LCUNIT*   LOGICAL, TRUE IF UNIT HAS TO CLOSED

!     METHOD.
!     -------
!     WRITES ARRAYS TO FILE (FORTRAN WRITES).

!     THE OUTPUT IS WRITTEN AS UNFORMATTED BINARY TO FILENAME CONNECTED
!     TO UNIT IU15
!     IT IS IN A FORM THAT IS INDEPENDENT OF THE MODEL DECOMPOSITION.


!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWMPP   , ONLY : NPROC
      USE YOWPARAM , ONLY : NBLO    ,NIBLO    ,LL1D
      USE YOWSTAT  , ONLY : CDTPRO
      USE YOWSPEC  , ONLY : IJ2NEWIJ
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWUNIT  , ONLY : IU15
      USE YOWUNPOOL, ONLY : LLUNSTR
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL
      USE YOMHOOK   ,ONLY : LHOOK    ,DR_HOOK

! ----------------------------------------------------------------------
      IMPLICIT NONE

      INTEGER :: IFLD, IJ, IG, NREAL

      REAL :: ZHOOK_HANDLE
      REAL,DIMENSION(NIBLO) :: RFIELD_G
      REAL,DIMENSION(NIBLO,NREAL) :: RFIELD

      CHARACTER(LEN=1) :: MODE
      CHARACTER(LEN=120) :: FILENAME

      LOGICAL :: LOUNIT, LCUNIT
! ----------------------------------------------------------------------

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('WRITESTRESS',0,ZHOOK_HANDLE)
#endif

      OPEN(IU15,FILE=FILENAME,STATUS='UNKNOWN',FORM='UNFORMATTED')

      WRITE(IU15) CDTPRO,CDATEWO,CDAWIFL,CDATEFL
      IF(LLUNSTR) THEN
        DO IFLD=1,NREAL
          CALL UNBLKRORD(1,1,1,1,1,RFIELD(1,IFLD),RFIELD_G(1))
          WRITE(IU15)(RFIELD_G(IJ),IJ=1,NIBLO)
        ENDDO
      ELSE IF(LL1D .OR. NPROC.EQ.1 ) THEN
        DO IFLD=1,NREAL
          WRITE(IU15)(RFIELD(IJ,IFLD),IJ=1,NIBLO)
        ENDDO
      ELSE
        DO IFLD=1,NREAL
          WRITE(IU15) (RFIELD(IJ2NEWIJ(IJ),IFLD),IJ=1,NIBLO)
        ENDDO
      ENDIF

      CLOSE(IU15)

      IF (ITEST.GE.2)
     &    WRITE(IU06,*) ' SUB. WRITESTRESS: OUTPUT DONE'

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('WRITESTRESS',1,ZHOOK_HANDLE)
#endif

      RETURN
      END SUBROUTINE WRITESTRESS
