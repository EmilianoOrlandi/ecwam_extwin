      SUBROUTINE WRITESTRESS(IJINF, IJSUP, NREAL, RFIELD,
     &                       FILENAME, LRSTPARAL)

! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      APRIL 1997

!*    PURPOSE.
!     --------
!     WRITES THE RESTART WIND AND STRESS FIELDS TO FILE.

!**   INTERFACE.
!     ----------

!     CALL *WRITESTRESS*(IJINF, IJSUP, NREAL, RFIELD,
!                        FILENAME,LRSTPARAL)
!     *IJINF*    FIRST LOWER DIMENSION OF RFIELD 
!     *IJSUP*    FIRST UPPER DIMENSION OF RFIELD 
!     *NREAL*    NUMBER OF FIELDS IN RFIELD
!     *RFIELD*   REAL RESTART FIELDS
!     *FILENAME* FILENAME (INCLUDING PATH) OF TARGET FILE
!     *LRSTPARAL* LOGICAL, TRUE THEN WRITE IN PARALLEL

!     METHOD.
!     -------
!     WRITES ARRAYS TO FILE (FORTRAN WRITES).

!     THE OUTPUT IS WRITTEN AS UNFORMATTED BINARY TO FILENAME
!     IT IS IN A FORM THAT IS INDEPENDENT OF THE MODEL DECOMPOSITION.
!     IF LRSTPARAL IS FALSE


!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWMPP   , ONLY : NPROC
      USE YOWPARAM , ONLY : NBLO    ,LL1D
      USE YOWSTAT  , ONLY : CDTPRO
      USE YOWSPEC  , ONLY : IJ2NEWIJ
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWUNPOOL, ONLY : LLUNSTR
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL
      USE YOMHOOK   ,ONLY : LHOOK    ,DR_HOOK

! ----------------------------------------------------------------------
      IMPLICIT NONE
#include "unblkrord.intfb.h"

      INTEGER, INTENT(IN) :: IJINF, IJSUP, NREAL
      REAL,DIMENSION(IJINF:IJSUP,NREAL),INTENT(INOUT) :: RFIELD
      CHARACTER(LEN=296), INTENT(IN) :: FILENAME
      LOGICAL, INTENT(IN) :: LRSTPARAL

      INTEGER :: IFLD, IJ, IG
      INTEGER :: LFILE, IUNIT
      INTEGER :: I_GET_UNIT

      REAL :: ZHOOK_HANDLE
      REAL,DIMENSION(IJINF:IJSUP) :: RFIELD_G

      CHARACTER(LEN=1) :: MODE

! ----------------------------------------------------------------------

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('WRITESTRESS',0,ZHOOK_HANDLE)
#endif

      LFILE=0
      IF (FILENAME.NE. ' ') LFILE=LEN_TRIM(FILENAME)
      IUNIT=I_GET_UNIT(IU06, FILENAME(1:LFILE) , 'w', 'u', 0)


      WRITE(IUNIT) CDTPRO, CDATEWO, CDAWIFL, CDATEFL

      IF(LLUNSTR .AND. .NOT.LRSTPARAL) THEN
        DO IFLD=1,NREAL
          CALL UNBLKRORD(1,IJINF,IJSUP,1,1,1,1,
     &                   RFIELD(1,IFLD),RFIELD_G(1))
          WRITE(IUNIT)(RFIELD_G(IJ),IJ=IJINF,IJSUP)
        ENDDO
      ELSE IF(LRSTPARAL .OR. LL1D .OR. NPROC.EQ.1) THEN
        DO IFLD=1,NREAL
          WRITE(IUNIT)(RFIELD(IJ,IFLD),IJ=IJINF,IJSUP)
        ENDDO
      ELSE
        DO IFLD=1,NREAL
          WRITE(IUNIT) (RFIELD(IJ2NEWIJ(IJ),IFLD),IJ=IJINF,IJSUP)
        ENDDO
      ENDIF

      CLOSE(IUNIT)

      IF (ITEST.GE.2)
     &    WRITE(IU06,*) ' SUB. WRITESTRESS: OUTPUT DONE'

#ifdef ECMWF
      IF (LHOOK) CALL DR_HOOK('WRITESTRESS',1,ZHOOK_HANDLE)
#endif

      RETURN
      END SUBROUTINE WRITESTRESS
