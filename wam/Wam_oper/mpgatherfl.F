      SUBROUTINE MPGATHERFL(IRECV,ITAG,NSTART,NEND,MINF,MSUP,MI,MS,FL) 

!****  *MPGATHERFL* - GATHER FL ONTO A SINGLE PROCESS 

!     J. BIDLOT    ECMWF   APRIL 1996  MESSAGE PASSING
!     J. BIDLOT    ECMWF   MARCH 1997  add use of MINF AND MSUP

!     PURPOSE.
!     --------

!     GATHER ARRAY FL DISTRIBUTED ACROSS THE
!     DIFFERENT PROCESSES ONTO THE SINGLE PROCESS IRECV

!*    INTERFACE.
!     ----------

!     CALL *MPGATHERFL*(IRECV,ITAG,NSTART,NEND,MINF,MSUP,MI,MS,FL) 

!     *IRECV*     RANK OF THE PROCESS ONTO WHICH FIELD IS COLLECTED 
!     *ITAG*      TAG ASSOCIATED WITH AS A PARTICULAR CALL TO SUBROUTINE
!                 THIS IS NECESSARY TO DIFFERENTIATE THE DIFFERENT CALLS 
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN
!     *MINF*      INDEX OF THE FIRST USEFUL FREQUENCY OF FL
!     *MSUP*      INDEX OF THE LAST USEFUL FREQUENCY OF FL
!     *MI*        INDEX OF THE FIRST FREQUENCY OF FL
!     *MS*        INDEX OF THE LAST FREQUENCY OF FL
!     *FL*        INPUT/OUTPUT ARRAY CONTAINING THE PART OF THE SPECTRUM 

!     METHOD.
!     -------
!     MPE SEND OF ARRAY FL TO PROCESS CORRESPONDING TO IRECV FOR
!     ALL PROCESS EXCEPT FOR THE PROCESS CORRESPONDING TO IRECV
!     WHERE IT IS RECEIVED.

!     EXTERNALS.
!     ----------
!     MPE PACKAGE :
!         MPE_SEND
!         MPE_RECV
!         MPEI_ABORT 

!     REFERENCES.
!     -----------
!         NONE
! -------------------------------------------------------------------


      USE YOWPARAM , ONLY : NANG     ,NFRE     ,NIBLO
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,MPMAXLENGTH



!----------------------------------------------------------------------
!     ALLOCATABLE ARRAYS THAT ARE PASSED AS SUBROUTINE ARGUMENTS

      INTEGER,DIMENSION(NPROC) :: NSTART,NEND
      REAL,DIMENSION(0:NIBLO,NANG,MI:MS) :: FL
!----------------------------------------------------------------------

      REAL,ALLOCATABLE :: ZCOMBUF(:)


      IRTAG=ITAG+NFRE

      IF((IRECV.EQ.0).OR.(NPROC.EQ.1)) THEN
        RETURN
      ELSEIF(IRANK.NE.IRECV) THEN
!     1.1 SEND TO THE PROCESS THAT GATHERS THE WHOLE FIELD
!         ------------------------------------------------
        MPLENGTH=MPMAXLENGTH*NANG*(MSUP-MINF+1)
        ALLOCATE(ZCOMBUF(MPLENGTH))

        KCOUNT=0
        DO M=MINF,MSUP
          DO K=1,NANG
            DO IJ=NSTART(IRANK),NEND(IRANK)
              KCOUNT=KCOUNT+1
              ZCOMBUF(KCOUNT)=FL(IJ,K,M)
            ENDDO
          ENDDO
        ENDDO

! GET SIGNAL THAT RECEIVING PE IS READY

        CALL MPE_RECV(IREADY,1,1,IRECV,IRTAG,0,0,0,
     &   KRCOUNT,KRFROM,KRTAG,IERR)
        IF(IERR.LT.0) CALL MPEI_ABORT
     &   ('MPE_RECV ERROR in MPGATHERFL IREADY' )
        IF(KRTAG.NE.IRTAG) CALL MPEI_ABORT
     &   ('MPE_RECV ERROR in MPGATHERFL MISMATCHED TAGS IRTAG' )

! SEND CONTRIBUTION TO RECEIVING PE

        CALL MPE_SEND(ZCOMBUF,MPLENGTH,2,IRECV,ITAG,0,0,0,IERR)
        IF(IERR.LT.0) CALL MPEI_ABORT
     &   ('MPE_SEND ERROR in MPGATHERFL' )

      ELSE
!     1.2 RECEIVE CONTRIBUTION TO THE FIELD FROM THE OTHER PROCESSES
!         ---------------------------------------------------------- 

! SIGNAL TO OTHER PE'S THAT RECEIVING PE IS READY
        IREADY=ITAG
        CALL MPE_BROADCAST
     &   (IREADY,1,1,IRANK,IRTAG,0,0,0,IERR)
        IF(IERR.LT.0) CALL MPEI_ABORT
     &   ('MPE_BROADCAST ERROR in MPGATHERFL IREADY' )


! GET CONTRIBUTION FROM OTHER PE'S
        MPLENGTH=MPMAXLENGTH*NANG*(MSUP-MINF+1)
        ALLOCATE(ZCOMBUF(MPLENGTH))

        DO IP=1,NPROC-1
          CALL MPE_RECV(ZCOMBUF,MPLENGTH,2,-1,ITAG,0,0,0,
     &     KRCOUNT,KRFROM,KRTAG,IERR)
          IF(IERR.LT.0) CALL MPEI_ABORT
     &     ('MPE_RECV ERROR in MPGATHERFL' )
          IF(KRCOUNT.NE.MPLENGTH) CALL MPEI_ABORT
     &     ('MPE_RECV ERROR in MPGATHERFL:MISMATCHED MESSAGE LENGTH')
          IF(KRTAG.NE.ITAG) CALL MPEI_ABORT
     &     ('MPE_RECV ERROR in MPGATHERFL MISMATCHED TAGS' )

          KCOUNT=0
          DO M=MINF,MSUP
            DO K=1,NANG
              DO IJ=NSTART(KRFROM),NEND(KRFROM)
                KCOUNT=KCOUNT+1
                FL(IJ,K,M)=ZCOMBUF(KCOUNT)
              ENDDO
            ENDDO 
          ENDDO 

        ENDDO
      ENDIF

      DEALLOCATE(ZCOMBUF)

      RETURN
      END SUBROUTINE MPGATHERFL
