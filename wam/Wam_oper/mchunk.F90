SUBROUTINE MCHUNK

!****  *MCHUNK* 

! DECOMPOSES A IJS to IJL BLOCK INTO CHUNKS OF MAXIMUM NPROMA_WAM POINTS

! -------------------------------------------------------------------

USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

USE YOWGRID  , ONLY : IJS, IJL, NPROMA_WAM, NCHNK, IJFROMCHNK, KIJL4CHNK

USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

!----------------------------------------------------------------------

IMPLICIT NONE

INTEGER(KIND=JWIM) :: IJ, IPRM, ICHNK

REAL(KIND=JWRB) :: ZHOOK_HANDLE

!----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('MCHUNK',0,ZHOOK_HANDLE)

! NUMBER OF CHUNKS OF MAXIMUM NPROMA_WAM POINTS:
! ---------------------------------------------
NCHNK =  (IJL-IJS+1) / NPROMA_WAM
IF( NCHNK*NPROMA_WAM <= (IJL-IJS) ) NCHNK = NCHNK + 1

! MAXIMUM GRID POINT INDEX FOR EACH CHUNCK:
! ----------------------------------------
IF (ALLOCATED(KIJL4CHNK)) DEALLOCATE(KIJL4CHNK)
ALLOCATE(KIJL4CHNK(NCHNK))
DO ICHNK = 1, NCHNK
  KIJL4CHNK(ICHNK) = MIN(NPROMA_WAM, IJL-IJS+1-(ICHNK-1)*NPROMA_WAM )
ENDDO

! EQUIVALENCE BLOCK <=> CHUNKS
! -----------------------------
IF (ALLOCATED(IJFROMCHNK)) DEALLOCATE(IJFROMCHNK)
ALLOCATE(IJFROMCHNK(NPROMA_WAM,NCHNK))
DO ICHNK = 1, NCHNK
  DO IPRM = 1, NPROMA_WAM 
    IJ = IJS + IPRM -1 + (ICHNK-1)*NPROMA_WAM 
    IF (IJ > IJL) IJ = 0 
    IJFROMCHNK(IPRM,ICHNK) = IJ
  ENDDO
ENDDO

IF (LHOOK) CALL DR_HOOK('MCHUNK',1,ZHOOK_HANDLE)

END SUBROUTINE MCHUNK
