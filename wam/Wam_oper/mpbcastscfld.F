      SUBROUTINE MPBCASTSCFLD(ITAG,NSTART,NEND,FIELD) 

!****  *MPBCASTSCFLD* - BROADCAST SCALAR BLOCK DATA FIELD ONTO ALL
!****                   PROCESS 

!     J. BIDLOT    ECMWF   SEPTEMBER 1996  MESSAGE PASSING

!     PURPOSE.
!     --------

!     BROADCAST SCALAR BLOCK DATA CONTAINED IN ARRAY FIELD DISTRIBUTED 
!     ACROSS THE DIFFERENT PROCESSES ONTO ALL PE's

!*    INTERFACE.
!     ----------

!     CALL *MPBCASTSCFLD*(ITAG,NSTART,NEND,FIELD) 

!     *ITAG*      TAG ASSOCIATED WITH A PARTICULAR CALL TO SUBROUTINE
!                 THIS IS NECESSARY TO DIFFERENTIATE THE DIFFERENT CALLS
!                 THE USE OF A SEPARATE TAG FOR EACH SPECIFIC CALL
!                 SHOULD ENSURE THAT NO CONFLICT ARISES BETWEEN 
!                 DIFFERENT MESSAGES
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN
!     *FIELD*     INPUT/OUTPUT ARRAY CONTAINING THE FIELD

!     METHOD.
!     -------
!     MPE BROADCAST OF ARRAY FIELD TO ALL PE's
!     FOLLOWED BY RECEIVE OF THE DIFFERENT CONTRIBUTION.

!     EXTERNALS.
!     ----------
!     MPE PACKAGE :
!         MPE_BROADCAST
!         MPE_RECV
!         MPEI_ABORT 

!     REFERENCES.
!     -----------
!         NONE
! -------------------------------------------------------------------


      USE YOWPARAM , ONLY : NIBLO
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,MPMAXLENGTH



!----------------------------------------------------------------------
!     ALLOCATABLE ARRAYS THAT ARE PASSED AS SUBROUTINE ARGUMENTS

      INTEGER,DIMENSION(NPROC) :: NSTART,NEND
      REAL,DIMENSION(NIBLO) :: FIELD
!----------------------------------------------------------------------

      REAL,DIMENSION(MPMAXLENGTH) :: ZCOMBUF


      IF(NPROC.EQ.1) THEN
        RETURN
      ELSE 
!     1.1 SEND TO ALL PE'S THE RESPECTIVE CONTRIBUTION 
!         ---------------------------------------------
        KCOUNT=0
        DO IJ=NSTART(IRANK),NEND(IRANK)
          KCOUNT=KCOUNT+1
          ZCOMBUF(KCOUNT)=FIELD(IJ)
        ENDDO
        CALL MPE_BROADCAST
     &   (ZCOMBUF,MPMAXLENGTH,2,IRANK,ITAG,0,0,0,IERR)
        IF(IERR.LT.0) CALL MPEI_ABORT
     &   ('MPE_BROADCAST ERROR in MPBCASTSCFLD' )

!     1.2 RECEIVE CONTRIBUTION TO THE FIELD FROM THE OTHER PROCESSES
!         ---------------------------------------------------------- 
        DO IP=1,NPROC-1
          CALL MPE_RECV(ZCOMBUF,MPMAXLENGTH,2,-1,ITAG,0,0,0,
     &     KRCOUNT,KRFROM,KRTAG,IERR)
          IF(IERR.LT.0) CALL MPEI_ABORT
     &     ('MPE_RECV ERROR in MPBCASTSCFLD' )
          IF(KRTAG.NE.ITAG) CALL MPEI_ABORT
     &     ('MPE_RECV ERROR in MPBCASTSCFLD MISMATCHED TAGS' )

          KCOUNT=0
          DO IJ=NSTART(KRFROM),NEND(KRFROM)
            KCOUNT=KCOUNT+1
            FIELD(IJ)=ZCOMBUF(KCOUNT)
          ENDDO 
        ENDDO
      ENDIF

      RETURN
      END SUBROUTINE MPBCASTSCFLD
