      SUBROUTINE MPBCASTSCFLD(ITAG,NSTART,NEND,FIELD)

!****  *MPBCASTSCFLD* - BROADCAST SCALAR BLOCK DATA FIELD ONTO ALL
!****                   PROCESS

!     J. BIDLOT    ECMWF   SEPTEMBER 1996  MESSAGE PASSING

!     PURPOSE.
!     --------

!     BROADCAST SCALAR BLOCK DATA CONTAINED IN ARRAY FIELD DISTRIBUTED
!     ACROSS THE DIFFERENT PROCESSES ONTO ALL PE's

!*    INTERFACE.
!     ----------

!     CALL *MPBCASTSCFLD*(ITAG,NSTART,NEND,FIELD)

!     *ITAG*      TAG ASSOCIATED WITH A PARTICULAR CALL TO SUBROUTINE
!                 THIS IS NECESSARY TO DIFFERENTIATE THE DIFFERENT CALLS
!                 THE USE OF A SEPARATE TAG FOR EACH SPECIFIC CALL
!                 SHOULD ENSURE THAT NO CONFLICT ARISES BETWEEN
!                 DIFFERENT MESSAGES
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN
!     *FIELD*     INPUT/OUTPUT ARRAY CONTAINING THE FIELD

!     METHOD.
!     -------
!     MPL ALLGATHER OF ARRAY FIELD TO ALL PE's

!     EXTERNALS.
!     ----------
!     MPL PACKAGE :
!         MPL_ALLGATHER

!     REFERENCES.
!     -----------
!         NONE
! -------------------------------------------------------------------

      USE YOWPARAM , ONLY : NIBLO
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,MPMAXLENGTH
      USE MPL_MODULE
      USE YOMGSTATS, ONLY : LSYNCSTATS
!----------------------------------------------------------------------
!     ALLOCATABLE ARRAYS THAT ARE PASSED AS SUBROUTINE ARGUMENTS

      INTEGER,DIMENSION(NPROC) :: NSTART,NEND
      REAL,DIMENSION(NIBLO) :: FIELD
!----------------------------------------------------------------------

      INTEGER,DIMENSION(NPROC) :: KRECVCOUNTS

      IF(NPROC.EQ.1) THEN
        RETURN
      ELSE

        DO IP=1,NPROC

         KRECVCOUNTS(IP)=NEND(IP) - NSTART(IP)+1

        ENDDO

        IF (.NOT.LSYNCSTATS) CALL GSTATS(616,0)
        CALL MPL_ALLGATHERV(FIELD(NSTART(IRANK):NEND(IRANK)),
     &                     FIELD(1:NIBLO),KRECVCOUNTS,
     &                     CDSTRING='MPBCASTSCFLD:')
        IF (.NOT.LSYNCSTATS) CALL GSTATS(616,1)

      ENDIF

      RETURN
      END SUBROUTINE MPBCASTSCFLD
