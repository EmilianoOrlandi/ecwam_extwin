      SUBROUTINE PREWIND (U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     &                    ROAIRO, ZIDLOLD, ICEMASK,
     &                    NFIELDS, NGPTOTG, NC, NR, FCRANGE,
     &                    ISEC1, ISEC2, FIELDS, MASK_IN)

! ----------------------------------------------------------------------

!**** *PREWIND* - PREPARES WIND DATA FOR WAVE MODEL.

!     P.GROENWOUD     DELFT HYDRAULICS LABORATORY  OKTOBER 1986

!     E. BAUER        MPI       FEB 1987   VERSION FOR CDC 205 HAMBURG.

!     S. HASSELMANN   MPI       MAY 1987   COMBINED CDC 205 AND CRAY
!                                          VERSIONS.
!     W. BRUEGGEMANN  MPI      AUGUST 1988   SIMPLIFIED PROGRAM.

!     L. ZAMBRESKY    ECMWF    JUNE 1988   MODIFIED EXTENSIVELY FOR
!                                          COUPLING TO SPECTRAL MODEL.

!     H. GUNTHER      ECMWF    JUNE 1990   MODIFIED FOR CYCLE_4.

!     J. BIDLOT       ECMWF    FEBRUARY 1996 MESSAGE PASSING

!     S. ABDALLA      ECMWF    OCTOBER 2001  AIR DENSITY AND Zi/L

!*    PURPOSE.
!     --------

!       EVALUATE WIND SPEED AND DIRECTION AT WAVE MODEL GRID POINTS.

!**   INTERFACE.
!     ----------

!     *CALL* *PREWIND (U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
!    &                 ROAIRO, ZIDLOLD, ICEMASK,
!    &                 NFIELDS, NGPTOTG, NC, NR, FCRANGE,
!    &                 ISEC1, ISEC2, FIELDS)*

!      *U10OLD*    REAL      INTERMEDIATE STORAGE OF MODULUS OF WIND
!                            VELOCITY.
!                            CLOCKWISE FROM NORTH).
!      *THWOLD*    REAL      INTERMEDIATE STORAGE OF ANGLE (RADIANS) OF
!                            WIND VELOCITY.
!      *USOLD*     REAL      INTERMEDIATE STORAGE OF MODULUS OF FRICTION
!                            VELOCITY.
!      *Z0OLD*     REAL      INTERMEDIATE STORAGE OF ROUGHNESS LENGTH IN
!                            M.
!      *TAUW*      REAL      WAVE STRESS IN (M/S)**2
!      *ROAIRO*    REAL      AIR DENSITY IN KG/M3.
!      *ZIDLOLD*   REAL      Zi/L (Zi: INVERSION HEIGHT,
!                                   L: MONIN-OBUKHOV LENGTH).
!      *ICEMASK*   INTEGER   SEA ICE MASK.
!      *NFIELDS*   INTEGER   NUMBER OF FIELDS HOLDING ATMOSPHERIC DATA
!      *NGPTOTG*   INTEGER   NUMBER OF ATMOSPHERIC GRID POINTS
!      *NC*        INTEGER   NUMBER OF ATM. COLUMNS OF LONGITUDE NEAR EQUATOR
!      *NR*        INTEGER   NUMBER OF ATM. ROWS OF LATITUDES
!      *FCRANGE*   REAL      FORECAST RANGE IN SECONDS
!      *ISEC1*     INTEGER   GRIB ISEC1 (TO EXTRACT DATE/TIME OF ATM. FIELDS)
!      *ISEC2*     INTEGER   GRIB ISEC2 (TO EXTRACT ATM. GRID CHARACTERISTICS)
!      *FIELDS*    REAL      ATM. FIELDS (U10, V10, AIR DENSITY, Zi/L)

!       *UNIT* *DESCRIPTION*

!          IU01    INPUT WIND DATA (SUB READWIND).
!          IU06    PRINTER OUTPUT (SUB INITMDL).
!          IUVELO  OUTPUT OF BLOCKED WIND FIELDS. (SUB CREWFN).
!          IUSCR   SCRATCH UNITS FOR ALL BLOCKS (INTERMEDIATE STORAGE,
!                  INPUT/OUTPUT) (SUB INITMDL).

!     METHOD.
!     -------

!       INPUT WIND FIELDS WHICH CAN BE YOWPONENTS OF
!                USTAR, U10, USTRESS
!       ARE TRANSFORMED TO FRICTION VELOCITIES.
!       THE INPUT FIELDS HAVE TO BE ON A LAT /LONG GRID.
!       SEE SUB READWIND FOR FORMATS AND HEADER INFORMATION,
!       WHICH HAVE TO BE GIVEN TO THE PROGRAM.

!       A DOUBLE LINEAR INTERPOLATION IN SPACE IS PERFORMED
!       ONTO THE MODEL BLOCKS.
!       IF THE WIND OUTPUT TIMSTEP IS LESS THAN THE INPUT TIMESTEP
!       A LINEAR INTERPOLATION IN TIME IS PERFORMED.

!       THERE ARE TWO POSSIBILITIES WITH RESPECT TO THE WIND
!       OUTPUT FILES:

!           1. PROPAGATION TIMESTEP >= WIND INPUT STEP
!              ONE OUTPUT FILE CONTAINS IDELPRO/IDELWO WINDFIELDS
!              I.E. INFORMATION FOR ONE PROPAGATION TIMESTEP.
!              TIME FILE(I+1)= TIME FILE(I)+ IDELPRO

!           2. PROPAGATION TIMESTEP < INPUT WIND TIMESTEP
!              ONE OUTPUT FILE CONTAINS IDELWI/IDELWO WINDFIELDS
!              I.E. INFORMATION FOR ONE WIND INPUT TIMESTEP.
!              TIME FILE(I+1)= TIME FILE(I) + IDELWI

!     EXTERNALS.
!     ----------

!       *ABORT*     - TERMINATES PROCESSING.
!       *AIRSEA*    - SURFACE LAYER STRESS.
!       *CREWFN*    - CREATES A WIND FILE NAME.
!       *INCDAT*    - INCREMENTS DATE TIME GROUP.
!       *LOCINT*    - INTERPOLATES IN SPACE.
!       *NOTIM*     - STEERING SUB FOR INTERPOLATION IN SPACE ONLY.
!       *READWIND*   - READS A WIND FIELD.
!       *TIMIN*     - STEERING SUB FOR INTERPOLATION IN SPACE AND TIME.
!       *WAMWND*    - BLOCKS A WIND FIELD AND CONVERTS TO USTAR.

!     REFERENCE.
!     -----------

!       NONE.

! ----------------------------------------------------------------------

      USE YOWGRIBHD, ONLY : NKSEK1
      USE YOWMPP   , ONLY : NINF     ,NSUP
      USE YOWPARAM , ONLY : NBLO
      USE YOWSTAT  , ONLY : CDATEA   ,CDATEE   ,IDELPRO  ,IDELWI   ,
     &            IDELWO   ,LANAONLY
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWWIND  , ONLY : CDA      ,CDAWIFL
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

      INTEGER NFIELDS, NGPTOTG, NC, NR
      REAL FCRANGE
      INTEGER ISEC1(NKSEK1), ISEC2(22+NR)
      REAL FIELDS(NGPTOTG,NFIELDS)
      INTEGER MASK_IN(NGPTOTG)

      CHARACTER*14 CDTWIE, CDTWIS, ZERO

      LOGICAL LLFRST
      DATA LLFRST / .TRUE. /

! ----------------------------------------------------------------------
!     ALLOCATABLE ARRAYS THAT ARE PASSED AS SUBROUTINE ARGUMENTS 

      REAL,DIMENSION(NINF:NSUP,NBLO) :: U10OLD,THWOLD,USOLD,Z0OLD,TAUW
     &                                 ,ROAIRO,ZIDLOLD
      INTEGER,DIMENSION(NINF:NSUP,NBLO) :: ICEMASK 
      REAL ZHOOK_HANDLE

! ----------------------------------------------------------------------
      IF (LHOOK) CALL DR_HOOK('PREWIND',0,ZHOOK_HANDLE)

      CALL GSTATS(1968,0)
!*    1. BEGIN AND END DATES OF WIND FIELDS TO BE PROCESSED.
!        ---------------------------------------------------

      ZERO = ' '

      IF (CDA.EQ.ZERO.OR.LANAONLY) THEN

!        IF START FROM PRESET FIELDS DO FIRST FIELD IN ADDITION.

        IF (ITEST.GE.2) THEN
          WRITE(IU06,*) "  PREWIND AT CDATEA = ", CDATEA 
          CALL FLUSH (IU06)
        ENDIF
        CDTWIS = CDATEA
      ELSE
        CDTWIS = CDAWIFL
        IDELWH = -MAX(IDELPRO,IDELWI)+IDELWO
        CALL INCDATE (CDTWIS,IDELWH)
      ENDIF

      IF(CDAWIFL.LT.CDATEE) THEN
        CDTWIE = CDAWIFL
        LLFRST = .FALSE.
      ELSE
        CDTWIE = CDATEE
      ENDIF

! ----------------------------------------------------------------------
!*    2. PROCESS WIND FIELDS.
!        --------------------

      IF (IDELWO.GE.IDELWI) THEN

!*    2.1 NO TIME INTERPOLATION.
!         ----------------------

        IF (ITEST.GE.2) THEN
          WRITE (IU06,*) '   SUB. PREWIND: WIND REQUEST'
          WRITE (IU06,*) '     NO TIME INTERPOLATION'
          WRITE (IU06,*) '     START OF PERIOD IS    CDTWIS = ',CDTWIS
          WRITE (IU06,*) '     END   OF PERIOD IS    CDTWIE = ',CDTWIE
          WRITE (IU06,*) '     WIND INPUT TIME STEP  IDELWI = ',IDELWI
          WRITE (IU06,*) '     WIND OUTPUT TIME STEP IDELWO = ',IDELWO
          CALL FLUSH (IU06)
        ENDIF
        CALL GSTATS(1968,2)
        CALL NOTIM (CDTWIS, CDTWIE,U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     &              ROAIRO, ZIDLOLD, ICEMASK,
     &              NFIELDS,NGPTOTG, NC,NR, FCRANGE,
     &              ISEC1,ISEC2, FIELDS, MASK_IN)
        CALL GSTATS(1968,3)
      ELSE

!*    2.2 TIME INTERPOLATION.
!         -------------------

        IF (ITEST.GE.2) THEN
          WRITE (IU06,*) '   SUB. PREWIND: WIND REQUEST'
          WRITE (IU06,*) '     TIME INTERPOLATION'
          WRITE (IU06,*) '     START OF PERIOD IS    CDTWIS = ',CDTWIS
          WRITE (IU06,*) '     END   OF PERIOD IS    CDTWIE = ',CDTWIE
          WRITE (IU06,*) '     WIND INPUT TIME STEP  IDELWI = ',IDELWI
          WRITE (IU06,*) '     WIND OUTPUT TIME STEP IDELWO = ',IDELWO
          CALL FLUSH (IU06)
        ENDIF
        CALL GSTATS(1968,2)
        CALL TIMIN (CDTWIS, CDTWIE,U10OLD,THWOLD,USOLD,TAUW,Z0OLD,
     &              ROAIRO, ZIDLOLD, ICEMASK,
     &              NFIELDS,NGPTOTG, NC,NR, FCRANGE, 
     &              ISEC1,ISEC2, FIELDS, MASK_IN)
        CALL GSTATS(1968,3)
      ENDIF
      CALL GSTATS(1968,1)
      IF (LHOOK) CALL DR_HOOK('PREWIND',1,ZHOOK_HANDLE)

      RETURN

      END SUBROUTINE PREWIND
