      SUBROUTINE SAVSPEC(FL,NSTART,NEND,CDTPRO,CDATEF,CDATER)
! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      MARCH 1997
!     J. BIDLOT    ECMWF      SEPTEMBER 1997 : use PBIO SOFTWARE
!     B. HANSEN    ECMWF      FEBRUARY  1998 : use FDB SOFTWARE

!*    PURPOSE.
!     --------
!     WRITE BINARY SPECTRA TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSPEC(FL,NSTART,NEND,CDTPRO,CDATEF,CDATER)
!     *FL*        ARRAY CONTAINING THE SPECTRA CONTRIBUTION ON EACH PE
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN
!     *CDTPRO*    CHAR*14   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*14   END DATE OF ANALYSIS RUN (YYMMDDHHMM).
!     *CDATER*    CHAR*14   DATE FOR OUTPUT OF BOTH RESTART FILES

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME OR BY THE FDB SOFTWARE IF FDB IS USED.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ON THE OUTPUT
!     PE BY CALLING MPGATHERFL.
!     IN ORDER TO USE MPL_GATHERV IN MPGATHERFL THE SPECTRA WILL BE
!     GATHERED MDEL FREQUENCIES AT A TIME
!     AND KDEL DIRECTIONS AT A TIME.

!     EXTERNALS.
!     ----------
!     GETENV
!     GRSTNAME
!     MPGATHERFL
!     WRITEFL

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWCOUT  , ONLY : JPPFLAG  ,IPFGTBL  ,KDEL    ,MDEL
      USE YOWGRID  , ONLY : IGL
      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
      USE YOWMESPAS, ONLY : LMESSPASS,LPBIOOUT
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NINF     ,NSUP     ,
     &            NPRECR
      USE YOWPARAM , ONLY : NANG     ,NFRE     ,NIBLO
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWUNIT  , ONLY : IU12     ,IU14     ,IU15

! ----------------------------------------------------------------------
      CHARACTER*14 CDTPRO,CDATEF,CDATER
      CHARACTER*120 FILENAME

      LOGICAL LPBOPEN, LPBCLOSE

      INTEGER,DIMENSION(NPROC) :: NSTART, NEND
      REAL,DIMENSION(NINF-1:NSUP,NANG,NFRE) :: FL
! ----------------------------------------------------------------------
      REAL,ALLOCATABLE,DIMENSION(:,:,:) :: RFL

      REAL ZHOOK_HANDLE

      IF (LHOOK) CALL DR_HOOK('SAVSPEC',0,ZHOOK_HANDLE)

      LPBOPEN = .TRUE.
      LPBCLOSE = .TRUE.

      CALL GRSTNAME(CDTPRO,CDATEF,'BLS',ICPLEN,CPATH,FILENAME)

! COLLECT THE DIFFERENT CONTRIBUTIONS AND PROCEED TO THE OUTPUT OF
! SPECTRUM FROM PE IPFGTBL(JPPFLAG+1)
      IRECV=IPFGTBL(JPPFLAG+1)

      DO MLOOP=1,NFRE,MDEL
        MINF=MLOOP
        MSUP=MIN(MLOOP+MDEL-1,NFRE)
        DO KLOOP=1,NANG,KDEL
          KINF=KLOOP
          KSUP=MIN(KLOOP+KDEL-1,NANG)

          LPBOPEN = .FALSE.
          LPBCLOSE = .FALSE.
          IF(MINF.EQ.1 .AND. KINF.EQ.1) LPBOPEN = .TRUE.
          IF(MSUP.EQ.NFRE .AND. KSUP.EQ.NANG) LPBCLOSE = .TRUE.

          ALLOCATE(RFL(0:NIBLO,KINF:KSUP,MINF:MSUP))
          RFL(0,:,:)=0.
          DO M=MINF,MSUP
            DO K=KINF,KSUP
              DO IJ=NSTART(IRANK),NEND(IRANK)
                RFL(IJ,K,M) = FL(IJ,K,M)
              ENDDO
            ENDDO
          ENDDO

          CALL MPGATHERFL(IRECV,NSTART,NEND,KINF,KSUP,
     &                    MINF,MSUP,RFL)

          IF (ITEST.GE.2)
     &     WRITE(IU06,*)
     &     'SUB. SAVSPEC: RESTART SPECTRUM COLLECTED, :',MLOOP,KLOOP

          IF (IRANK.EQ.IPFGTBL(JPPFLAG+1) .OR. .NOT.LMESSPASS) THEN
            CALL WRITEFL(RFL,KINF,KSUP,MINF,MSUP,
     &                   FILENAME,IUNIT,LPBOPEN,LPBCLOSE)
          ENDIF
          DEALLOCATE(RFL)
        ENDDO
      ENDDO

      WRITE(IU06,*) ' SPECTRUM FILE DISPOSED AT........',
     &                ' CDTPRO  = ', CDTPRO

      IF (LHOOK) CALL DR_HOOK('SAVSPEC',1,ZHOOK_HANDLE)

      RETURN
      END SUBROUTINE SAVSPEC
