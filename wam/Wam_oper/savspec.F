      SUBROUTINE SAVSPEC(FL,NSTART,NEND,CDTPRO,CDATEF,CDATER,
     &                   CDMARS,CDCLASS, CDEXPVER, CDFDB2DSP)
! ----------------------------------------------------------------------
!     J. BIDLOT    ECMWF      MARCH 1997
!     J. BIDLOT    ECMWF      SEPTEMBER 1997 : use PBIO SOFTWARE
!     B. HANSEN    ECMWF      FEBRUARY  1998 : use FDB SOFTWARE

!*    PURPOSE.
!     --------
!     WRITE BINARY SPECTRA TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSPEC(FL,NSTART,NEND,CDTPRO,CDATEF,CDATER,
!    1                CDMARS,CDCLASS, CDEXPVER, CDFDB2DSP)
!     *FL*        ARRAY CONTAINING THE SPECTRA CONTRIBUTION ON EACH PE
!     *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
!     *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN
!     *CDTPRO*    CHAR*12   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*12   END DATE OF ANALYSIS RUN (YYMMDDHHMM).
!     *CDATER*    CHAR*12   DATE FOR OUTPUT OF BOTH RESTART FILES
!     *CDMARS*    CHAR*     MARS TYPE OF FIELD.
!     *CDCLASS*   CHAR*     MARS CLASS OF FIELD.
!     *CDEXPVER*  CHAR*     EXPERIMENT VERSION IDENTIFIER.
!     *CDFDB2DSP* CHAR*     ROOT DIRECTORY FOR 2D SPECTRA NON GRIB DATA.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME OR BY THE FDB SOFTWARE IF FDB IS USED.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ON THE OUTPUT
!     PE BY CALLING MPGATHERFL.
!     IN ORDER NOT TO EXCEED THE MAXIMUN SIZE OF THE MESSAGE MAILBOX,
!     ONLY PART OF THE SPECTRA WILL BE GATHERED PER CALL TO MPGATHERFL
!     BY ALLOWING ONLY MDEL FREQUENCIES PER CALL.

!     EXTERNALS.
!     ----------
!     GETENV
!     GRSTNAME
!     MPGATHERFL
!     SAVREST
!     WRITEFL

!     REFERENCE.
!     ----------
!     NONE
! ----------------------------------------------------------------------

      USE YOWCOUT  , ONLY : JPPFLAG  ,IPFGTBL
      USE YOWGRID  , ONLY : IGL
      USE YOWMESPAS, ONLY : LMESSPASS,LPBIOOUT ,LFDBIOOUT
      USE YOWMPP   , ONLY : IRANK    ,NPROC    ,NINF     ,NSUP     ,
     &            NPRECR
      USE YOWPARAM , ONLY : NANG     ,NFRE     ,NIBLO
      USE YOWTEST  , ONLY : IU06     ,ITEST
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWUNIT  , ONLY : IU12     ,IU14     ,IU15     ,IU04

! ----------------------------------------------------------------------
      CHARACTER *2 CDMARS
      CHARACTER *2 CDCLASS
      CHARACTER *4 CDEXPVER
      CHARACTER*12 CDTPRO,CDATEF,CDATER
      CHARACTER*9 CMBXSIZE
      CHARACTER*80 FILENAME
      CHARACTER *(*) CDFDB2DSP

      LOGICAL LFRSTIME, LPBOPEN, LPBCLOSE

      INTEGER,DIMENSION(NPROC) :: NSTART, NEND
      REAL,DIMENSION(NINF-1:NSUP,NANG,NFRE) :: FL
! ----------------------------------------------------------------------
!     define local allocatable arrays
      REAL,ALLOCATABLE,DIMENSION(:,:,:) :: RFL
      DATA MDEL, LFRSTIME / 1, .TRUE./

      INTEGER IUNIT
      SAVE IUNIT

      LPBOPEN = .TRUE.
      LPBCLOSE = .TRUE.

      IF (LMESSPASS) THEN

! OPEN OUPUT FILE

! FOR OPERATION, THE RESTART FILES SHOULD BE OUTPUT TO A VFL FILE SYSTEM
! WHERE PREALLOCATION OF THE SPACE CAN BE DONE BEFORE WRITE TO DISK

        CALL GRSTNAME(CDTPRO,CDATEF,'BLS',ICPLEN,CPATH,FILENAME)

! COLLECT THE DIFFERENT CONTRIBUTIONS AND PROCEED TO THE OUTPUT OF
! SPECTRUM FROM PE IPFGTBL(JPPFLAG+1)

! DETERMINE MAXIMUM NUMBER OF FREQUENCIES WHICH CAN BE USED TO COLLECT
! SPECTRA ON ONE PE WITHOUT EXCEEDING THE MAXIMUM MAILBOX SIZE

        IF(LFRSTIME) THEN
          CALL GETENV('VPP_MBX_SIZE',CMBXSIZE)
          READ(CMBXSIZE,'(I9)') MBXSIZE
          MINSIZE=NINT(REAL(NPROC-1)*NIBLO*NANG*NPRECR/NPROC)
          IF(MINSIZE.GT.MBXSIZE) THEN
            WRITE (IU06,*) '***************************************'
            WRITE (IU06,*) '*                                     *'
            WRITE (IU06,*) '* MESSAGE MAIL BOX SIZE IS TOO SMALL  *'
            WRITE (IU06,*) '* TO OUTPUT THE RESTART SPECTRUM FILE *'
            WRITE (IU06,*) '* THE PRESCRIBED SIZE SIZE IS         *'
            WRITE (IU06,*) '* ', MBXSIZE,' BYTES                  *'
            WRITE (IU06,*) '* PLEASE INCREASE SIZE TO AT LEAST    *'
            WRITE (IU06,*) '* ', MINSIZE, 'BYTES                  *'
            WRITE (IU06,*) '* SEE ENVIRONMENT VARIABLE            *'
            WRITE (IU06,*) '* VPP_MBX_SIZE                        *'
            WRITE (IU06,*) '*                                     *'
            WRITE (IU06,*) '***************************************'
            CALL ABORT1
          ENDIF
          MDEL = 1
!!           MDEL = NFRE
!!           DO WHILE (MINSIZE*MDEL.GE.MBXSIZE.AND.MDEL.GT.1)
!!              MDEL=MDEL-1
!!           ENDDO
          WRITE (IU06,*)''
          WRITE (IU06,*)' IN SAVSPEC :'
          WRITE (IU06,*)' MESSAGE MAIL BOX SIZE OF ',MBXSIZE,' BYTES'
          IF (MDEL.EQ.1) THEN
            WRITE (IU06,*)' ',MDEL,' FREQUENCY IS GATHERED AT ONE TIME'
          ELSE
            WRITE (IU06,*)' ',MDEL,'FREQUENCIES ARE GATHERED AT ONE 
     &       TIME'
          ENDIF
          WRITE (IU06,*)' WHICH SHOULD RESULT IN A MAXIMUM USAGE OF'
          WRITE (IU06,*)' ',MINSIZE*MDEL, 'BYTES FOR EACH CALL TO ',
     &     'MPGATHERFL'
          WRITE (IU06,*)''

          LFRSTIME=.FALSE.
        ENDIF
        IF (LPBIOOUT .OR. LFDBIOOUT) THEN
! use the ability of the PBIO or FDB software to write and append
! output by frequency bands

          DO MLOOP=1,NFRE,MDEL

            MINF=MLOOP
            MSUP=MIN(MLOOP+MDEL-1,NFRE)

            LPBOPEN = .FALSE.
            LPBCLOSE = .FALSE.
            IF(MINF.EQ.1) LPBOPEN = .TRUE.
            IF(MSUP.EQ.NFRE) LPBCLOSE = .TRUE.

            ALLOCATE(RFL(0:NIBLO,NANG,MINF:MSUP))
            RFL=0.

            DO M=MINF,MSUP
              DO K=1,NANG
                DO IJ=NSTART(IRANK),NEND(IRANK)
                  RFL(IJ,K,M) = FL(IJ,K,M)
                ENDDO
              ENDDO
            ENDDO
            IRECV=IPFGTBL(JPPFLAG+1)
            CALL MPGATHERFL(IRECV,121+MLOOP,NSTART,NEND,MINF,MSUP,
     &       MINF,MSUP,RFL)
            IF (ITEST.GE.2)
     &       WRITE(IU06,*)
     &       'SUB. SAVSPEC: RESTART SPECTRUM COLLECTED, PART:',MLOOP

            IF (LFDBIOOUT) THEN
              ILFN = LEN_TRIM(FILENAME)
              WRITE(FILENAME(ILFN+1:ILFN+6),'(i6.6)') MLOOP
            ENDIF
            IF (IRANK.EQ.IPFGTBL(JPPFLAG+1)) THEN
              IF (ITEST .GE. 2)
     &         WRITE(IU06,'(" savspec: CALLS WRITEFL: NIBLO=", I6, 
     &         " MINF=", I3, " MSUP=", I3, " FILENAME=", A40, " IUNIT=",
     &         I2, " LPBOPEN=", L1, " LPBCLOSE=", L1)')
     &         NIBLO,MINF,MSUP,FILENAME(1:40),IUNIT,LPBOPEN,LPBCLOSE
              CALL WRITEFL(RFL,0,NIBLO,MINF,MSUP,FILENAME,IUNIT,
     &         LPBOPEN,LPBCLOSE,CDMARS,CDCLASS,CDEXPVER,
     &         CDFDB2DSP)
            ENDIF
            DEALLOCATE(RFL)

          ENDDO


        ELSE

          ALLOCATE(RFL(0:NIBLO,NANG,NFRE))
          RFL=0.

          DO MLOOP=1,NFRE,MDEL

            MINF=MLOOP
            MSUP=MIN(MLOOP+MDEL-1,NFRE)

            DO M=MINF,MSUP
              DO K=1,NANG
                DO IJ=NSTART(IRANK),NEND(IRANK)
                  RFL(IJ,K,M) = FL(IJ,K,M)
                ENDDO
              ENDDO
            ENDDO

            IRECV=IPFGTBL(JPPFLAG+1)
            CALL MPGATHERFL(IRECV,121+MLOOP,NSTART,NEND,MINF,MSUP,
     &       1,NFRE,RFL)

            IF (ITEST.GE.2)
     &       WRITE(IU06,*)
     &       'SUB. SAVSPEC: RESTART SPECTRUM COLLECTED, PART:',MLOOP

          ENDDO

          IF (IRANK.EQ.IPFGTBL(JPPFLAG+1))
     &     CALL WRITEFL(RFL,0,NIBLO,1,NFRE,FILENAME,IUNIT,
     &     LPBOPEN,LPBCLOSE,CDMARS,CDCLASS,CDEXPVER,
     &     CDFDB2DSP)

          DEALLOCATE(RFL)

        ENDIF


      ELSE

        IF(LPBIOOUT.OR.LFDBIOOUT) THEN
          CALL GRSTNAME(CDTPRO,CDATEF,'BLS',ICPLEN,CPATH,FILENAME)
        ENDIF

        CALL WRITEFL(FL,0,NIBLO,1,NFRE,FILENAME,IUNIT,
     &               LPBOPEN,LPBCLOSE,CDMARS,CDCLASS,CDEXPVER,
     &               CDFDB2DSP)

        IF(.NOT.LPBIOOUT.AND..NOT.LFDBIOOUT)
     &  CALL SAVREST(IU06, IU04, IU12, IU14, IU15, IGL,
     &               CDTPRO, CDATEF, CDATER)

      ENDIF

      WRITE(IU06,*) ' SPECTRUM FILE DISPOSED AT........',
     &                ' CDTPRO  = ', CDTPRO

      RETURN
      END SUBROUTINE SAVSPEC
