SUBROUTINE WVCOUPLE_INIT_EARLY(YREWCOU, YDGEOMETRY, YDRIP)

!----------------------------------------------------------------------

!**** *WVCOUPLE_INIT_EARLY* PERFORMS SOME INITIALIZATION OF THE WAVE
!     MODEL WHEN COUPLED TO IFS.

!     J. HAWKES    ECMWF NVOEMBER 2017

!*    PURPOSE.
!     --------

!     IT IS PRIMARILY USED TO SET UP PARTS OF THE MODEL IN
!     CNT0 (WHEREAS MOST WAM INITIALIZATION HAPPENS THE FIRST
!     TIME THE WAVE MODEL IS CALLED). IMPORTANT FOR THE IO SERVER
!     WHICH REQUIRES GRIB HEADERS AND LOCAL-TO-GLOBAL 
!	  MAPPINGS AT INITIALIZATION.

!**   INTERFACE.
!     ----------

!     SUBROUTINE WVCOUPLE_INIT_EARLY
!                INPUT: YDGEOMETRY
!                OUTPUT:

!     METHOD.
!     -------

!     EXTERNALS.
!     ----------
!	  WVCOUPLE
!     SU0YOMB

!     REFERENCE.
!     ----------

!       NONE.

!-------------------------------------------------------------------

	USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK
	USE PARKIND1 , ONLY : JPIM, JPRB
	USE YOEWCOU  , ONLY : TEWCOU
	USE YOMCT0   , ONLY : LFDBOP
	USE YOMCT3   , ONLY : NSTEP
	USE YOMRIP   , ONLY : TRIP
	USE YOWGRIB_HANDLES, ONLY:    NGRIB_HANDLE_WAM_S, &
							 	& NGRIB_HANDLE_WAM_I, &
								& NGRIB_HANDLE_IFS
	USE IOSTREAM_MIX, ONLY : NGRIB_HANDLE_GG
	USE YOMLUN   , ONLY : NULOUT
	USE GEOMETRY_MOD , ONLY : GEOMETRY
	USE GRIB_API_INTERFACE, ONLY : IGRIB_CLONE
	USE GRIB_API_INTERFACE, ONLY : IGRIB_SET_VALUE

	USE YOMDYNCORE, ONLY: LPPSTEPS

	IMPLICIT NONE

	LOGICAL :: LLRNL
	INTEGER(KIND=JPIM) :: IGPTOTG,IGPTOT
	TYPE(TEWCOU)    	  ,INTENT(INOUT)    :: YREWCOU
	TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
	TYPE(TRIP)    	  ,INTENT(IN)    :: YDRIP
	REAL :: ZHOOK_HANDLE

	!TEST
	INTEGER(KIND=JPIM) :: ILEV, IGRIBCD
	CHARACTER (LEN=2) :: CLREPR
	CHARACTER (LEN=3) :: CLTYPE
	INTEGER(KIND=JPIM) :: ITOP_DUM,IBOT_DUM
	LOGICAL :: LLGRAD_DUM
	REAL(KIND=JPRB) :: ZDUMMY(1)
	INTEGER(KIND=JPIM) :: ISTEP_OUT

!-------------------------------------------------------------------

#ifdef ECMWF 
      IF (LHOOK) CALL DR_HOOK('WVCOUPLE_INIT_EARLY',0,ZHOOK_HANDLE)
#endif

	ASSOCIATE(YDGEM=>YDGEOMETRY%YRGEM)
	ASSOCIATE( &
		& NGPTOT=>YDGEM%NGPTOT, &
		& NGPTOTG=>YDGEM%NGPTOTG, &
		& LWFRSTIME=>YREWCOU%LWFRSTIME, &
		& LWCOU=>YREWCOU%LWCOU, &
		& LWINIT=>YREWCOU%LWINIT, &
		& LWCOU2W=>YREWCOU%LWCOU2W, &
		& LWFLUX=>YREWCOU%LWFLUX, &
		& LWCUR=>YREWCOU%LWCUR, &
		& NLATW=>YREWCOU%NLATW, &
		& NLONW=>YREWCOU%NLONW, &
		& RNORTW=>YREWCOU%RNORTW, &
		& RSOUTW=>YREWCOU%RSOUTW, &
		& RDEGREW=>YREWCOU%RDEGREW, &
		& LWVIN_MASK_NOT_SET=>YREWCOU%LWVIN_MASK_NOT_SET, &
		& LWVIN_UNINITIALISED=>YREWCOU%LWVIN_UNINITIALISED, &
		& LWVOUT_MASK_NOT_SET=>YREWCOU%LWVOUT_MASK_NOT_SET, &
		& NWV_W2IWGHT=>YREWCOU%NWV_W2IWGHT, &
		& LWCOUNORMS=>YREWCOU%LWCOUNORMS, &
		& NGRIB_HANDLE_FOR_WAM=>YREWCOU%NGRIB_HANDLE_FOR_WAM, &
		& TSTEP=>YDRIP%TSTEP, &
		& KSTPW=>YREWCOU%NSTPW &
	&)

	! -- Initialize WAM variables and read WAM namelist
	IF (.NOT.LWINIT .OR. .NOT.ALLOCATED(YREWCOU%MASK_WAVE_OUT)) THEN

		LLRNL=.TRUE.
		CALL WVWAMINIT(LWCOU,NULOUT,LLRNL,NLONW,NLATW,RSOUTW,RNORTW)

		RDEGREW=(RNORTW-RSOUTW)/(NLATW-1)

		! Allocate and initialise masks for optimised coupling communications
		IGPTOTG=NGPTOTG
		IGPTOT=NGPTOT
		ALLOCATE(YREWCOU%MASK_WAVE_IN(IGPTOTG))
		ALLOCATE(YREWCOU%MASK_WAVE_OUT(NLONW,NLATW))
		YREWCOU%MASK_WAVE_IN(:)=0
		YREWCOU%MASK_WAVE_OUT(:,:)=0
		! Set flags to indicate mask and comms data structures are not initialised
		LWVIN_MASK_NOT_SET=.TRUE.
		LWVOUT_MASK_NOT_SET=.TRUE.
		LWVIN_UNINITIALISED=.TRUE.
		! Inactive until WV_W2IWGHT is defined.
		NWV_W2IWGHT=0
		! Set default for producing global norms
		LWCOUNORMS=.FALSE.

	END IF
	
	! -- Perform WAM decomposition and create GRIB templates
	IF(.NOT.LWINIT) THEN

		CALL WVWAMINIT1(LWCOU,LWCOU2W,LWFLUX,LWCUR,LFDBOP)
		CALL WVWAMDECOMP
		CALL SETMARSTYPE

		! Initialize and update GRIB handles for WAM (integrated and spectral)
		CALL WVCOUPLE_UPDATE_GRIB_HANDLES(YREWCOU,YDGEOMETRY, YDRIP)
		NGRIB_HANDLE_IFS=NGRIB_HANDLE_FOR_WAM
		CALL PRESET_WGRIB_TEMPLATE("I",NGRIB_HANDLE_WAM_I)
		CALL PRESET_WGRIB_TEMPLATE("S",NGRIB_HANDLE_WAM_S)

	END IF

	LWINIT=.TRUE.

	END ASSOCIATE
	END ASSOCIATE

#ifdef ECMWF 
	IF (LHOOK) CALL DR_HOOK('WVCOUPLE_INIT_EARLY',1,ZHOOK_HANDLE)
#endif

      RETURN
END SUBROUTINE WVCOUPLE_INIT_EARLY 
