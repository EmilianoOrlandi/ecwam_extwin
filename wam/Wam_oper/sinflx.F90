SUBROUTINE SINFLX (ICALL, NCALL, IJS, IJL, &
&                  LUPDTUS, &
&                  U10NEW, THWNEW, ROAIRN, WSTAR, &
&                  CICVR, &
&                  FMEAN, FLM, &
&                  FMEANWS, FL1, &
&                  USNEW, TAUW, Z0NEW, PHIWA, &
&                  FL, SL, SPOS, &
&                  MIJ, MIJFLX, RHOWGDFTH, XLLWS)

! ----------------------------------------------------------------------

!**** *SINFLX* - UPDATE STRESS AND COMPUTE WIND INPUT SOURCE TERM. 

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : LWCOU
      USE YOWPARAM , ONLY : NANG     ,NFRE
      USE YOWWNDG  , ONLY : ICODE    ,ICODE_CPL
      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "imphftail.intfb.h"
#include "airsea.intfb.h"
#include "femeanws.intfb.h"
#include "frcutindex.intfb.h"
#include "sinput.intfb.h"
#include "stresso.intfb.h"

INTEGER(KIND=JWIM), INTENT(IN) :: ICALL  !! CALL NUMBER.
INTEGER(KIND=JWIM), INTENT(IN) :: NCALL  !! TOTAL NUMBER OF CALLS.
INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL !! GRID POINT INDEXES.  

LOGICAL, INTENT(IN) :: LUPDTUS  !! IF TRUE USNEW AND Z0NEW WILL BE UPDATED (CALLING AIRSEA).

REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: U10NEW !! NEW WIND SPEED IN M/S.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: THWNEW !! WIND DIRECTION IN RADIANS IN OCEANOGRAPHIC NOTATION.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: ROAIRN  !! AIR DENSITY IN KG/M3.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: WSTAR !! FREE CONVECTION VELOCITY SCALE (M/S)
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: CICVR  !! SEA ICE COVER.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: FMEAN  !! MEAN FREQUENCY.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG), INTENT(IN) :: FLM  !! SPECTRAL NOISE LEVEL.

REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: FMEANWS  !! MEAN FREQUENCY OF THE WINDSEA.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(INOUT) :: FL1  !! WAVE SPECTRUM.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: USNEW !! FRICTION VELOCITY IN M/S.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: TAUW  !! WAVE STRESS IN (M/S)**2
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: Z0NEW  !! ROUGHNESS LENGTH IN M.

REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(OUT) :: PHIWA  !! ENERGY FLUX FROM WIND INTO WAVES.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: FL !! DIAGONAL MATRIX OF FUNCTIONAL DERIVATIVE.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: SL !! TOTAL SOURCE FUNCTION ARRAY.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: SPOS !!  POSITIVE SINPUT ONLY.

INTEGER(KIND=JWIM), INTENT(OUT) :: MIJ(IJS:IJL)  !! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE.
INTEGER(KIND=JWIM), INTENT(OUT) :: MIJFLX(IJS:IJL) !! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE FOR FLUX CALCULATION.

REAL(KIND=JWRB), DIMENSION(IJS:IJL,NFRE), INTENT(OUT) :: RHOWGDFTH  !! WATER DENSITY * G * DF * DTHETA
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: XLLWS  !! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM.


INTEGER(KIND=JWIM) :: IUSFG, ICODE_WND
INTEGER(KIND=JWIM) :: NGST

REAL(KIND=JWRB) :: ZHOOK_HANDLE
REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: EMEANWS

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',0,ZHOOK_HANDLE)


! UPDATE USNEW AND Z0NEW
IF(ICALL == 1 ) THEN
  IUSFG = 0
  IF (LWCOU) THEN
    ICODE_WND = ICODE_CPL
  ELSE
    ICODE_WND = ICODE
  ENDIF
ELSE
  IUSFG = 1
  ICODE_WND = 3
ENDIF
IF(LUPDTUS) CALL AIRSEA (FL1, FMEAN, FMEANWS, U10NEW, THWNEW, ROAIRN, TAUW, USNEW, Z0NEW, IJS, IJL, ICODE_WND, IUSFG) 

! COMPUTE WIND INPUT
!!  FL AND SL ARE INITIALISED IN SINPUT
IF(ICALL < NCALL ) THEN
  NGST = 1
ELSE
  NGST = 2
ENDIF
CALL SINPUT (NGST, FL1, FL, IJS, IJL, THWNEW, USNEW, Z0NEW, ROAIRN, WSTAR, SL, SPOS, XLLWS) 

! MEAN FREQUENCY CHARACTERISTIC FOR WIND SEA
CALL FEMEANWS(FL1, IJS, IJL, EMEANWS, FMEANWS, XLLWS)

! COMPUTE LAST FREQUENCY INDEX OF PROGNOSTIC PART OF SPECTRUM.
CALL FRCUTINDEX(IJS, IJL, FMEAN, FMEANWS, USNEW, CICVR, MIJ, MIJFLX, RHOWGDFTH)

! RE-IMPOSE HIGH FREQUENCY TAIL
!!!!!   debile IF (LUPDTUS) CALL IMPHFTAIL (IJS, IJL, MIJ, FLM, FL1)

! UPDATE TAUW
CALL STRESSO (FL1, SL, SPOS, IJS, IJL, MIJFLX, RHOWGDFTH, THWNEW, USNEW, Z0NEW, ROAIRN, TAUW, PHIWA) 

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',1,ZHOOK_HANDLE)

END SUBROUTINE SINFLX
