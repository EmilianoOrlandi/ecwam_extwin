SUBROUTINE SINFLX (ICALL, NCALL, IJS, IJL, &
&                  LUPDTUS, &
&                  U10NEW, THWNEW, ROAIRN, WSTAR, &
&                  CICVR, &
&                  FMEAN, FMEANWS, &
&                  FLM, FL1, &
&                  USNEW, TAUW, TAUWDIR, Z0NEW, Z0B, PHIWA, &
&                  FL, SL, SPOS, &
&                  MIJ, RHOWGDFTH, XLLWS)

! ----------------------------------------------------------------------

!**** *SINFLX* - UPDATE STRESS AND COMPUTE WIND INPUT SOURCE TERM. 

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : LWCOU    ,LLCAPCHNK , LLNORMAGAM
      USE YOWPARAM , ONLY : NANG     ,NFRE
      USE YOWPHYS  , ONLY : DTHRN_A, DTHRN_U 
      USE YOWWNDG  , ONLY : ICODE    ,ICODE_CPL
      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "airsea.intfb.h"
#include "femeanws.intfb.h"
#include "frcutindex.intfb.h"
#include "flmintail.intfb.h"
#include "sinput.intfb.h"
#include "stresso.intfb.h"

INTEGER(KIND=JWIM), INTENT(IN) :: ICALL  !! CALL NUMBER.
INTEGER(KIND=JWIM), INTENT(IN) :: NCALL  !! TOTAL NUMBER OF CALLS.
INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL !! GRID POINT INDEXES.  

LOGICAL, INTENT(IN) :: LUPDTUS  !! IF TRUE USNEW AND Z0NEW WILL BE UPDATED (CALLING AIRSEA).

REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: U10NEW !! NEW WIND SPEED IN M/S.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: THWNEW !! WIND DIRECTION IN RADIANS IN OCEANOGRAPHIC NOTATION.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: ROAIRN  !! AIR DENSITY IN KG/M3.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: WSTAR !! FREE CONVECTION VELOCITY SCALE (M/S)
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: CICVR  !! SEA ICE COVER.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: FMEAN  !! MEAN FREQUENCY.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(OUT) :: FMEANWS  !! MEAN FREQUENCY OF THE WINDSEA.

REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG), INTENT(IN) :: FLM  !! SPECTAL DENSITY MINIMUM VALUE
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(INOUT) :: FL1  !! WAVE SPECTRUM.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: USNEW !! FRICTION VELOCITY IN M/S.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: TAUW  !! WAVE STRESS IN (M/S)**2
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: TAUWDIR  !! WAVE STRESS DIRECTION.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: Z0NEW  !! ROUGHNESS LENGTH IN M.
REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: Z0B  !! BACKGROUND ROUGHNESS LENGTH.

REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(OUT) :: PHIWA  !! ENERGY FLUX FROM WIND INTO WAVES.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: FL !! DIAGONAL MATRIX OF FUNCTIONAL DERIVATIVE.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: SL !! TOTAL SOURCE FUNCTION ARRAY.
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: SPOS !!  POSITIVE SINPUT ONLY.

INTEGER(KIND=JWIM), INTENT(OUT) :: MIJ(IJS:IJL)  !! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE.

REAL(KIND=JWRB), DIMENSION(IJS:IJL,NFRE), INTENT(OUT) :: RHOWGDFTH  !! WATER DENSITY * G * DF * DTHETA
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: XLLWS  !! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM.


INTEGER(KIND=JWIM) :: IUSFG, ICODE_WND
INTEGER(KIND=JWIM) :: NGST

REAL(KIND=JWRB) :: ZHOOK_HANDLE
REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: EMEANWS, RNFAC

LOGICAL :: LLPHIWA

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',0,ZHOOK_HANDLE)

! UPDATE USNEW AND Z0NEW
IF(ICALL == 1 ) THEN
  IUSFG = 0
  IF (LWCOU) THEN
    ICODE_WND = ICODE_CPL
  ELSE
    ICODE_WND = ICODE
  ENDIF
ELSE
  IUSFG = 1
  ICODE_WND = 3
ENDIF

IF(LLNORMAGAM .AND. LLCAPCHNK ) THEN
  RNFAC(:) = 1.0_JWRB+DTHRN_A*(1.0_JWRB+TANH(U10NEW(:)-DTHRN_U))
ELSE
  RNFAC(:) = 1.0_JWRB 
ENDIF


IF(LUPDTUS) THEN
  ! increase noise level in the tail
  IF(ICALL == 1 ) FL1(:,:,NFRE) = MAX(FL1(:,:,NFRE),FLM(:,:))

  CALL AIRSEA (FL1, U10NEW, THWNEW, ROAIRN, TAUW, TAUWDIR, RNFAC, USNEW, Z0NEW, Z0B, IJS, IJL, ICODE_WND, IUSFG) 
ENDIF

! COMPUTE WIND INPUT
!!  FL AND SL ARE INITIALISED IN SINPUT
IF(ICALL < NCALL ) THEN
  NGST = 1
  LLPHIWA = .FALSE.
ELSE
  NGST = 2
  LLPHIWA = .TRUE.
ENDIF

CALL SINPUT (NGST, FL1, FL, IJS, IJL, THWNEW, U10NEW, USNEW, Z0NEW, ROAIRN, WSTAR, RNFAC, SL, SPOS, XLLWS) 

! MEAN FREQUENCY CHARACTERISTIC FOR WIND SEA
CALL FEMEANWS(FL1, IJS, IJL, EMEANWS, FMEANWS, XLLWS)

! COMPUTE LAST FREQUENCY INDEX OF PROGNOSTIC PART OF SPECTRUM.
CALL FRCUTINDEX(IJS, IJL, FMEAN, FMEANWS, USNEW, CICVR, MIJ, RHOWGDFTH)

! UPDATE TAUW
CALL STRESSO (FL1, SL, SPOS, IJS, IJL, THWNEW, USNEW, Z0NEW, ROAIRN, RNFAC, TAUW, TAUWDIR, PHIWA, LLPHIWA) 

! INSURE MINIMUN ENERGY AT THE LAST FREQUENCY BIN.
!!!1 not needed ? IF(ICALL < NCALL ) CALL FLMINTAIL(IJS, IJL, U10NEW, THWNEW, USNEW, FMEANWS, FL1) 

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',1,ZHOOK_HANDLE)

END SUBROUTINE SINFLX
