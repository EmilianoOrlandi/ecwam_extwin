SUBROUTINE SINFLX (ICALL, NCALL, IJS, IJL, KIJS, KIJL,      &
&                  LUPDTUS,                                 &
&                  GFL,                                     &
&                  U10NEW, THWNEW, ROAIRN, WSTAR, CICVR,    &
&                  FMEAN, FMEANWS,                          &
&                  FLM,                                     &
&                  USNEW, TAUW, TAUWDIR, Z0NEW, Z0B, PHIWA, &
&                  FLD, SL, SPOS,                           &
&                  MIJ, RHOWGDFTH, GXLLWS)

! ----------------------------------------------------------------------

!**** *SINFLX* - UPDATE STRESS AND COMPUTE WIND INPUT SOURCE TERM. 

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : LWCOU    ,LLCAPCHNK , LLNORMAGAM
      USE YOWPARAM , ONLY : NANG     ,NFRE
      USE YOWPHYS  , ONLY : DTHRN_A, DTHRN_U 
      USE YOWWNDG  , ONLY : ICODE    ,ICODE_CPL
      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "airsea.intfb.h"
#include "femeanws.intfb.h"
#include "frcutindex.intfb.h"
#include "flmintail.intfb.h"
#include "sinput.intfb.h"
#include "stresso.intfb.h"

INTEGER(KIND=JWIM), INTENT(IN) :: ICALL  !! CALL NUMBER.
INTEGER(KIND=JWIM), INTENT(IN) :: NCALL  !! TOTAL NUMBER OF CALLS.
INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL !! IJS:IJL 1st DIMENSION of GFL and GXLLWS.  
INTEGER(KIND=JWIM), INTENT(IN) :: KIJS, KIJL !! GRID POINT INDEXES.  

LOGICAL, INTENT(IN) :: LUPDTUS  !! IF TRUE USNEW AND Z0NEW WILL BE UPDATED (CALLING AIRSEA).

REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(INOUT) :: GFL  !! WAVE SPECTRUM.

REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(INOUT) :: U10NEW !! NEW WIND SPEED IN M/S.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(IN) :: THWNEW !! WIND DIRECTION IN RADIANS IN OCEANOGRAPHIC NOTATION.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(IN) :: ROAIRN  !! AIR DENSITY IN KG/M3.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(IN) :: WSTAR !! FREE CONVECTION VELOCITY SCALE (M/S)
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(IN) :: CICVR  !! SEA ICE COVER.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(IN) :: FMEAN  !! MEAN FREQUENCY.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(OUT) :: FMEANWS  !! MEAN FREQUENCY OF THE WINDSEA.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL,NANG), INTENT(IN) :: FLM  !! SPECTAL DENSITY MINIMUM VALUE
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(INOUT) :: USNEW !! FRICTION VELOCITY IN M/S.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(INOUT) :: TAUW  !! WAVE STRESS IN (M/S)**2
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(INOUT) :: TAUWDIR  !! WAVE STRESS DIRECTION.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(INOUT) :: Z0NEW  !! ROUGHNESS LENGTH IN M.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(INOUT) :: Z0B  !! BACKGROUND ROUGHNESS LENGTH.

REAL(KIND=JWRB), DIMENSION(KIJS:KIJL), INTENT(OUT) :: PHIWA  !! ENERGY FLUX FROM WIND INTO WAVES.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL,NANG,NFRE), INTENT(OUT) :: FLD !! DIAGONAL MATRIX OF FUNCTIONAL DERIVATIVE.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL,NANG,NFRE), INTENT(OUT) :: SL !! TOTAL SOURCE FUNCTION ARRAY.
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL,NANG,NFRE), INTENT(OUT) :: SPOS !!  POSITIVE SINPUT ONLY.

INTEGER(KIND=JWIM), INTENT(OUT) :: MIJ(KIJS:KIJL)  !! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE.

REAL(KIND=JWRB), DIMENSION(KIJS:KIJL,NFRE), INTENT(OUT) :: RHOWGDFTH  !! WATER DENSITY * G * DF * DTHETA

REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(OUT) :: GXLLWS  !! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM.


INTEGER(KIND=JWIM) :: IUSFG, ICODE_WND
INTEGER(KIND=JWIM) :: NGST

REAL(KIND=JWRB) :: ZHOOK_HANDLE
REAL(KIND=JWRB), DIMENSION(KIJS:KIJL) :: EMEANWS, RNFAC

LOGICAL :: LLPHIWA

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',0,ZHOOK_HANDLE)

! UPDATE USNEW AND Z0NEW
IF(ICALL == 1 ) THEN
  IUSFG = 0
  IF (LWCOU) THEN
    ICODE_WND = ICODE_CPL
  ELSE
    ICODE_WND = ICODE
  ENDIF
ELSE
  IUSFG = 1
  ICODE_WND = 3
ENDIF

IF(LLNORMAGAM .AND. LLCAPCHNK ) THEN
  RNFAC(:) = 1.0_JWRB+DTHRN_A*(1.0_JWRB+TANH(U10NEW(:)-DTHRN_U))
ELSE
  RNFAC(:) = 1.0_JWRB 
ENDIF


IF(LUPDTUS) THEN
  ! increase noise level in the tail
  IF(ICALL == 1 ) GFL(KIJS:KIJL,:,NFRE) = MAX(GFL(KIJS:KIJL,:,NFRE),FLM(KIJS:KIJL,:))

  CALL AIRSEA (IJS, IJL, KIJS, KIJL, GFL, U10NEW, THWNEW, ROAIRN, TAUW, TAUWDIR, RNFAC, USNEW, Z0NEW, Z0B, ICODE_WND, IUSFG) 
ENDIF

! COMPUTE WIND INPUT
!!  FLD AND SL ARE INITIALISED IN SINPUT
IF(ICALL < NCALL ) THEN
  NGST = 1
  LLPHIWA = .FALSE.
ELSE
  NGST = 2
  LLPHIWA = .TRUE.
ENDIF

CALL SINPUT (NGST, IJS, IJL, KIJS, KIJL, GFL, THWNEW, U10NEW, USNEW, Z0NEW, ROAIRN, WSTAR, RNFAC, FLD, SL, SPOS, GXLLWS) 

! MEAN FREQUENCY CHARACTERISTIC FOR WIND SEA
CALL FEMEANWS(GFL, GXLLWS, IJS, IJL, KIJS, KIJL, EMEANWS, FMEANWS)

! COMPUTE LAST FREQUENCY INDEX OF PROGNOSTIC PART OF SPECTRUM.
CALL FRCUTINDEX(KIJS, KIJL, FMEAN, FMEANWS, USNEW, CICVR, MIJ, RHOWGDFTH)

! UPDATE TAUW
CALL STRESSO (IJS, IJL, KIJS, KIJL, GFL, SL, SPOS, THWNEW, USNEW, Z0NEW, ROAIRN, RNFAC, TAUW, TAUWDIR, PHIWA, LLPHIWA) 

! INSURE MINIMUN ENERGY AT THE LAST FREQUENCY BIN.
IF(ICALL < NCALL ) CALL FLMINTAIL(IJS, IJL, KIJS, KIJL, U10NEW, THWNEW, USNEW, FMEANWS, GFL) 

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',1,ZHOOK_HANDLE)

END SUBROUTINE SINFLX
