SUBROUTINE TAUT_Z0(IJS, IJL, IUSFG, XLEV, FL1, UTOP, ROAIRN, TAUW, USTAR, Z0)

! ----------------------------------------------------------------------

!**** *TAUT_Z0* - COMPUTATION OF TOTAL STRESS AND ROUGHNESS LENGTH SCALE.


!**   INTERFACE.
!     ----------

!       *CALL* *TAUT_Z0(IJS, IJL, IUSFG, XLEV, UTOP, ROAIRN, TAUW, XLEV, USTAR, Z0)
!          *IJS* - INDEX OF FIRST GRIDPOINT
!          *IJL* - INDEX OF LAST GRIDPOINT
!          *IUSFG* - IF = 1 THEN USE THE FRICTION VELOCITY (US) AS FIRST GUESS in TAUT_Z0
!                         0 DO NOT USE THE FIELD USTAR
!          *XLEV* - WIND SPEED REFERENCE LEVEL (m)
!          *FL1*  - SPECTRA
!          *UTOP* - WIND SPEED AT REFERENCE LEVEL XLEV
!          *ROAIRN* - AIR DENSITY IN KG/M3
!          *TAUW* - WAVE STRESS.
!          *USTAR*- FRICTION VELOCITY
!          *Z0*   - ROUGHNESS LENGTH 



!     METHOD.
!     -------

!       A STEADY STATE WIND PROFILE IS ASSUMED.
!       THE WIND STRESS IS COMPUTED USING THE ROUGHNESSLENGTH

!                  Z1=Z0/SQRT(1-TAUW/TAU)

!       WHERE Z0 IS THE CHARNOCK RELATION , TAUW IS THE WAVE-
!       INDUCED STRESS AND TAU IS THE TOTAL STRESS.
!       WE SEARCH FOR STEADY-STATE SOLUTIONS FOR WHICH TAUW/TAU < 1.

!     EXTERNALS.
!     ----------

!       NONE.

!     REFERENCE.
!     ----------

!       FOR QUASILINEAR EFFECT SEE PETER A.E.M. JANSSEN,1990.

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : ALPHA, XKAPPA, RNU, RNUM, LLCAPCHNK, LLGCBZ0
      USE YOWFRED  , ONLY : FR       ,DELTH
      USE YOWPARAM , ONLY : NANG     ,NFRE
      USE YOWPCONS , ONLY : G, ZPI, ROWATER, EPSUS, ACD, BCD, CDMAX
      USE YOWTABL  , ONLY : JPLEVT   ,EPS1 
      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

      USE YOWTEST  , ONLY : IU06

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "stress_gc.intfb.h"

      INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL, IUSFG
      REAL(KIND=JWRB), INTENT(IN) :: XLEV
      REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(IN) :: FL1
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: UTOP, ROAIRN, TAUW
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: USTAR
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(OUT) :: Z0

      INTEGER(KIND=JWIM), PARAMETER :: NITER=15

      REAL(KIND=JWRB), PARAMETER :: TWOXMP1=3.0_JWRB

      INTEGER(KIND=JWIM) :: IJ, K, ITER

      REAL(KIND=JWRB), PARAMETER :: PCE = 0.005_JWRB
      REAL(KIND=JWRB), PARAMETER :: Z0MIN = 0.00000001_JWRB
      REAL(KIND=JWRB) :: CHNKMIN
      REAL(KIND=JWRB) :: CHARNOCK_MIN
      REAL(KIND=JWRB) :: US2TOTAUW
      REAL(KIND=JWRB) :: XLOGXL, XKUTOP, XOLOGZ0
      REAL(KIND=JWRB) :: USTOLD, USTNEW, TAUOLD, TAUNEW, X, F, DELF
      REAL(KIND=JWRB) :: USTM1, Z0TOT, Z0CH, Z0VIS, ZZ, GM1
      REAL(KIND=JWRB) :: CONST0, TAUV, DEL
      REAL(KIND=JWRB) :: RWINV, RNUEFF
      REAL(KIND=JWRB) :: ZHOOK_HANDLE
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: ALPHAOG, XMIN
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: EPSIL
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: ALPHAP, W1, XMSS, TAUUNR, ZB

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('TAUT_Z0',0,ZHOOK_HANDLE)

      XLOGXL=LOG(XLEV)
      US2TOTAUW=1.0_JWRB+EPS1
      GM1= 1.0_JWRB/G

IF (LLGCBZ0) THEN

!     COMPUTE THE PHILLIPS PARAMETER
      CONST0 = DELTH*ZPI**4*FR(NFRE)**5*GM1**2
      ALPHAP(:) = 0.0_JWRB
      DO K = 1, NANG
        DO IJ = IJS, IJL
          ALPHAP(IJ) = ALPHAP(IJ) + CONST0*FL1(IJ,K,NFRE)
        ENDDO
      ENDDO

      RWINV = 1.0_JWRB/ROWATER
      DO IJ = IJS, IJL
        EPSIL(IJ) = ROAIRN(IJ)*RWINV
      ENDDO
      RNUEFF = 0.04_JWRB*RNU

      DO IJ = IJS, IJL
        W1(IJ) = 0.85_JWRB - 0.05_JWRB*( TANH(10.0_JWRB*(UTOP(IJ)-5.0_JWRB)) + 1.0_JWRB )
      ENDDO

      DO IJ = IJS, IJL
        XKUTOP = XKAPPA*UTOP(IJ)
        USTOLD = (1-IUSFG)*UTOP(IJ)*0.035847_JWRB + IUSFG*USTAR(IJ)
        TAUOLD = MAX(USTOLD**2,TAUW(IJ)*US2TOTAUW)
        USTAR(IJ) = SQRT(TAUOLD)

        DO ITER=1,NITER
          USTAR(IJ) = USTOLD
!         Z0 IS DERIVED FROM THE NEUTRAL LOG PROFILE: UTOP = (USTAR/XKAPPA)*LOG((XLEV+Z0)/Z0)
          Z0(IJ)  = MAX(XLEV/(EXP(XKUTOP/USTAR(IJ))-1.0_JWRB),Z0MIN)
!         GRAVITY CAPILLARY CONTRIBUTION:
          CALL STRESS_GC(USTAR(IJ),EPSIL(IJ),Z0(IJ),ALPHAP(IJ),XMSS(IJ),TAUUNR(IJ))
          ZB(IJ)  = Z0(IJ)*SQRT(TAUUNR(IJ)/TAUOLD) 
!         TOTAL kinematic STRESS:
          ! Viscous kinematic stress nu_air * dU/dz at z=0 of the neutral log profile reduced by factor 25 (0.04)
          TAUV = RNUEFF*USTAR(IJ)/(XKAPPA*Z0(IJ))
          TAUNEW  = US2TOTAUW*TAUW(IJ) + TAUV + TAUUNR(IJ)
          USTNEW  = SQRT(TAUNEW)
          USTAR(IJ) = W1(IJ)*USTOLD+(1.0_JWRB-W1(IJ))*USTNEW 
!         CONVERGENCE ?
          DEL = USTAR(IJ)-USTOLD
          IF (ABS(DEL).LT.PCE*USTAR(IJ)) EXIT 
          TAUOLD = USTAR(IJ)**2
          USTOLD = USTAR(IJ)
        ENDDO
        Z0(IJ)  = MAX(XLEV/(EXP(XKUTOP/USTAR(IJ))-1.0_JWRB),Z0MIN)

!!!!debile
       write(IU06,*) 'debile ',USTAR(IJ), Z0(IJ), ZB(IJ), G*Z0(IJ)/USTAR(IJ)**2, ITER 
       write(IU06,*) 'stress ',TAUNEW, TAUW(IJ) , TAUV , TAUUNR(IJ)
       CALL FLUSH(IU06)
       

      ENDDO

ELSE

      IF(LLCAPCHNK) THEN
        DO IJ=IJS,IJL
          CHARNOCK_MIN = CHNKMIN(UTOP(IJ))
          XMIN(IJ) = 0.15_JWRB*(ALPHA-CHARNOCK_MIN)
          ALPHAOG(IJ) = CHARNOCK_MIN*GM1
        ENDDO
      ELSE
        DO IJ=IJS,IJL
          XMIN(IJ)= 0.0_JWRB
          ALPHAOG(IJ)= ALPHA*GM1
        ENDDO
      ENDIF

      DO IJ=IJS,IJL
        XKUTOP = XKAPPA*UTOP(IJ)
        USTOLD = (1-IUSFG)*UTOP(IJ)*SQRT(MIN(ACD+BCD*UTOP(IJ),CDMAX)) + IUSFG*USTAR(IJ)
        TAUOLD = MAX(USTOLD**2,TAUW(IJ)*US2TOTAUW)
        USTAR(IJ) = SQRT(TAUOLD)
        USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS) 

        DO ITER=1,NITER
          X = MAX(TAUW(IJ)/TAUOLD,XMIN(IJ))
          Z0CH = ALPHAOG(IJ)*TAUOLD/SQRT(1.0_JWRB-X)
          Z0VIS = RNUM*USTM1
          Z0TOT = Z0CH+Z0VIS

          XOLOGZ0= 1.0_JWRB/(XLOGXL-LOG(Z0TOT))
          F = USTAR(IJ)-XKUTOP*XOLOGZ0
          ZZ = USTM1*(Z0CH*(2.0_JWRB-TWOXMP1*X)/(1.0_JWRB-X)-Z0VIS)/Z0TOT
          DELF= 1.0_JWRB-XKUTOP*XOLOGZ0**2*ZZ

          USTAR(IJ) = USTAR(IJ)-F/DELF
          TAUNEW = MAX(USTAR(IJ)**2,TAUW(IJ)*US2TOTAUW)
          USTAR(IJ) = SQRT(TAUNEW)
          IF(TAUNEW.EQ.TAUOLD) EXIT
          USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS) 
          TAUOLD = TAUNEW
        ENDDO

        Z0(IJ)=Z0CH

      ENDDO

ENDIF

IF (LHOOK) CALL DR_HOOK('TAUT_Z0',1,ZHOOK_HANDLE)

END SUBROUTINE TAUT_Z0
