SUBROUTINE TAUT_Z0(IJS, IJL, IUSFG, FL1, UTOP, UDIR, ROAIRN, TAUW, TAUWDIR, RNFAC, USTAR, Z0, Z0B)

! ----------------------------------------------------------------------

!**** *TAUT_Z0* - COMPUTATION OF TOTAL STRESS AND ROUGHNESS LENGTH SCALE.


!**   INTERFACE.
!     ----------

!       *CALL* *TAUT_Z0(IJS, IJL, IUSFG, FL1, UTOP, UDIR, ROAIRN, TAUW, TAUWDIR, RNFAC, USTAR, Z0, Z0B)
!          *IJS* - INDEX OF FIRST GRIDPOINT
!          *IJL* - INDEX OF LAST GRIDPOINT
!          *IUSFG* - IF = 1 THEN USE THE FRICTION VELOCITY (US) AS FIRST GUESS in TAUT_Z0
!                         0 DO NOT USE THE FIELD USTAR
!          *FL1*  - SPECTRA
!          *UTOP* - WIND SPEED AT REFERENCE LEVEL XNLEV
!          *UDIR* - WIND SPEED DIRECTION AT REFERENCE LEVEL XNLEV
!          *ROAIRN* - AIR DENSITY IN KG/M3
!          *TAUW* - WAVE STRESS.
!          *TAUWDIR* - WAVE STRESS DIRECTION.
!          *RNFAC* - WIND DEPENDENT FACTOR USED IN THE GROWTH RENORMALISATION.
!          *USTAR*- FRICTION VELOCITY
!          *Z0*   - ROUGHNESS LENGTH
!          *Z0B*  - BACKGROUND ROUGHNESS LENGTH

!     METHOD.
!     -------

!       A STEADY STATE WIND PROFILE IS ASSUMED.
!       THE WIND STRESS IS COMPUTED USING THE ROUGHNESS LENGTH

!                  Z1=Z0/SQRT(1-TAUW/TAU)

!       WHERE Z0 IS THE CHARNOCK RELATION , TAUW IS THE WAVE-
!       INDUCED STRESS AND TAU IS THE TOTAL STRESS.
!       WE SEARCH FOR STEADY-STATE SOLUTIONS FOR WHICH TAUW/TAU < 1.

!       IT WAS EXTENDED TO INCLUDE THE GRAVITY-CAPILLARY MODEL FOR THE CALCULATION
!       OF THE BACKGROUND ROUGHNESS.

!     EXTERNALS.
!     ----------

!       NONE.

!     REFERENCE.
!     ----------

!       FOR QUASILINEAR EFFECT SEE PETER A.E.M. JANSSEN,1990.

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : LLCAPCHNK, LLGCBZ0
      USE YOWPARAM , ONLY : NANG     ,NFRE
      USE YOWPCONS , ONLY : G, GM1, EPSUS, EPSMIN, ACD, BCD, CDMAX
      USE YOWPHYS  , ONLY : XKAPPA, XNLEV, RNU, RNUM, ALPHA, ALPHAMIN, ALPHAMAX
      USE YOWTABL  , ONLY : EPS1 
      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "halphap.intfb.h"
#include "stress_gc.intfb.h"

      INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL, IUSFG
      REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(IN) :: FL1
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: UTOP, UDIR, ROAIRN, TAUW, TAUWDIR, RNFAC
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: USTAR
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(OUT) :: Z0, Z0B

      INTEGER(KIND=JWIM), PARAMETER :: NITER=25

      REAL(KIND=JWRB), PARAMETER :: TWOXMP1=3.0_JWRB

      INTEGER(KIND=JWIM) :: IJ, ITER
      INTEGER(KIND=JWIM) :: IFRPH

      REAL(KIND=JWRB), PARAMETER :: PCE_GC = 0.001_JWRB
      ! Cd and Z0 from Hersbach 2010, ECMWF Tech Memo (without the viscous part)
!     CD = ACDLIN + BCDLIN*SQRT(PCHAR) * U10
      REAL(KIND=JWRB), PARAMETER :: ACDLIN=0.0008_JWRB
      REAL(KIND=JWRB), PARAMETER :: BCDLIN=0.00047_JWRB
      REAL(KIND=JWRB) :: ALPHAGM1
      REAL(KIND=JWRB) :: DIRSPRD_GC


      REAL(KIND=JWRB), PARAMETER :: Z0MIN = 0.000001_JWRB
      REAL(KIND=JWRB) :: CHNKMIN
      REAL(KIND=JWRB) :: CHARNOCK_MIN
      REAL(KIND=JWRB) :: COSDIFF 
      REAL(KIND=JWRB) :: ZCHAR
      REAL(KIND=JWRB) :: US2TOTAUW, USMAX
      REAL(KIND=JWRB) :: XLOGXL, XKUTOP, XOLOGZ0
      REAL(KIND=JWRB) :: USTOLD, USTNEW, TAUOLD, TAUNEW, X, F, DELF, CDFG
      REAL(KIND=JWRB) :: USTM1, Z0TOT, Z0CH, Z0VIS, HZ0VISO1MX, ZZ
      REAL(KIND=JWRB) :: CONST, TAUV, DEL
      REAL(KIND=JWRB) :: RNUEFF, RNUKAPPAM1
      REAL(KIND=JWRB) :: ZHOOK_HANDLE
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: ALPHAOG, XMIN
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: W1
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: TAUWACT, TAUWEFF 
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: ANG_GC, ALPHAP, HALP, TAUUNR

      LOGICAL,  DIMENSION(IJS:IJL) :: LLCOSDIFF

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('TAUT_Z0',0,ZHOOK_HANDLE)

      XLOGXL=LOG(XNLEV)
      US2TOTAUW=1.0_JWRB+EPS1

!     ONLY take the contribution of TAUW that is in the wind direction
      DO IJ = IJS, IJL
        COSDIFF = COS(UDIR(IJ)-TAUWDIR(IJ))
        TAUWACT(IJ) = MAX(TAUW(IJ)*COSDIFF, EPSMIN )
        LLCOSDIFF(IJ) = (COSDIFF > 0.9_JWRB )
      ENDDO

!  USING THE CG MODEL:
IF (LLGCBZ0) THEN

      DO IJ = IJS, IJL
        USMAX = MAX(-0.21339_JWRB + 0.093698_JWRB*UTOP(IJ) -0.0020944_JWRB*UTOP(IJ)**2 + 5.5091E-5_JWRB*UTOP(IJ)**3, 0.03_JWRB)
        TAUWEFF(IJ) = MIN(TAUWACT(IJ)*US2TOTAUW, USMAX**2 )
      ENDDO

!     COMPUTE THE PHILLIPS PARAMETER
      CALL HALPHAP(IJS, IJL, USTAR, UDIR, FL1, HALP) 

      RNUEFF = 0.04_JWRB*RNU

      RNUKAPPAM1 = RNUEFF/XKAPPA

      IF (IUSFG == 0 ) THEN
        ALPHAGM1 = ALPHA*GM1
        DO IJ = IJS, IJL
          IF ( LLCOSDIFF(IJ) ) THEN
            X = MIN(TAUWACT(IJ)/MAX(USTAR(IJ),EPSUS)**2,0.99_JWRB)
            ZCHAR = MIN( ALPHAGM1 * USTAR(IJ)**2 / SQRT(1.0_JWRB - X), 0.05_JWRB*EXP(-0.05_JWRB*(UTOP(IJ)-35._JWRB)) )
            ZCHAR = MIN(ZCHAR,ALPHAMAX)
            CDFG = ACDLIN + BCDLIN*SQRT(ZCHAR) * UTOP(IJ)
          ELSE
            CDFG = MAX(MIN(0.00008_JWRB*UTOP(IJ), 0.001_JWRB+0.0018_JWRB*EXP(-0.05_JWRB*(UTOP(IJ)-35._JWRB))),0.0005_JWRB)
          ENDIF
          USTAR(IJ) = UTOP(IJ)*SQRT(CDFG)
        ENDDO
      ENDIF

      DO IJ=IJS,IJL
        ALPHAOG(IJ) = ALPHAMIN*GM1
      ENDDO

      DO IJ = IJS, IJL
        W1(IJ) = 0.85_JWRB - 0.05_JWRB*( TANH(10.0_JWRB*(UTOP(IJ)-5.0_JWRB)) + 1.0_JWRB )
      ENDDO

      DO IJ = IJS, IJL
        XKUTOP = XKAPPA*UTOP(IJ)
        USTOLD = USTAR(IJ)
        TAUOLD = USTOLD**2

        DO ITER=1,NITER
!         Z0 IS DERIVED FROM THE NEUTRAL LOG PROFILE: UTOP = (USTAR/XKAPPA)*LOG((XNLEV+Z0)/Z0)
          Z0(IJ) = MAX(XNLEV/(EXP(XKUTOP/USTOLD)-1.0_JWRB), Z0MIN)
          ! Viscous kinematic stress nu_air * dU/dz at z=0 of the neutral log profile reduced by factor 25 (0.04)
          TAUV = RNUKAPPAM1*USTOLD/Z0(IJ)

          ANG_GC(IJ) = DIRSPRD_GC(USTAR(IJ))

          CALL STRESS_GC(ANG_GC(IJ), USTAR(IJ), Z0(IJ), Z0MIN, HALP(IJ), RNFAC(IJ), TAUUNR(IJ))
!         TOTAL kinematic STRESS:
          TAUNEW = TAUWEFF(IJ) + TAUV + TAUUNR(IJ)
          USTNEW = SQRT(TAUNEW)
          USTAR(IJ) = W1(IJ)*USTOLD+(1.0_JWRB-W1(IJ))*USTNEW

!         CONVERGENCE ?
          DEL = USTAR(IJ)-USTOLD
          IF (ABS(DEL).LT.PCE_GC*USTAR(IJ)) EXIT 
          TAUOLD = USTAR(IJ)**2
          USTOLD = USTAR(IJ)
        ENDDO
        Z0(IJ) = MAX(XNLEV/(EXP(XKUTOP/USTAR(IJ))-1.0_JWRB), Z0MIN)
        Z0B(IJ) = Z0(IJ)*SQRT(TAUUNR(IJ)/TAUOLD)

!       Refine solution
        X = TAUWEFF(IJ)/TAUOLD
        IF (X < 0.99_JWRB) THEN
          USTOLD = USTAR(IJ)
          TAUOLD = MAX(USTOLD**2,TAUWEFF(IJ))

          DO ITER=1,NITER
            X = MIN(TAUWEFF(IJ)/TAUOLD,0.99_JWRB)
            USTM1 = 1.0_JWRB/MAX(USTOLD,EPSUS) 

            CALL STRESS_GC(ANG_GC(IJ), USTOLD, Z0(IJ), Z0MIN, HALP(IJ), RNFAC(IJ), TAUUNR(IJ))

            Z0B(IJ) = Z0(IJ)*SQRT(TAUUNR(IJ)/TAUOLD)
            Z0VIS = RNUM*USTM1
            HZ0VISO1MX = 0.5_JWRB*Z0VIS/(1.0_JWRB-X)
            Z0(IJ) = HZ0VISO1MX+SQRT(HZ0VISO1MX**2+Z0B(IJ)**2/(1.0_JWRB-X))

            XOLOGZ0= 1.0_JWRB/(XLOGXL-LOG(Z0(IJ)))
            F = USTOLD-XKUTOP*XOLOGZ0
            ZZ = 2.0_JWRB*USTM1*(3.0_JWRB*Z0B(IJ)**2+0.5_JWRB*Z0VIS*Z0(IJ)-Z0(IJ)**2) &
&                / (2.0_JWRB*Z0(IJ)**2*(1.0_JWRB-X)-Z0VIS*Z0(IJ))

            DELF= 1.0_JWRB-XKUTOP*XOLOGZ0**2*ZZ
            IF(DELF /= 0.0_JWRB) USTAR(IJ) = USTOLD-F/DELF

!           CONVERGENCE ?
            DEL = USTAR(IJ)-USTOLD

            IF (ABS(DEL).LT.PCE_GC*USTAR(IJ)) EXIT 
            USTOLD = USTAR(IJ)
            TAUOLD = MAX(USTOLD**2,TAUWEFF(IJ))
            Z0(IJ) = MAX(XNLEV/EXP(XKUTOP/USTAR(IJ)), Z0MIN)
          ENDDO

        ENDIF

      ENDDO


ELSE

      DO IJ = IJS, IJL
        TAUWEFF(IJ) = TAUWACT(IJ)*US2TOTAUW
      ENDDO

      IF(LLCAPCHNK) THEN
        DO IJ=IJS,IJL
          CHARNOCK_MIN = CHNKMIN(UTOP(IJ))
          XMIN(IJ) = 0.15_JWRB*(ALPHA-CHARNOCK_MIN)
          ALPHAOG(IJ) = CHARNOCK_MIN*GM1
        ENDDO
      ELSE
        DO IJ=IJS,IJL
          XMIN(IJ)= 0.0_JWRB
          ALPHAOG(IJ)= ALPHA*GM1
        ENDDO
      ENDIF

      DO IJ=IJS,IJL
        XKUTOP = XKAPPA*UTOP(IJ)
        USTOLD = (1-IUSFG)*UTOP(IJ)*SQRT(MIN(ACD+BCD*UTOP(IJ),CDMAX)) + IUSFG*USTAR(IJ)
        TAUOLD = MAX(USTOLD**2,TAUWEFF(IJ))
        USTAR(IJ) = SQRT(TAUOLD)
        USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS) 

        DO ITER=1,NITER
          X = MAX(TAUWACT(IJ)/TAUOLD,XMIN(IJ))
          Z0CH = ALPHAOG(IJ)*TAUOLD/SQRT(1.0_JWRB-X)
          Z0VIS = RNUM*USTM1
          Z0TOT = Z0CH+Z0VIS

          XOLOGZ0= 1.0_JWRB/(XLOGXL-LOG(Z0TOT))
          F = USTAR(IJ)-XKUTOP*XOLOGZ0
          ZZ = USTM1*(Z0CH*(2.0_JWRB-TWOXMP1*X)/(1.0_JWRB-X)-Z0VIS)/Z0TOT
          DELF= 1.0_JWRB-XKUTOP*XOLOGZ0**2*ZZ

          IF(DELF /= 0.0_JWRB) USTAR(IJ) = USTAR(IJ)-F/DELF
          TAUNEW = MAX(USTAR(IJ)**2,TAUWEFF(IJ))
          USTAR(IJ) = SQRT(TAUNEW)
          IF(TAUNEW.EQ.TAUOLD) EXIT
          USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS)
          TAUOLD = TAUNEW
        ENDDO

        Z0(IJ) = Z0CH
        Z0B(IJ) = ALPHAOG(IJ)*TAUOLD

      ENDDO

ENDIF

IF (LHOOK) CALL DR_HOOK('TAUT_Z0',1,ZHOOK_HANDLE)

END SUBROUTINE TAUT_Z0
