      SUBROUTINE TAUT_Z0(IJS, IJL, XLEV, UTOP, TAUW, USTAR, Z0)

! ----------------------------------------------------------------------

!**** *TAUT_Z0* - COMPUTATION OF TOTAL STRESS AND ROUGHNESS LENGTH SCALE.


!**   INTERFACE.
!     ----------

!       *CALL* *TAUT_Z0(IJS, IJL, XLEV, UTOP, TAUW, XLEV, USTAR, Z0)
!          *IJS* - INDEX OF FIRST GRIDPOINT
!          *IJL* - INDEX OF LAST GRIDPOINT
!          *XLEV* - WIND SPEED REFERENCE LEVEL (m)
!          *UTOP* - WIND SPEED AT REFERENCE LEVEL XLEV
!          *TAUW* - WAVE STRESS.
!          *USTAR*- FRICTION VELOCITY
!          *Z0*   - ROUGHNESS LENGTH 



!     METHOD.
!     -------

!       A STEADY STATE WIND PROFILE IS ASSUMED.
!       THE WIND STRESS IS COMPUTED USING THE ROUGHNESSLENGTH

!                  Z1=Z0/SQRT(1-TAUW/TAU)

!       WHERE Z0 IS THE CHARNOCK RELATION , TAUW IS THE WAVE-
!       INDUCED STRESS AND TAU IS THE TOTAL STRESS.
!       WE SEARCH FOR STEADY-STATE SOLUTIONS FOR WHICH TAUW/TAU < 1.

!     EXTERNALS.
!     ----------

!       NONE.

!     REFERENCE.
!     ----------

!       FOR QUASILINEAR EFFECT SEE PETER A.E.M. JANSSEN,1990.

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : ALPHA    ,XKAPPA  ,RNUM     ,LLCAPCHNK
      USE YOWPCONS , ONLY : G        ,EPSUS   ,ACD      ,BCD
      USE YOWTABL  , ONLY : JPLEVT   ,EPS1 
      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK, JPHOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE

      INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL
      REAL(KIND=JWRB), INTENT(IN) :: XLEV
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: UTOP, TAUW
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(OUT) :: USTAR, Z0

      INTEGER(KIND=JWIM), PARAMETER :: NITER=15

      REAL(KIND=JWRB), PARAMETER :: TWOXMP1=3.0_JWRB
      REAL(KIND=JWRB), PARAMETER :: CDMAX=0.0045_JWRB

      INTEGER(KIND=JWIM) :: IJ, ITER

      REAL(KIND=JWRB) :: CHNKMIN
      REAL(KIND=JWRB) :: CHARNOCK_MIN
      REAL(KIND=JWRB) :: US2TOTAUW
      REAL(KIND=JWRB) :: XLOGXL, XKUTOP, XOLOGZ0
      REAL(KIND=JWRB) :: USTOLD, TAUOLD, TAUNEW, X, F, DELF
      REAL(KIND=JWRB) :: USTM1, Z0TOT, Z0CH, Z0VIS, ZZ, GM1
      REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: ALPHAOG, XMIN

! ----------------------------------------------------------------------

      IF (LHOOK) CALL DR_HOOK('TAUT_Z0',0,ZHOOK_HANDLE)

      XLOGXL=LOG(XLEV)
      US2TOTAUW=1.0_JWRB+EPS1
      GM1= 1.0_JWRB/G

      IF(LLCAPCHNK) THEN
        DO IJ=IJS,IJL
          CHARNOCK_MIN = CHNKMIN(UTOP(IJ))
          XMIN(IJ) = 0.15_JWRB*(ALPHA-CHARNOCK_MIN)
          ALPHAOG(IJ) = CHARNOCK_MIN*GM1
        ENDDO
      ELSE
        DO IJ=IJS,IJL
          XMIN(IJ)= 0.0_JWRB
          ALPHAOG(IJ)= ALPHA*GM1
        ENDDO
      ENDIF

      DO IJ=IJS,IJL
        XKUTOP = XKAPPA*UTOP(IJ)
        USTOLD = UTOP(IJ)*SQRT(MIN(ACD+BCD*UTOP(IJ),CDMAX))
        TAUOLD = MAX(USTOLD**2,TAUW(IJ)*US2TOTAUW)
        USTAR(IJ) = SQRT(TAUOLD)
        USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS) 

        DO ITER=1,NITER
          X = MAX(TAUW(IJ)/TAUOLD,XMIN(IJ))
          Z0CH = ALPHAOG(IJ)*TAUOLD/SQRT(1.0_JWRB-X)
          Z0VIS = RNUM*USTM1
          Z0TOT = Z0CH+Z0VIS

          XOLOGZ0= 1.0_JWRB/(XLOGXL-LOG(Z0TOT))
          F = USTAR(IJ)-XKUTOP*XOLOGZ0
          ZZ = USTM1*(Z0CH*(2.0_JWRB-TWOXMP1*X)/(1.0_JWRB-X)-Z0VIS)/Z0TOT
          DELF= 1.0_JWRB-XKUTOP*XOLOGZ0**2*ZZ

          USTAR(IJ) = USTAR(IJ)-F/DELF
          TAUNEW = MAX(USTAR(IJ)**2,TAUW(IJ)*US2TOTAUW)
          USTAR(IJ) = SQRT(TAUNEW)
          IF(TAUNEW.EQ.TAUOLD) EXIT
          USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS) 
          TAUOLD = TAUNEW
        ENDDO

        Z0(IJ)=Z0CH

      ENDDO

      IF (LHOOK) CALL DR_HOOK('TAUT_Z0',1,ZHOOK_HANDLE)

      END SUBROUTINE TAUT_Z0
