SUBROUTINE TAUT_Z0(IJS, IJL, IUSFG, FL1, FMEAN, FMEANWS, UTOP, ROAIRN, TAUW, USTAR, Z0)

! ----------------------------------------------------------------------

!**** *TAUT_Z0* - COMPUTATION OF TOTAL STRESS AND ROUGHNESS LENGTH SCALE.


!**   INTERFACE.
!     ----------

!       *CALL* *TAUT_Z0(IJS, IJL, IUSFG, FL1, UTOP, ROAIRN, TAUW, USTAR, Z0)
!          *IJS* - INDEX OF FIRST GRIDPOINT
!          *IJL* - INDEX OF LAST GRIDPOINT
!          *IUSFG* - IF = 1 THEN USE THE FRICTION VELOCITY (US) AS FIRST GUESS in TAUT_Z0
!                         0 DO NOT USE THE FIELD USTAR
!          *FL1*  - SPECTRA
!          *UTOP* - WIND SPEED AT REFERENCE LEVEL XNLEV
!          *ROAIRN* - AIR DENSITY IN KG/M3
!          *TAUW* - WAVE STRESS.
!          *USTAR*- FRICTION VELOCITY
!          *Z0*   - ROUGHNESS LENGTH 

!     METHOD.
!     -------

!       A STEADY STATE WIND PROFILE IS ASSUMED.
!       THE WIND STRESS IS COMPUTED USING THE ROUGHNESS LENGTH

!                  Z1=Z0/SQRT(1-TAUW/TAU)

!       WHERE Z0 IS THE CHARNOCK RELATION , TAUW IS THE WAVE-
!       INDUCED STRESS AND TAU IS THE TOTAL STRESS.
!       WE SEARCH FOR STEADY-STATE SOLUTIONS FOR WHICH TAUW/TAU < 1.

!       IT WAS EXTENDED TO INCLUDE THE GRAVITY-CAPILLARY MODEL FOR THE CALCULATION
!       OF THE BACKGROUND ROUGHNESS.

!     EXTERNALS.
!     ----------

!       NONE.

!     REFERENCE.
!     ----------

!       FOR QUASILINEAR EFFECT SEE PETER A.E.M. JANSSEN,1990.

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : LLCAPCHNK, LLGCBZ0
      USE YOWPARAM , ONLY : NANG     ,NFRE
      USE YOWPCONS , ONLY : G, GM1, EPSUS, ACD, BCD, CDMAX
      USE YOWPHYS  , ONLY : XKAPPA, XNLEV, RNU, RNUM, ALPHA, &
&                           ANG_GC_A, ANG_GC_B, ANG_GC_C, ANG_GC_D, ANG_GC_E
      USE YOWTABL  , ONLY : EPS1 
      USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE
#include "halphap.intfb.h"
#include "stress_gc.intfb.h"

      INTEGER(KIND=JWIM), INTENT(IN) :: IJS, IJL, IUSFG
      REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(IN) :: FL1
      REAL(KIND=JWRB), DIMENSION (IJS:IJL), INTENT (IN) :: FMEAN, FMEANWS
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(IN) :: UTOP, ROAIRN, TAUW
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(INOUT) :: USTAR
      REAL(KIND=JWRB), DIMENSION(IJS:IJL), INTENT(OUT) :: Z0

      INTEGER(KIND=JWIM), PARAMETER :: NITER=20

      REAL(KIND=JWRB), PARAMETER :: TWOXMP1=3.0_JWRB

      INTEGER(KIND=JWIM) :: IJ, ITER
      INTEGER(KIND=JWIM) :: IFRPH

      REAL(KIND=JWRB), PARAMETER :: PCE_GC = 0.001_JWRB
      REAL(KIND=JWRB), PARAMETER :: Z0MIN = 0.000001_JWRB
      REAL(KIND=JWRB) :: CHNKMIN
      REAL(KIND=JWRB) :: CHARNOCK_MIN
      REAL(KIND=JWRB) :: US2TOTAUW, USMAX, ANG_GC
      REAL(KIND=JWRB) :: XLOGXL, XKUTOP, XOLOGZ0
      REAL(KIND=JWRB) :: USTOLD, USTNEW, TAUOLD, TAUNEW, X, F, DELF, CDFG
      REAL(KIND=JWRB) :: USTM1, Z0TOT, Z0CH, Z0VIS, ZZ
      REAL(KIND=JWRB) :: CONST, TAUV, DEL
      REAL(KIND=JWRB) :: RNUEFF, RNUKAPPAM1
      REAL(KIND=JWRB) :: ZHOOK_HANDLE
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: ALPHAOG, XMIN
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: W1
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: TAUWEFF 
      REAL(KIND=JWRB), DIMENSION(IJS:IJL) :: ALPHAP, HALP, TAUUNR, ZB

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('TAUT_Z0',0,ZHOOK_HANDLE)

      XLOGXL=LOG(XNLEV)
      US2TOTAUW=1.0_JWRB+EPS1

!  USING THE CG MODEL:
IF (LLGCBZ0) THEN

      DO IJ = IJS, IJL
        USMAX = MAX(-0.21339_JWRB + 0.093698_JWRB*UTOP(IJ) -0.0020944_JWRB*UTOP(IJ)**2 + 5.5091E-5_JWRB*UTOP(IJ)**3, 0.03_JWRB)
        TAUWEFF(IJ) = MIN(TAUW(IJ)*US2TOTAUW, USMAX**2 )
      ENDDO

!     COMPUTE THE PHILLIPS PARAMETER
      CALL HALPHAP(IJS, IJL, FL1, HALP) 

      RNUEFF = 0.04_JWRB*RNU
      RNUKAPPAM1 = RNUEFF/XKAPPA

      DO IJ = IJS, IJL
        W1(IJ) = 0.85_JWRB - 0.05_JWRB*( TANH(10.0_JWRB*(UTOP(IJ)-5.0_JWRB)) + 1.0_JWRB )
      ENDDO

      DO IJ = IJS, IJL
        CDFG = MAX(MIN(0.00008_JWRB*UTOP(IJ), 0.001_JWRB+0.0018_JWRB*EXP(-0.05_JWRB*(UTOP(IJ)-35._JWRB))),0.0005_JWRB)
        USTOLD = (1-IUSFG)*UTOP(IJ)*SQRT(CDFG) + IUSFG*USTAR(IJ)
        TAUOLD = MAX(USTOLD**2,TAUWEFF(IJ))
        USTAR(IJ) = SQRT(TAUOLD)
      ENDDO

!! needed if static test
!      IF(LLCAPCHNK) THEN
!        DO IJ=IJS,IJL
!          CHARNOCK_MIN = CHNKMIN(UTOP(IJ))
!          ALPHAOG(IJ) = CHARNOCK_MIN*GM1
!        ENDDO
!      ELSE
!        DO IJ=IJS,IJL
!          ALPHAOG(IJ)= ALPHA*GM1
!        ENDDO
!      ENDIF
!!

      DO IJ = IJS, IJL
        XKUTOP = XKAPPA*UTOP(IJ)
        USTOLD = USTAR(IJ)
        TAUOLD = USTOLD**2
        ANG_GC = MAX(ANG_GC_A+ANG_GC_B*TANH(ANG_GC_C*(UTOP(IJ)-ANG_GC_D)),ANG_GC_E)

        DO ITER=1,NITER
!         Z0 IS DERIVED FROM THE NEUTRAL LOG PROFILE: UTOP = (USTAR/XKAPPA)*LOG((XNLEV+Z0)/Z0)
          Z0(IJ) = MAX(XNLEV/(EXP(XKUTOP/USTOLD)-1.0_JWRB),Z0MIN)
          ! Viscous kinematic stress nu_air * dU/dz at z=0 of the neutral log profile reduced by factor 25 (0.04)
          TAUV = RNUKAPPAM1*USTOLD/Z0(IJ)

          CALL STRESS_GC(ANG_GC, USTAR(IJ), Z0(IJ), HALP(IJ), TAUUNR(IJ))
!!! static test (need the lines above for the calculation of ALPHAOG
!!          ZB(IJ) = ALPHAOG(IJ)*TAUOLD
!!          TAUUNR(IJ) = (ZB(IJ)/Z0(IJ))**2*TAUOLD
!!
!! ZB is diagnostic, so could be removed when not needed
!!          ZB(IJ) = MAX(Z0(IJ)*SQRT(TAUUNR(IJ)/TAUOLD), Z0MIN)

!!!
         write(*,*) 'debile ', iter, Z0(IJ)*SQRT(TAUUNR(IJ)/TAUOLD), Z0(IJ), TAUUNR(IJ)

!         TOTAL kinematic STRESS:
          TAUNEW = TAUWEFF(IJ) + TAUV + TAUUNR(IJ)
          USTNEW = SQRT(TAUNEW)
          USTAR(IJ) = W1(IJ)*USTOLD+(1.0_JWRB-W1(IJ))*USTNEW

!         CONVERGENCE ?
          DEL = USTAR(IJ)-USTOLD
          IF (ABS(DEL).LT.PCE_GC*USTAR(IJ)) EXIT 
          TAUOLD = USTAR(IJ)**2
          USTOLD = USTAR(IJ)
        ENDDO
        Z0(IJ)  = MAX(XNLEV/(EXP(XKUTOP/USTAR(IJ))-1.0_JWRB), Z0MIN)

      ENDDO

ELSE

      DO IJ = IJS, IJL
        TAUWEFF(IJ) = TAUW(IJ)*US2TOTAUW
      ENDDO

      IF(LLCAPCHNK) THEN
        DO IJ=IJS,IJL
          CHARNOCK_MIN = CHNKMIN(UTOP(IJ))
          XMIN(IJ) = 0.15_JWRB*(ALPHA-CHARNOCK_MIN)
          ALPHAOG(IJ) = CHARNOCK_MIN*GM1
        ENDDO
      ELSE
        DO IJ=IJS,IJL
          XMIN(IJ)= 0.0_JWRB
          ALPHAOG(IJ)= ALPHA*GM1
        ENDDO
      ENDIF

      DO IJ=IJS,IJL
        XKUTOP = XKAPPA*UTOP(IJ)
        USTOLD = (1-IUSFG)*UTOP(IJ)*SQRT(MIN(ACD+BCD*UTOP(IJ),CDMAX)) + IUSFG*USTAR(IJ)
        TAUOLD = MAX(USTOLD**2,TAUWEFF(IJ))
        USTAR(IJ) = SQRT(TAUOLD)
        USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS) 

        DO ITER=1,NITER
          X = MAX(TAUW(IJ)/TAUOLD,XMIN(IJ))
          Z0CH = ALPHAOG(IJ)*TAUOLD/SQRT(1.0_JWRB-X)
          Z0VIS = RNUM*USTM1
          Z0TOT = Z0CH+Z0VIS

          XOLOGZ0= 1.0_JWRB/(XLOGXL-LOG(Z0TOT))
          F = USTAR(IJ)-XKUTOP*XOLOGZ0
          ZZ = USTM1*(Z0CH*(2.0_JWRB-TWOXMP1*X)/(1.0_JWRB-X)-Z0VIS)/Z0TOT
          DELF= 1.0_JWRB-XKUTOP*XOLOGZ0**2*ZZ

          USTAR(IJ) = USTAR(IJ)-F/DELF
          TAUNEW = MAX(USTAR(IJ)**2,TAUWEFF(IJ))
          USTAR(IJ) = SQRT(TAUNEW)
          IF(TAUNEW.EQ.TAUOLD) EXIT
          USTM1 = 1.0_JWRB/MAX(USTAR(IJ),EPSUS) 
          TAUOLD = TAUNEW
        ENDDO

        Z0(IJ)=Z0CH

      ENDDO

ENDIF

IF (LHOOK) CALL DR_HOOK('TAUT_Z0',1,ZHOOK_HANDLE)

END SUBROUTINE TAUT_Z0
