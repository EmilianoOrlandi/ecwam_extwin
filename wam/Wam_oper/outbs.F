      SUBROUTINE OUTBS (FL3, FL, IJS, IJL, IG, IGL, IU25, IU26,
     &                  THWOLD,USOLD,U10NEW,THWNEW,USNEW,TAUW,
     &                  NSTART,NEND)
! ----------------------------------------------------------------------

!**** *OUTBS* - MODEL OUTPUT FROM BLOCK TO FILE, PRINTER AND COMMON.

!     H. GUNTHER         GKSS/ECMWF         JUNE 1990
!     J. BIDLOT          ECMWF   FEBRUARY 1996 MESSAGE PASSING

!*    PURPOSE.
!     --------

!       CONTROL OUTPUT OF WAVE AND WIND FIELDS.


!**   INTERFACE.
!     ----------
!   *CALL* *OUTBS (FL3, FL, IJS, IJL, IG, IGL, IU25, IU26,
!    1                  THWOLD,USOLD,U10NEW,THWNEW,USNEW,TAUW)
!    2                  NSTART,NEND)*
!      *FL3*    - SPECTRUM.
!      *FL*     - WORKING ARRAY FOR SWELL OUTPUT.
!      *IJS*    - INDEX OF FIRST GRIDPOINT
!      *IJL*    - INDEX OF LAST GRIDPOINT
!      *IG*     - BLOCK NUMBER
!      *IGL*    - TOTAL NUMBER OF BLOCKS.
!      *IU25*   - OUTPUT UNIT FOR SPECTRA.
!      *IU26*   - OUTPUT UNIT FOR SEA AND SWELL SPECTRA.
!      *THWOLD*    INTERMEDIATE STORAGE OF ANGLE (RADIANS) OF
!      *USOLD*     INTERMEDIATE STORAGE OF MODULUS OF FRICTION
!                  VELOCITY.
!      *U10NEW*    NEW WIND SPEED IN M/S.
!                  WIND VELOCITY.
!      *THWNEW*    WIND DIRECTION IN RADIANS IN OCEANOGRAPHIC
!                  NOTATION (POINTING ANGLE OF WIND VECTOR,
!                  CLOCKWISE FROM NORTH).
!      *USNEW*     NEW FRICTION VELOCITY IN M/S.
!      *TAUW*      WAVE STRESS IN (M/S)**2
!      *NSTART*    INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN
!      *NEND*      INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN


!     EXTERNALS.
!     ----------

!       *FEMEAN*    - COMPUTATION OF MEAN FREQUENCY AT EACH GRID POINT.
!       *OUTERS*    - OUTPUT OF SATELLITE COLOCATION SPECTRA.
!       *OUTGRID*   - SAVE BLOCKED PARAMETERS INTO GRID ARRAYS.
!       *OUTSPP*    - OUTPUT OF SPECTRA AT SELECTED POINTS.
!       *SEMEAN*    - COMPUTATION OF TOTAL ENERGY AT EACH GRID POINT.
!       *SEPWISW*   - SEPARATION OF WINDSEA AND SWELL.
!       *STHQ*      - COMPUTATION OF MEAN WAVE DIRECTION AT EACH
!                     GRID POINT.

!     METHOD.
!     -------

!       NONE.

!     REFERENCE.
!     ----------

!       NONE.

! ----------------------------------------------------------------------

      USE YOWINTP  , ONLY : WHGTTG   ,WDIRTG   ,WPKFTG   ,WMNFTG   ,
     &            USTARG   ,UDIRG    ,TAUWG    ,CDG      ,SMEANG   ,
     &            U10G
      USE YOWINTS  , ONLY : WHGTSG   ,WDIRSG   ,WMNFSG   ,WHGTWG   ,
     &            WDIRWG   ,WMNFWG
      USE YOWMPP   , ONLY : NPROC    ,NINF     ,NSUP
      USE YOWPARAM , ONLY : NGX      ,NGY      ,NANG     ,NFRE     ,
     &            NIBLO    ,NBLO
      USE YOWSTAT  , ONLY : CDATEA   ,CDTPRO   ,CDTINTS  ,CDTSPT   ,
     &            CDTSPS
      USE YOWSWEL  , ONLY : ESWELL   ,FSWELL   ,THSWELL  ,ESEA     ,
     &            FSEA     ,THWISEA
      USE YOWTEST  , ONLY : IU06     ,ITEST    ,ITESTB

! ----------------------------------------------------------------------

!     ALLOCATABLE ARRAYS THAT ARE PASSED AS SUBROUTINE ARGUMENTS 

      INTEGER,DIMENSION(NPROC) :: NSTART,NEND
      REAL,DIMENSION(NINF-1,NSUP,NANG,NFRE) :: FL3, FL
      REAL,DIMENSION(NINF:NSUP,NBLO) :: THWOLD,USOLD,TAUW
      REAL,DIMENSION(NINF:NSUP) :: U10NEW,THWNEW,USNEW

! ----------------------------------------------------------------------
!     0. ALLOCATE ARRAYS
!        ---------------
        IF(.NOT.ALLOCATED(WHGTTG)) ALLOCATE(WHGTTG(NGX,NGY))
        IF(.NOT.ALLOCATED(WDIRTG)) ALLOCATE(WDIRTG(NGX,NGY))
        IF(.NOT.ALLOCATED(WPKFTG)) ALLOCATE(WPKFTG(NGX,NGY))
        IF(.NOT.ALLOCATED(WMNFTG)) ALLOCATE(WMNFTG(NGX,NGY))
        IF(.NOT.ALLOCATED(USTARG)) ALLOCATE(USTARG(NGX,NGY))
        IF(.NOT.ALLOCATED(UDIRG)) ALLOCATE(UDIRG(NGX,NGY))
        IF(.NOT.ALLOCATED(TAUWG)) ALLOCATE(TAUWG(NGX,NGY))
        IF(.NOT.ALLOCATED(CDG)) ALLOCATE(CDG(NGX,NGY))
        IF(.NOT.ALLOCATED(SMEANG)) ALLOCATE(SMEANG(NGX,NGY))
        IF(.NOT.ALLOCATED(U10G)) ALLOCATE(U10G(NGX,NGY))

        IF(.NOT.ALLOCATED(WHGTSG)) ALLOCATE(WHGTSG(NGX,NGY))
        IF(.NOT.ALLOCATED(WDIRSG)) ALLOCATE(WDIRSG(NGX,NGY))
        IF(.NOT.ALLOCATED(WMNFSG)) ALLOCATE(WMNFSG(NGX,NGY))
        IF(.NOT.ALLOCATED(WHGTWG)) ALLOCATE(WHGTWG(NGX,NGY))
        IF(.NOT.ALLOCATED(WDIRWG)) ALLOCATE(WDIRWG(NGX,NGY))
        IF(.NOT.ALLOCATED(WMNFWG)) ALLOCATE(WMNFWG(NGX,NGY))

!*    1. COMPUTE MEAN PARAMETERS.
!        ------------------------

      CALL SEMEAN (FL3, IJS, IJL)
      CALL FEMEAN (FL3, IJS, IJL)
      CALL STHQ   (FL3, IJS, IJL)
      CALL MEANSQS (FL3, IJS, IJL, USNEW)
      IF (ITEST.GE.3) THEN
        IF (ITESTB.GE.IG) THEN
          WRITE(IU06,*) '      SUB. OUTBS: INTEGRATED',
     &     ' PARAMETERS COMPUTED FOR OUTPUT'
        ENDIF
      ENDIF

!*    2. WINDSEA SWELL SEPARATION.
!        -------------------------

      IF (CDTINTS.EQ.CDTPRO .OR. CDTSPS.EQ.CDTPRO) THEN

        IF(.NOT.ALLOCATED(ESWELL)) ALLOCATE(ESWELL(NIBLO))
        IF(.NOT.ALLOCATED(FSWELL)) ALLOCATE(FSWELL(NIBLO))
        IF(.NOT.ALLOCATED(THSWELL)) ALLOCATE(THSWELL(NIBLO))
        IF(.NOT.ALLOCATED(ESEA)) ALLOCATE(ESEA(NIBLO))
        IF(.NOT.ALLOCATED(FSEA)) ALLOCATE(FSEA(NIBLO))
        IF(.NOT.ALLOCATED(THWISEA)) ALLOCATE(THWISEA(NIBLO))

        CALL SEPWISW (FL3, FL, IJS, IJL, IG,THWOLD,USOLD,USNEW)
        IF (ITEST.GE.3) THEN
          IF (ITESTB.GE.IG) THEN
            WRITE(IU06,*) '      SUB. OUTBS: SWELL /SEA',
     &       ' SEPARATION DONE'
          ENDIF
        ENDIF
      ENDIF

!*    3. OUTPUT OF SPECTRA AT SELECTED GRID POINTS.
!        ------------------------------------------

      IF (CDTSPT.EQ.CDTPRO .OR. CDTSPS.EQ.CDTPRO) THEN
        CALL OUTSPP (FL3, FL, IG, IU25, IU26,NSTART,NEND,
     &   U10NEW,THWNEW,USNEW)
        IF (ITEST.GE.3) THEN
          IF (ITESTB.GE.IG) THEN
            WRITE(IU06,*) '      SUB. OUTBS: OUTPUT OF SPECTRA',
     &       ' AT SELECTED POINTS DONE'
          ENDIF
        ENDIF
      ENDIF

!*    4. OUTPUT OF INTEGRATED PARAMETERS.
!        --------------------------------

      CALL OUTGRID (FL3, IJS, IJL, IG,U10NEW,THWNEW,USNEW,TAUW,
     &              NSTART,NEND)
      IF (ITEST.GE.3) THEN
        IF (ITESTB.GE.IG) THEN
          WRITE(IU06,*) '      SUB. OUTBS: OUTPUT OF ',
     &     'INTEGRATED PARAMETERS DONE'
          CALL FLUSH(IU06)
        ENDIF
      ENDIF

      IF (CDTINTS.EQ.CDTPRO .OR. CDTSPS.EQ.CDTPRO) THEN
        DEALLOCATE(ESWELL)
        DEALLOCATE(FSWELL)
        DEALLOCATE(THSWELL)
        DEALLOCATE(ESEA)
        DEALLOCATE(FSEA)
        DEALLOCATE(THWISEA)
        IF (ITEST.GE.3) THEN
          IF (ITESTB.GE.IG) THEN
            WRITE(IU06,*) '      SUB. OUTBS: DEALLOCATE OF ARRAYS FOR',
     &       ' INTEGRATED PARAMETERS DONE'
            CALL FLUSH(IU06)
          ENDIF
        ENDIF
      ENDIF

!*    5. OUTPUT OF SPECTRA FOR SATELLITE COLLOCATION.
!        --------------------------------------------

      IF (CDTPRO.NE.CDATEA) THEN
        CALL OUTERS (FL3, IG, IGL, CDTPRO,
     &   NSTART,NEND,U10NEW,THWNEW,USNEW)
        IF (ITEST.GE.3) THEN
          IF (ITESTB.GE.IG) THEN
            WRITE(IU06,*) '      SUB. OUTBS: OUTPUT OF SPECTRA',
     &       ' FOR SATELLITE COLOCATION DONE FOR ', CDTPRO
          ENDIF
        ENDIF
      ENDIF

      RETURN
      END SUBROUTINE OUTBS
