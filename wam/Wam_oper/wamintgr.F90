SUBROUTINE WAMINTGR (IJS, IJL, BLK2GLO,                           &
 &                   CDTPRA, CDATE, CDATEWH, CDTIMP, CDTIMPNEXT,  &
 &                   WVENVI, WVPRPT, FF_NOW, FF_NEXT, INTFLDS,    &
 &                   WAM2NEMO, MIJ, FL1, XLLWS)


! ----------------------------------------------------------------------

!**** *WAMINTGR* - 3-G WAM MODEL - TIME INTEGRATION OF WAVE FIELDS.

!*    PURPOSE.
!     --------

!       COMPUTATION OF THE 2-D FREQUENCY-DIRECTION WAVE SPECTRUM AT ALL
!       GRID POINTS FOR A GIVEN INITIAL SPECTRUM AND FORCING SURFACE
!       STRESS FIELD.

!     REFERENCE.
!     ----------

!         IFS DOCUMENTATION, part VII 

! -------------------------------------------------------------------

USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU
USE YOWDRVTYPE  , ONLY : WVGRIDGLO, ENVIRONMENT, FREQUENCY, FORCING_FIELDS,  &
 &                       INTGT_PARAM_FIELDS, WAVE2OCEAN

USE YOWCOUP  , ONLY : LWNEMOCOU, NEMONTAU
USE YOWGRID  , ONLY : NPROMA_WAM, NBLOC
USE YOWPARAM , ONLY : NIBLO, NANG, NFRE
USE YOWPCONS , ONLY : EPSMIN
USE YOWSTAT  , ONLY : CDTPRO, IDELPRO, IDELT, IDELWI, LLSOURCE
USE YOWWIND  , ONLY : CDAWIFL, CDATEWO, CDATEFL

USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
      
! ----------------------------------------------------------------------

IMPLICIT NONE
#include "implsch.intfb.h"
#include "incdate.intfb.h"
#include "newwind.intfb.h"
#include "propag_wam.intfb.h"

INTEGER(KIND=JWIM), INTENT(IN) :: IJS  ! INDEX OF FIRST GRIDPOINT
INTEGER(KIND=JWIM), INTENT(IN) :: IJL  ! INDEX OF LAST GRIDPOINT
TYPE(WVGRIDGLO), DIMENSION(NIBLO), INTENT(IN) :: BLK2GLO  ! BLOCK TO GRID TRANSFORMATION
CHARACTER(LEN=14), INTENT(IN) :: CDTPRA  ! DATE FOR CALL PROPAGATION
CHARACTER(LEN=14), INTENT(INOUT) :: CDATE  ! CURRENT DATE
CHARACTER(LEN=14), INTENT(INOUT) :: CDATEWH ! DATE OF THE NEXT FORCING FIELDS
CHARACTER(LEN=14), INTENT(INOUT) :: CDTIMP  ! START DATE OF SOURCE FUNCTION INTEGRATION
CHARACTER(LEN=14), INTENT(INOUT) :: CDTIMPNEXT  ! NEXT START DATE OF SOURCE FUNCTION INTEGRATION
TYPE(ENVIRONMENT), DIMENSION(IJS:IJL), INTENT(INOUT) :: WVENVI !  WAVE ENVIRONMENT FIELDS
TYPE(FREQUENCY), DIMENSION(IJS:IJL,NFRE), INTENT(INOUT) :: WVPRPT  ! WAVE PROPERTIES FIELDS
TYPE(FORCING_FIELDS), DIMENSION(IJS:IJL), INTENT(INOUT) :: FF_NOW  ! FORCING FIELDS AT CURRENT TIME
TYPE(FORCING_FIELDS), DIMENSION(IJS:IJL), INTENT(IN) :: FF_NEXT  !  DATA STRUCTURE WITH THE NEXT FORCING FIELDS
TYPE(INTGT_PARAM_FIELDS), DIMENSION(IJS:IJL), INTENT(INOUT) :: INTFLDS  ! INTEGRATED/DERIVED PARAMETERS
TYPE(WAVE2OCEAN), DIMENSION(IJS:IJL), INTENT(INOUT) :: WAM2NEMO  ! WAVE FIELDS PASSED TO NEMO
INTEGER(KIND=JWIM), DIMENSION(IJS:IJL), INTENT(INOUT) :: MIJ  ! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(INOUT) :: FL1  ! SPECTRUM(INPUT AND OUTPUT)
REAL(KIND=JWRB), DIMENSION(IJS:IJL,NANG,NFRE), INTENT(INOUT) :: XLLWS ! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM


INTEGER(KIND=JWIM) :: IJ, K, M
INTEGER(KIND=JWIM) :: JKGLO, KIJS, KIJL, NPROMA
INTEGER(KIND=JWIM) :: IDELWH

REAL(KIND=JWRB) :: ZHOOK_HANDLE


LOGICAL, SAVE :: LLNEWFILE

DATA LLNEWFILE / .FALSE. /

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('WAMINTGR',0,ZHOOK_HANDLE)

!*     PROPAGATION TIME
!      ----------------

IF (CDATE == CDTPRA) THEN
  CALL PROPAG_WAM(IJS, IJL, BLK2GLO, WVENVI, WVPRPT, FL1)
  CDATE=CDTPRO
ENDIF


!* RETRIEVING NEW FORCING FIELDS IF NEEDED.
!  ----------------------------------------
CALL NEWWIND(IJS, IJL, CDTIMP, CDATEWH,               &
 &           LLNEWFILE,                               &
 &           WVPRPT, FF_NOW, FF_NEXT)

! IT IS TIME TO INTEGRATE THE SOURCE TERMS
! ----------------------------------------
IF (CDATE >= CDTIMPNEXT) THEN
! COMPUTE UPDATE DUE TO SOURCE TERMS
  CALL GSTATS(1431,0)
  IF (LLSOURCE) THEN
    NPROMA=NPROMA_WAM
!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) PRIVATE(JKGLO,KIJS,KIJL)
    DO JKGLO=IJS,IJL,NPROMA
      KIJS=JKGLO
      KIJL=MIN(KIJS+NPROMA-1,IJL)
      CALL IMPLSCH (KIJS, KIJL, FL1(KIJS:KIJL,:,:),       &
 &                  WVPRPT(KIJS:KIJL,:),                  &
 &                  WVENVI(KIJS), FF_NOW(KIJS),           &
 &                  INTFLDS(KIJS), WAM2NEMO(KIJS),        &
 &                  MIJ(KIJS), XLLWS(KIJS:KIJL,:,:) )

    ENDDO
!$OMP END PARALLEL DO

    IF (LWNEMOCOU) NEMONTAU = NEMONTAU + 1

  ELSE
!   NO SOURCE TERM CONTRIBUTION
!$OMP      PARALLEL DO SCHEDULE(STATIC) PRIVATE(JKGLO,KIJS,KIJL,IJ,K,M)  
    DO JKGLO=IJS,IJL,NPROMA
      KIJS=JKGLO
      KIJL=MIN(KIJS+NPROMA-1,IJL)
      DO IJ=KIJS,KIJL
        MIJ(IJ) = NFRE
        DO K=1,NANG
          DO M=1,NFRE
            FL1(IJ,K,M) = MAX(FL1(IJ,K,M),EPSMIN)
            XLLWS(IJ,K,M) = 0.0_JWRB
          ENDDO
        ENDDO
      ENDDO
    ENDDO
!$OMP      END PARALLEL DO
  ENDIF
  CALL GSTATS(1431,1)


!*       UPDATE FORCING FIELDS TIME COUNTER 
!        ----------------------------------
  IF (LLNEWFILE) THEN
    LLNEWFILE = .FALSE.
    IDELWH = MAX(IDELWI,IDELPRO)
    CALL INCDATE(CDAWIFL,IDELWH)
    CALL INCDATE(CDATEFL,IDELWH)
  ENDIF

  CDATEWO=CDATEWH
  CDTIMP=CDTIMPNEXT
  CALL INCDATE(CDTIMPNEXT,IDELT)   

ENDIF

IF (LHOOK) CALL DR_HOOK('WAMINTGR',1,ZHOOK_HANDLE)

END SUBROUTINE WAMINTGR
